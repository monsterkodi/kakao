 ###
000   000   0000000   00000000   000      0000000  
000 0 000  000   000  000   000  000      000   000
000000000  000   000  0000000    000      000   000
000   000  000   000  000   000  000      000   000
00     00   0000000   000   000  0000000  0000000  
###

use ../kxk ▪ $ drag stopEvent prefs post fade randRange randInt randIntRange

use ◆ gee tweaky tube matrix

{cos, sin, PI} = Math
TAU = 2*PI

COL_SHADOW  = [0 0 0 0.1]
COL_BG      = [0.15 0.15 0.15 1]
COL_GRID    = [0 0 0 0.5]
COL_TUBE    = [0.5 0.5 0.5 1]
COL_PLANT   = [0 0.5 0 1]
COL_LEAF    = [0 0.5 0 1]
COL_EGG     = [1 1 1 1]
COL_CRITTER = [0.5 0.5 1 1]
COL_STARVE  = [0.25 0.25 0.25 1]
COL_DEAD    = [0.1 0.1 0.1 1]
COL_EGG_DOT = [0 0 0 0.5]
COL_GRINDER = [1 0 0 1]

threshMold  = p n m -> parseInt(p/m) != parseInt(n/m)

sort        = a b => [min(a,b), max(a,b)]
posInCircle = p cx cy cr => (cx - cr) <= p[0] <= (cx + cr) and (cy - cr) <= p[1] <= (cy + cr)
posInRect   = p x1 y1 x2 y2 => [x1,x2] = sort(x1,x2); [y1,y2] = sort(y1,y2); x1 <= p[0] <= x2 and y1 <= p[1] <= y2
posInPipeH  = p x y w r => posInCircle(p, x, y, r) or posInCircle(p, x+w, y, r) or posInRect(p, x, y-r, x+w, y+r)
valuePipeH  = p x y w r => if posInPipeH(p, x, y, w, r) ➜ (p[0] - x) / w 

function world extends matrix

    @: ->
        
        @main =$ 'main'
        @pause = false
        
        @speed = 10
                
        @tweaky = new tweaky @main
        
        @speedGauge = x:0 y:0 s:0.5 sx:0.3125 sw:1 sr:0.125
        
        super()
        
        @main.focus()
                                
        @g = new gee @main
        @h = new gee @main
        
        @g.camScale = 0.08
        @g.camPosX  = 1 / @g.camScale
        @g.camPosY  = 1 / @g.camScale
        @g.updateCamera()

        @h.camCenter  = 'topleft'
        @h.clearColor = [0 0 0 0]
        @h.canvas.style.pointerEvents = 'none'
        @hudGridY     = 10
        @h.camScale   = 2 / @hudGridY
        @h.camPosX    = -0.5
        @h.camPosY    =  0.5
        @h.updateCamera()
                    
        window.addEventListener 'wheel'       @onWheel
        @main.addEventListener  'mousemove'   @onMouseMove
        @main.addEventListener  'contextmenu' @onContextMenu
        
        s = 40.96/2048
        @tubeUV = [
            [ s*1 s*2 s*2 s*3 ] # right / left
            [ s*2 s*0 s*3 s*1 ] # right down / up left
            [ s*2 s*1 s*3 s*2 ] # down / up
            [ s*2 s*2 s*3 s*3 ] # down left / right up
            [ s*0 s*2 s*1 s*3 ] # down right / left up
            [ s*0 s*0 s*1 s*1 ] # up right / left down
            ]
            
        uv = u uu v vv -> [ s*u, s*v, s*uu, s*vv ]

        @ringUV = []
        for ri in 0..8
            @ringUV.push uv ri*4 (ri+1)*4 8 12
        
        @tubeSquareUV = uv 0   3 0  3
        @quadUV       = uv 37 39 9 11 
        @circleUV     = uv 36 40 8 12 
        @circleTopUV  = uv 36 40 8 10 
        @pieUV = [
            uv 36 38  8 10 
            uv 38 40  8 10 
            uv 38 40 10 12 
            uv 36 38 10 12 
        ]
        
        @eggUV     = @circleUV
        
        @mouse = pos: [0 0]
        
        @drag = new drag 
            target:  @g.canvas
            onStart: @onDragStart
            onMove:  @onDragMove
            onStop:  @onDragStop
            cursor: 'pointer'
            
    # 000   000  000   000  00000000  00000000  000      
    # 000 0 000  000   000  000       000       000      
    # 000000000  000000000  0000000   0000000   000      
    # 000   000  000   000  000       000       000      
    # 00     00  000   000  00000000  00000000  0000000  
    
    onWheel: event =>

        if event.ctrlKey or event.metaKey
            
            @g.camScale -= event.deltaY/(event.metaKey ? 20000 : 4000)
            @g.camScale = clamp 0.01 0.2 @g.camScale
            
        else
            
            @g.camPosX += event.deltaX / (4000 * @g.camScale) 
            @g.camPosY -= event.deltaY / (4000 * @g.camScale) 
            
        @g.camPosX = clamp 0, @ws, @g.camPosX
        @g.camPosY = clamp 0, @ws, @g.camPosY
        
        @g.updateCamera()
        
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
        
    onMouseMove: event =>
        
        winPos = @eventPos event
        
        @mouse = 
            grid: @win2Grid winPos
            pos:  @win2Pos winPos
            win:  winPos
        
    win2Pos: winPos => @g.win2Pos winPos
            
    win2Grid: winPos =>
        
        [x, y] = @win2Pos winPos
        
        x = clamp 0 @ws-1, Math.round x
        y = clamp 0 @ws-1, Math.round y
        
        [x,y]
    
    eventPos: event => x:event.clientX - @g.br.left, y:event.clientY - @g.br.top
    mouseInWorld: => @isInWorld @mouse.pos
        
    # 0000000    00000000    0000000    0000000   
    # 000   000  000   000  000   000  000        
    # 000   000  0000000    000000000  000  0000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000   0000000   
        
    onDragStart: drag event =>
        
        hp = @h.win2Pos drag.pos
        
        if posInCircle hp, @speedGauge.x, @speedGauge.y, @speedGauge.s/2
            @togglePause()
            ⮐ 
            
        if v = valuePipeH hp, @speedGauge.sx, @speedGauge.y, @speedGauge.sw, @speedGauge.sr
            @speed = fade 1 100 v
            ⮐ 
        
        ⮐ if not @mouseInWorld()
        
        p  = @win2Grid drag.pos
        
        if event.button == 2
            @delAt p
            ⮐ 
        
        if event.metaKey
            @dragPath = [@win2Grid(drag.pos), @win2Grid(drag.pos)]
        else if event.shiftKey
            @addGrinder p[0], p[1]
        else
            ⮐ if @buildingAt p
            @addPlant p[0], p[1]
        
    onDragMove: drag event =>
        
        hp = @h.win2Pos drag.pos
                
        if v = valuePipeH hp, @speedGauge.sx, @speedGauge.y, @speedGauge.sw, 2.5*@speedGauge.sr
            @speed = fade 1 100 v
            ⮐ 

        p = @win2Grid drag.pos
        
        if event.button == 2
            @delAt p
            ⮐ 
            
        ⮐ if event.shiftKey
        
        if not event.metaKey
            ⮐ if @buildingAt p
            @addPlant p[0], p[1]
        
        ⮐ if not @dragPath
        
        l = @dragPath[-1]
        k = @dragPath[-2]
        
        if k[0] == l[0] == p[0] and k[1] == l[1] == p[1] and @dragPath.length > 2
            @dragPath.pop()
        else if k[0] == l[0] == p[0]
            l[1] = p[1]
        else if k[1] == l[1] == p[1]
            l[0] = p[0]
        else
            @dragPath.push p
            if l[0] != k[0] and l[1] != k[1]
                if l[0] == k[0]
                    l[1] = p[1]
                else
                    l[0] = p[0]
                    
    onDragStop: drag event =>
        
        if @dragPath
            tube.path @dragPath, @addTube
            delete @dragPath
            
    onContextMenu: (event) => stopEvent event
        
    togglePause:  => @pause = not @pause; post.emit 'pause'
    toggleValues: => 
                    
    # 0000000    00000000    0000000   000   000  
    # 000   000  000   000  000   000  000 0 000  
    # 000   000  0000000    000000000  000000000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000  00     00  
    
    drawPlant: p =>
        
        s = 0.25
        @g.addQuad p.x, p.y, s, s, COL_PLANT, @circleUV, 0 0
        
        s = 0.15
        r = 0.2
        for li in 0...p.leaves.length
            l   = p.leaves[li]
            af  = l.age/@leafMaxAge
            ls  = s*clamp 0 1 af
            col = [((af>1) ? 1 : 0), ((af>1) ? 1 : COL_LEAF[1]), COL_LEAF[2], COL_LEAF[3]]
            
            lx = p.x+cos(-li*TAU/@numLeaves+PI)*r
            ly = p.y+sin(-li*TAU/@numLeaves+PI)*r
            
            if l.ef
                lx = fade l.c.x + l.c.ox - 0.25, lx, l.ef
                ly = fade l.c.y + l.c.oy, ly, l.ef
                ls = s
                col = [1 1 0 1]
                
            @g.addQuad lx, ly, ls, ls, col, @circleUV, 0 1
    
    drawTube: x y idx =>
        
        if empty y
            t = x
            x = t.x 
            y = t.y
            idx = t.idx
        
        @g.addQuad x, y, 1 1 COL_TUBE, @tubeUV[idx], 0 1
        
    drawEgg: e =>
        
        ageFac = e.age / @eggMaxAge
        s = fade 0.1 0.3 ageFac
        a = 1
        if e.age > @eggMaxAge
            a = fade 1.0 0.0 (e.age-@eggMaxAge)/@eggFadeTime
            
        ox = e.ox ? 0
        oy = e.oy ? 0
        
        @g.addQuad e.x+ox, e.y+oy, s, s, [COL_EGG[0], COL_EGG[1], COL_EGG[2], a], @eggUV, 0 1
        
    drawGrinder: g =>
        
        @g.addQuad g.x, g.y, 3 3 COL_GRINDER, @tubeSquareUV, 0 1
        
        if g.bot.mf
            g.bot.x = fade g.bot.c.x, g.x, g.bot.mf
            g.bot.y = fade g.bot.c.y, g.y, g.bot.mf
        else if g.bot.rf
            g.bot.x = fade g.x, g.bot.s.x, g.bot.rf
            g.bot.y = fade g.y, g.bot.s.y, g.bot.rf
        
        @g.addQuad g.bot.x, g.bot.y, 1 1 COL_GRINDER, @circleTopUV, 0 1 

    drawCritter: c =>
        
        sx = sy = fade 0.2 1 c.age / @critterAdultAge
        
        rot  = 0
        rcos = 1
        rsin = 0
        
        col = COL_CRITTER
        
        if c.eat < 0
            h = clamp 0 1 -c.eat / @critterStarveTime
            col = [
                fade(col[0], COL_STARVE[0], h), 
                fade(col[1], COL_STARVE[1], h),
                fade(col[2], COL_STARVE[2], h), 1
                ]
        
        if c.df
            rot = min PI, c.df * PI
            rcos = cos rot
            rsin = sin rot
            h = clamp 0 1 c.df
            col = [
                fade(col[0], COL_DEAD[0], h), 
                fade(col[1], COL_DEAD[1], h),
                fade(col[2], COL_DEAD[2], h), 1
                ]
               
        ox = c.ox ? 0
        oy = c.oy ? 0
                
        cx = c.x + ox 
        cy = c.y + oy 

        @g.addQuad cx-rsin*0.25*sx, cy+rcos*0.25*sy, sx, sy*0.5, col, @circleTopUV, rot, 1
        
        @g.addQuad cx-rcos*(1/4 )*sx, cy-rsin*(1/4 )*sy,   0.5*sx,   0.5*sy, col, @circleUV, 0, 1    
        @g.addQuad cx+rcos*(1/12)*sx, cy+rsin*(1/12)*sy, (1/6)*sx, (1/6)*sy, col, @circleUV, 0, 1    
        @g.addQuad cx+rcos*(3/12)*sx, cy+rsin*(3/12)*sy, (1/6)*sx, (1/6)*sy, col, @circleUV, 0, 1    
        @g.addQuad cx+rcos*(5/12)*sx, cy+rsin*(5/12)*sy, (1/6)*sx, (1/6)*sy, col, @circleUV, 0, 1    
            
        thrd = 1/3
        se = 0.6
        for e in 0...c.eggs
            xo = [-thrd, 0, thrd][e]*se*sx
            yo = [0.15, 0.25, 0.15][e]*sy
            
            rxo = rcos*xo-rsin*yo
            ryo = rsin*xo+rcos*yo
            
            @g.addQuad cx+rxo, cy+ryo, [1, 1.25, 1][e]*thrd*sx*se, [1, 1.25, 1][e]*thrd*se*sy, COL_EGG_DOT, @circleUV, 0 1    
            
        if c.age > @critterAdultAge and not c.df
            f = min 1 @critterEggFactor c
            wp = @critterWombPos c
            @g.addQuad wp.x, wp.y, [1, 1.25, 1][e]*thrd*sx*se*f, [1, 1.25, 1][e]*thrd*se*sy*f, COL_EGG, @circleUV, 0 1    
            
    critterWombPos: c e=c.eggs =>
        
        cx = c.x + (c.ox ? 0)
        cy = c.y + (c.oy ? 0)
        
        xo = [-0.2, 0, 0.2][e]
        yo = [0.15, 0.25, 0.15][e]
        x:cx+xo, y:cy+yo
        
    roundedQuadRect: x0 y0 x1 y1 color layer=0 =>
        
        [x0, x1] = [min(x0, x1), max(x0, x1)]
        [y0, y1] = [min(y0, y1), max(y0, y1)]
        
        @g.addQuad x0, y0, 1 1  color, @pieUV[3], 0 layer
        @g.addQuad x1, y0, 1 1  color, @pieUV[2], 0 layer
        @g.addQuad x1, y1, 1 1  color, @pieUV[1], 0 layer
        @g.addQuad x0, y1, 1 1  color, @pieUV[0], 0 layer
        
        @g.addQuad (x0+x1)/2, (y0+y1)/2, (x1-x0)-1, (y1-y0)+1, color, @quadUV, 0 layer 
        @g.addQuad  x0,       (y0+y1)/2, 1,         (y1-y0)-1, color, @quadUV, 0 layer 
        @g.addQuad  x1,       (y0+y1)/2, 1,         (y1-y0)-1, color, @quadUV, 0 layer 
        
    gridQuadRect: x0 y0 x1 y1 color layer=0 w=0.02 =>
        
        [x0, x1] = [min(x0, x1), max(x0, x1)]
        [y0, y1] = [min(y0, y1), max(y0, y1)]
        
        sx = x1 - x0
        sy = y1 - y0
        
        for x in 0..sx
            @g.addQuad x, sx/2, w, sx, [0 0 0 0.15], @quadUV, 0 layer

        for y in 0..sy
            @g.addQuad sy/2, y, sy, w, [0 0 0 0.15], @quadUV, 0 layer 
        
    # 000000000  000   0000000  000   000  
    #    000     000  000       000  000   
    #    000     000  000       0000000    
    #    000     000  000       000  000   
    #    000     000   0000000  000   000  
    
    tick: @tickInfo ->
        
        @simulate @tickInfo
        @tweaky.update()
        
        @roundedQuadRect  0    -0.5  @ws-0.5  @ws-1,   COL_SHADOW
        @roundedQuadRect -0.25 -0.25 @ws-0.75 @ws-0.75 COL_BG
        # @gridQuadRect     0     0    @ws-1,   @ws-1,   COL_GRID
                
        if @dragPath ➜ tube.path @dragPath, @drawTube
            
        for t in @types[@TUBE]    ➜ @drawTube t
        for p in @types[@PLANT]   ➜ @drawPlant p
        for c in @types[@CRITTER] ➜ @drawCritter c
        for e in @types[@EGG]     ➜ @drawEgg e
        for g in @types[@GRINDER] ➜ @drawGrinder g
                    
        @g.draw @tickInfo.time
        
        # @h.addQuad 0 -1 1 1 COL_EGG,     @quadUV, 0 0
        # @h.addQuad 1 -1 1 1 COL_CRITTER, @quadUV, 0 0
        
        @drawSpeedGauge()
        
        @h.draw @tickInfo.time
        
        delete @oneStep
            
    drawSpeedGauge: =>
        
        x = @speedGauge.x
        y = @speedGauge.y
        s = @speedGauge.s
        sx = @speedGauge.sx
        sr = @speedGauge.sr
        sw = @speedGauge.sw
        
        bc = [0.2 0.2 0.2 1]
        
        @h.addPipe x, y, sx+sw, y, 0.25, bc
        
        @h.addPipe sx, y, sx+sw, y, sr, [0.1 0.1 0.1 1]
        
        @h.addCircle x+fade(sx, sx+sw, @speed/100), y, sr, [0.5 0.5 0.5 1]
        
        @h.addCircle x, y, s, bc
        
        wave = hz ph=0 => sin hz*ph*TAU + hz*TAU*(@speed/60)*@tickInfo.time/1000
        
        @h.addQuad x, y, s, s, [0 0 0 1], @ringUV[4], 0 1
        
        pc = @pause ? [1 0 0 1] : [0 0 0 0]
        @h.addCircle x, y, s/4, pc, 1
        ⮐ if @pause
        sz = fade s/8 s/4 @speed/100
        for ri in 0..8
            phs = (@speed/60)*(ri/16)
            @h.addQuad x+wave(1, phs)*s/4, y-wave(1, phs-0.25)*s/4, sz, sz, [ri/8 ri/8 ri/8 1], @circleUV, 0 1
        
    #  0000000  000  00     00  000   000  000       0000000   000000000  00000000  
    # 000       000  000   000  000   000  000      000   000     000     000       
    # 0000000   000  000000000  000   000  000      000000000     000     0000000   
    #      000  000  000 0 000  000   000  000      000   000     000     000       
    # 0000000   000  000   000   0000000   0000000  000   000     000     00000000  
    
    simulate: tickInfo ->
        
        ⮐ if @pause and not @oneStep
        ⮐ if isNaN tickInfo.delta
        
        # ● simulate
                
        sec = @speed * tickInfo.delta / 1000
        
        @advance sec
                        
    singleStep: => @oneStep = true; @pause = true; post.emit 'pause'
        
export world
