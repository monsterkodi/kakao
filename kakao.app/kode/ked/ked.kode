###
000   000  00000000  0000000    
000  000   000       000   000  
0000000    0000000   000   000  
000  000   000       000   000  
000   000  00000000  0000000    
###

use ◆ ttio linenr cells state
use ../kxk ▪ karg
use ../kxk/nfs

args = karg """
    ked [file]
        options                                   **
        version    log version                    = false
    """

function KED

    @: ->

        @t = new ttio
        
        @cells  = new cells @t
        @state  = new state  @cells
        @linenr = new linenr @cells @state
        
        @t.on 'key'    @onKey
        @t.on 'mouse'  @onMouse
        @t.on 'wheel'  @onWheel
        @t.on 'resize' @onResize
        @t.on 'focus'  -> 
        @t.on 'blur'   -> 
            
        if args.version ➜ log '0.0.1'; process.exit 0
                        
        if valid args.options
            log 'file(s):' args.options
            @loadFile args.options[0]
        else
            @state.init ['']
            @t.setCursor 4 0
            @onResize @t.cols() @t.rows()
        
    @run: -> new KED()
    
    loadFile: p ○=>
        
        text =○ nfs.read p
        lines = text.split /\r?\n/
        @state.init lines
        @redraw()
    
    onMouse: event col row button =>
        
        switch event
            'press'
                if @state.setCursor col - @state.s.view[0] - 5 row - @state.s.view[1] - 1
                    @redraw()
        
        # log 'mouse' event col row button
        
    moveCursor: dir steps =>
        
        if @state.moveCursor dir steps
            @redraw()
            
    setCursor: x y =>
        
        if @state.setCursor x y
            @redraw()
        
    onWheel: dir mods =>
        
        steps = switch mods
            
            'shift'          ➜ 2
            'shift+ctrl'     ➜ 4
            'alt'            ➜ 8
            'shift+alt'      ➜ 16
            'ctrl+alt'       ➜ 32
            'shift+ctrl+alt' ➜ 64
            ➜ 1
        
        switch dir
            'up'        
            'down'      
            'left'      
            'right'     ➜ ⮐ @moveCursor dir steps
    
    onKey: key => 
        
        switch key
            
            'up'        
            'down'      
            'left'      
            'right'                 ➜ ⮐ @moveCursor key
            
            'ctrl+up'               ➜ ⮐ @moveCursor 'up'    4
            'ctrl+down'             ➜ ⮐ @moveCursor 'down'  4
            'ctrl+left'             ➜ ⮐ @moveCursor 'left'  4
            'ctrl+right'            ➜ ⮐ @moveCursor 'right' 4
                   
            'ctrl+alt+up'           ➜ ⮐ @moveCursor 'up'    8
            'ctrl+alt+down'         ➜ ⮐ @moveCursor 'down'  8
            'ctrl+alt+left'         ➜ ⮐ @moveCursor 'left'  8
            'ctrl+alt+right'        ➜ ⮐ @moveCursor 'right' 8

            'shift+ctrl+alt+up'     ➜ ⮐ @moveCursor 'up'    16
            'shift+ctrl+alt+down'   ➜ ⮐ @moveCursor 'down'  16
            'shift+ctrl+alt+left'   ➜ ⮐ @moveCursor 'left'  16
            'shift+ctrl+alt+right'  ➜ ⮐ @moveCursor 'right' 16
            
            'ctrl+a'        ➜ ⮐ @setCursor 0 @state.s.cursor[1]
            'ctrl+e'        ➜ ⮐ @setCursor @state.s.lines[@state.s.cursor[1]].length @state.s.cursor[1]
            'ctrl+h'        ➜ ⮐ @setCursor 0 0
            'ctrl+j'
            'shift+ctrl+h'  ➜ ⮐ @setCursor @state.s.lines[-1].length @state.s.lines.length-1
            
            'ctrl+k'    ➜ ⮐ @t.write '\x1b[0K'
            'return'    ➜ ⮐ @t.write '\n'
            'space'     ➜ ⮐ @t.write ' '
            'delete'    ➜ ⮐ @t.write '\x1b[D\x1b[P'
            'tab'       ➜ ⮐ @t.write '    '
            'ctrl+c'    ➜ ⮐ process.exit 0
            'ctrl+q'    ➜ ⮐ process.exit 0
            
        @t.write key
        
    onResize: cols rows => @redraw()
    
    redraw: =>
        
        @t.hideCursor()
        @cells.init()
        @linenr.draw()
        @state.draw()
        @cells.render()
        @t.showCursor()
        
export KED.run

