###
    ██     ██   ███████   ███████    ████████
    ███   ███  ███   ███  ███   ███  ███     
    █████████  ███   ███  ███   ███  ███████ 
    ███ █ ███  ███   ███  ███   ███  ███     
    ███   ███   ███████   ███████    ████████

    the class stores modes per editor state
    the instances handle input and text insertion

    brckts   insert and highlight matching surround 
    salter   ascii-header         
    uniko    unicode drawing
    unype    unicode type writer
    vimple   simple vi
    record   
###

use ../../kxk ◆ kseg
use ../theme  ◆ theme
use ./mode    ◆ brckts salter unype
 
function mode

    @active: {} # maps editor state names to active mode instances
    @modes: {}  # maps mode names to mode subclasses

    @: @name -> # log "mode: #{@name}"
    
    @names: -> Object.keys mode.modes

    #  ███████  █████████   ███████   ████████   █████████
    # ███          ███     ███   ███  ███   ███     ███   
    # ███████      ███     █████████  ███████       ███   
    #      ███     ███     ███   ███  ███   ███     ███   
    # ███████      ███     ███   ███  ███   ███     ███   
    
    @start: state name ->
    
        ⮐ if @isActive state name
        
        log "mode.start #{name}"
        
        @active[state.name] ?= []
        @active[state.name].push new mode.modes[name] state
        
        log "mode.start" @active[state.name].map((m) -> m.name)
            
    @stop: state name ->
        
        log "mode.stop #{name}"
        
        m = @get state name
        m.stop() if m.stop is func
        
        @active[state.name].splice @active[state.name].indexOf(m) 1
        
        log "mode.stop" @active[state.name].map((m) -> m.name)

    @toggle: state name ->
        
        if @isActive state name
            @stop state name
        else
            @start state name
        
    #  ███████    ███████  █████████  ███  ███   ███  ████████
    # ███   ███  ███          ███     ███  ███   ███  ███     
    # █████████  ███          ███     ███   ███ ███   ███████ 
    # ███   ███  ███          ███     ███     ███     ███     
    # ███   ███   ███████     ███     ███      █      ████████
    
    @isActive: state name -> valid @get(state name)
        
    @get: state name ->
        
        for m in @active[state.name]
            ⮐ m if m.name == name

    # ███  ███   ███   ███████  ████████  ████████   █████████
    # ███  ████  ███  ███       ███       ███   ███     ███   
    # ███  ███ █ ███  ███████   ███████   ███████       ███   
    # ███  ███  ████       ███  ███       ███   ███     ███   
    # ███  ███   ███  ███████   ████████  ███   ███     ███   

    @insert: state text ->
        
        for mode in @active[state.name]
            if mode.insert is func
                text = mode.insert text
        text
        
    # ███   ███  ████████  ███   ███
    # ███  ███   ███        ███ ███ 
    # ███████    ███████     █████  
    # ███  ███   ███          ███   
    # ███   ███  ████████     ███   

    @handleKey: state key event ->
        
        for mode in @active[state.name]
            if mode.handleKey is func
                ⮐ if mode.handleKey(key event) != 'unhandled'
        'unhandled'
        
    # █████████  ███   ███  ████████  ██     ██  ████████   ███████   ███████   ███       ███████   ████████ 
    #    ███     ███   ███  ███       ███   ███  ███       ███       ███   ███  ███      ███   ███  ███   ███
    #    ███     █████████  ███████   █████████  ███████   ███       ███   ███  ███      ███   ███  ███████  
    #    ███     ███   ███  ███       ███ █ ███  ███       ███       ███   ███  ███      ███   ███  ███   ███
    #    ███     ███   ███  ████████  ███   ███  ████████   ███████   ███████   ███████   ███████   ███   ███
    
    @themeColor: state colorName ->
    
        for mode in @active[state.name]
            if mode.themeColor is func
                ⮐  mode.themeColor colorName
        
        # log "themeColor #{colorName} #{theme[colorName]}"
        theme[colorName]
            
# ███   ███  ███  ██     ██  ████████   ███      ████████
# ███   ███  ███  ███   ███  ███   ███  ███      ███     
#  ███ ███   ███  █████████  ████████   ███      ███████ 
#    ███     ███  ███ █ ███  ███        ███      ███     
#     █      ███  ███   ███  ███        ███████  ████████

function vimple extends mode
    
    @: -> super 'vimple'

# ███   ███  ███   ███  ███  ███   ███   ███████ 
# ███   ███  ████  ███  ███  ███  ███   ███   ███
# ███   ███  ███ █ ███  ███  ███████    ███   ███
# ███   ███  ███  ████  ███  ███  ███   ███   ███
#  ███████   ███   ███  ███  ███   ███   ███████ 

function uniko extends mode

    @: -> super 'uniko'
        
function record extends mode

    @: -> super 'record'
    
mode.modes = 
    brckts: brckts
    salter: salter
    unype:  unype
    uniko:  uniko
    vimple: vimple
    record: record
    
export mode
