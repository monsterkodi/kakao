
use kxk.kxk
use ked

class ScrollWheel

    vel_x:   0
    vel_y:   0
    inert_x: 0
    inert_y: 0
    damp:    0.95
    
    update: dt ->
    
        if @vel_x == 0 and @vel_y == 0 and @inert_x == 0 and @inert_y == 0
            ⮐  

        if @vel_y * @inert_y < 0
            @stop()
            
        if @vel_y == 0
            @vel_y = clamp -3 3 int(@inert_y)
            @inert_y *= @damp
            if abs(@inert_y) < 1
                @inert_y = 0

        if @vel_x == 0
            @vel_x = clamp -6 6 int(@inert_x)
            @inert_x *= @damp
            if abs(@inert_x) < 1
                @inert_x = 0
        
        mx = clamp -6 6 int(@vel_x)
        my = clamp -3 3 int(@vel_y)
        ax = abs(mx)
        ay = abs(my)
    
        dir = ''
        if
            abs(my) >= abs(mx) and my > 0 ➜ dir = 'up'   ; ax = 0
            abs(my) >= abs(mx) and my < 0 ➜ dir = 'down' ; ax = 0
            abs(mx) > abs(my)  and mx < 0 ➜ dir = 'left'
            abs(mx) > abs(my)  and mx > 0 ➜ dir = 'right'

        if dir != ''
            event = {x:ax y:ay type:"wheel" cell:mouseCell dir:dir}
            ked∙onMouse event
        
        @vel_x -= mx
        @vel_y -= my
        
        @inert_x += @vel_x
        @inert_y += @vel_y
        
        @vel_x = 0
        @vel_y = 0
        
    stop: ->
    
        @vel_x = 0
        @vel_y = 0
        @inert_x    = 0
        @inert_y    = 0
        
    impulse: x y -> 

        @vel_x = x
        @vel_y = y

setFontWidth = fw ->

    _G.fontWidth = fw
    _G.font  = love.graphics.setNewFont "fonts/Twilio.ttf" fontWidth*2
    fb1 = love.graphics.newFont "fonts/Helvetica.ttf"      fontWidth*2
    fb2 = love.graphics.newFont "fonts/Menlo.ttf"          fontWidth*2
    _G.font∙setFallbacks(fb1, fb2)
    
screenCell = x y -> [int(x/_G.screen.cw)+1 int(y/_G.screen.ch)+1]

love.load = ->

    (width height) = love.window.getDesktopDimensions()
    
    love.window.setMode width/2 height {
        highdpi: true
        usedpiscale: false
        resizable: true
        }
        
    love.window.setPosition 0 0
    
    love.graphics.setDefaultFilter "nearest" "nearest"

    setFontWidth 20
    
    _G.count = 0
    _G.mouseClick = []
    
    _G.scrollWheel = ScrollWheel()
    
    #love.window.maximize()
    love.keyboard.setKeyRepeat true
    
    _G.ked = ked()
    
    nil

love.draw = ->       

    count += 1
    (_ _ w h) = love.window.getSafeArea()

    lg = love.graphics
    
    tth = lg.newText _G.font "Xg" 
    ttw = lg.newText _G.font "W" 
    
    (tw th) = ttw∙getDimensions() ; cw = tw
    (tw th) = tth∙getDimensions() ; ch = th
    
    cols = floor w/cw
    rows = floor h/ch
    
    ked∙draw cols rows cw ch
    
    lg.setColor 0.2 0.2 0.2 
    
    # lg.print "#{cols} #{rows} #{w} #{h} #{cw} #{ch} #{love.timer.getFPS()} #{floor(scrollWheel.vel_y)} #{floor(scrollWheel.inert_y)} ◂" 16*cw 0
    
    # log "hasGlyphs", _G.font∙hasGlyphs("⮐➜▸∙◌○◇□✔✘●◆▪◼■△┬")
    
    nil
    
love.update = dt ->

    scrollWheel∙update dt
    
love.quit = ->

    ked∙quit()
    false
    
love.keypressed = key scancode isrepeat ->
    
    mods = ""
    switch key 
        "escape"    ➜ key = "esc"
        "backspace" ➜ key = "delete"
        "lshift" "rshift"
        "lctrl"  "rctrl"
        "lalt"   "ralt"
        "lgui"   "rgui"
            ⮐  
            
    lk = love.keyboard        
    
    if lk.isDown('lshift') or lk.isDown('rshift') ➜ mods &= "shift+"
    if lk.isDown('lctrl')  or lk.isDown('rctrl')  ➜ mods &= "ctrl+"
    if lk.isDown('lalt')   or lk.isDown('ralt')   ➜ mods &= "alt+"
    if lk.isDown('lgui')   or lk.isDown('rgui')   ➜ mods &= "cmd+"

    combo = mods & key
    char = switch key
        "return" ➜ "\n"
        "tab"    ➜ "\t"
        "space"  ➜ " "
        "up"
        "down"
        "right"
        "esc"
        "delete"
        "left"   ➜ ""
                 ➜ 
                    if mods.len == 0 
                        key 
                    elif mods == "shift+"
                        switch key
                            "1" ➜ "!"
                            "2" ➜ "@"
                            "3" ➜ "#"
                            "4" ➜ "$"
                            "5" ➜ "%"
                            "6" ➜ "^"
                            "7" ➜ "&"
                            "8" ➜ "*"
                            "9" ➜ "("
                            "0" ➜ ")"
                            "-" ➜ "_"
                            "=" ➜ "+"
                            "[" ➜ "{"
                            "]" ➜ "}"
                            "\\" ➜ "|"
                            ";" ➜ ":"
                            "'" ➜ '"'
                            "," ➜ "<"
                            "." ➜ ">"
                            "/" ➜ "?"
                            '`' ➜ "~"
                                ➜ string.upper key
                    else
                        ""
                        
    switch combo
        "cmd+-" ➜   setFontWidth max(14 fontWidth-1)
        "cmd+=" ➜   setFontWidth min(128 fontWidth+1)
        "cmd+0" ➜   setFontWidth()
                ➜ 
                    event = {"repeat":isrepeat combo:combo char:char}
                    # log "key", dict.str(event), ◌b, scancode
                    ked∙onKey combo event
    true
    
love.mousepressed = x y button istouch presses ->

    button = if button == 1 ➜ "left" ➜ "right"
    _G.mouseCell = screenCell x y
    
    scrollWheel∙stop()
    
    count = 1
    if _G.mouseClick[button]
        click = _G.mouseClick[button]
        if abs(x-click.x) < 10 and abs(y-click.y) < 10 
            if love.timer.getTime() - click.time < 300
                count = click.count+1
    
    _G.mouseClick[button] = {
        x:      x
        y:      y
        count:  count
        time:   love.timer.getTime()}
        
    event = {x:x y:y type:"press" button:button count:mouseClick[button].count cell:mouseCell}
    # log "EVENT" noon(event)
    ked∙onMouse event

love.mousereleased = x y button istouch presses ->

    button = if button == 1 ➜ "left" ➜ "right"
    _G.mouseCell = screenCell x y
    event = {x:x y:y type:"release" button:button count:1 cell:mouseCell}
    # log "EVENT" noon(event)
    ked∙onMouse event
    
love.touchpressed = id x y ->

    log "TOUCH DOWN" x, y
    
love.touchreleased = id x y ->

    log "TOUCH UP" x, y

love.mousemoved = x y ->

    button = ""
    _G.mouseCell = screenCell x y
    typ = "move"
    if love.mouse.isDown(1)
        button = "right"
        typ = "drag"
    event = {x:x y:y type:typ button:button cell:mouseCell}
    ked∙onMouse event
    
love.wheelmoved = x y ->

    scrollWheel∙impulse x y

# love.keyreleased = key scancode ->
# 
#     log 'keyreleased ' key, " " scancode
