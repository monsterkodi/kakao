###
0000000    000  00000000   000000000  00000000   00000000  00000000
000   000  000  000   000     000     000   000  000       000     
000   000  000  0000000       000     0000000    0000000   0000000 
000   000  000  000   000     000     000   000  000       000     
0000000    000  000   000     000     000   000  00000000  00000000
###

# import rgxs from './quicky.json' with { type: "json" }

use view.colmns.diritem
use view.menu.choices

class dirtree extends choices

    @: name features ->
        
        choices.init @ name features
                        
        # @state.syntax.setRgxs rgxs
        
        post∙on 'session.merge' @onSessionMerge
        post∙on 'file.change'   @onFileChange
        post∙on 'git.status'    @onGitStatus
        
        @frontRoundOffset = 0
        
    #  ███████   ███  █████████         ███████  █████████   ███████   █████████  ███   ███   ███████
    # ███        ███     ███           ███          ███     ███   ███     ███     ███   ███  ███     
    # ███  ████  ███     ███           ███████      ███     █████████     ███     ███   ███  ███████ 
    # ███   ███  ███     ███                ███     ███     ███   ███     ███     ███   ███       ███
    #  ███████   ███     ███           ███████      ███     ███   ███     ███      ███████   ███████ 

    tilde: item -> item.tilde = kstr.lpad(item.depth*2+1) & diritem.symbolName(item)

    onGitStatus: status ->
    
        # todo: handle added files
        
        # log 'dirtree.onGitStatus' status
    
        for item in @items
        
            if item.type == 'dir' 
            
                foundChange = false
                
                if status.dirs∙has item.path
                
                    for file in status.files
                        if file.startsWith item.path
                            item.modified = true
                            @tilde item
                            redraw = true
                            foundChange = true
                            break
                            
                if item.modified and not foundChange
                
                    item.modified = nil
                    @tilde item
                    redraw = true
                            
            else
        
                if status.changed∙has item.path
                    item.modified = true
                    @tilde item                    
                    redraw = true
                    
                if status.added∙has item.path
                    item.added = true
                    @tilde item
                    redraw = true
                    
                if item.modified and not status.changed∙has(item.path) or item.added and not status.added∙has(item.path)
                    item.modified = nil
                    @tilde item
                    redraw = true
                
        if redraw
            @set @items @currentIndex()        
            post∙emit 'redraw' 
                
    # ████████  ███  ███      ████████         ███████  ███   ███   ███████   ███   ███   ███████   ████████
    # ███       ███  ███      ███             ███       ███   ███  ███   ███  ████  ███  ███        ███     
    # ██████    ███  ███      ███████         ███       █████████  █████████  ███ █ ███  ███  ████  ███████ 
    # ███       ███  ███      ███             ███       ███   ███  ███   ███  ███  ████  ███   ███  ███     
    # ███       ███  ███████  ████████         ███████  ███   ███  ███   ███  ███   ███   ███████   ████████

    onFileChange: info ->
        
        if kstr.startWith info.path @currentRoot
            if info.change == 'rename'
                for item in @items
                    if item.path == info.path
                        ⮐  
                @setRoot @currentRoot {index:@currentIndex()}
                
            if ['remove' 'deleted']∙has info.change
                for item in @items
                    if item.path == info.path
                        @setRoot @currentRoot {index:@currentIndex()}
                        ⮐  
                    
    # 00000000    0000000    0000000   000000000  
    # 000   000  000   000  000   000     000     
    # 0000000    000   000  000   000     000     
    # 000   000  000   000  000   000     000     
    # 000   000   0000000    0000000      000     
    
    setRoot: path opt ->
        
        opt ?= {}
        
        dir = slash.untilde path
        
        items = @dirItems dir 'dirtree.setRoot'

        @currentRoot = dir
                
        ⮐  if empty items
        
        for item in items∙each()
            item.depth = 0
            @tilde item

        # items∙sort((a b) -> @weight(a) - @weight(b))
        
        @set items opt.index or 1
        
        @restoreSessionState opt
        
    # 00000000   00000000   0000000  000000000   0000000   00000000   00000000  
    # 000   000  000       000          000     000   000  000   000  000       
    # 0000000    0000000   0000000      000     000   000  0000000    0000000   
    # 000   000  000            000     000     000   000  000   000  000       
    # 000   000  00000000  0000000      000      0000000   000   000  00000000  
        
    restoreSessionState: opt ->
        
        opt ?= {}
        opt.redraw = true
        
        state = ked_session∙get @name {}

        ⮐  if empty state.open
        
        for key value in pairs state.open
            
            if kstr.startsWith key @currentRoot
                @openDir @itemForPath(key) opt
        
    #  0000000  000000000   0000000   000000000  00000000  
    # 000          000     000   000     000     000       
    # 0000000      000     000000000     000     0000000   
    #      000     000     000   000     000     000       
    # 0000000      000     000   000     000     00000000  
    
    onSessionMerge: recent -> @setState recent[@name]    
        
    setState: state ->
        
        ⮐  if empty state
        
        ked_session∙set @name state
                    
    #  0000000    0000000  000000000  000   0000000   000   000  
    # 000   000  000          000     000  000   000  0000  000  
    # 000000000  000          000     000  000   000  000 0 000  
    # 000   000  000          000     000  000   000  000  0000  
    # 000   000   0000000     000     000   0000000   000   000  
    
    emitAction: action choice event  ->
    
        if action == 'hover'
            @grabFocus()
            if (event.alt or event.cmd) and choice.type == 'file'
                post∙emit 'quicky' choice.path
            ⮐ 
            
        if action == 'cmd+delete'
            if choice.type == 'file'
                post∙emit 'file.trash' choice.path
            ⮐  
            
        switch choice.type
            
            'dir' 
                
                switch action
                    
                    'click'
                    'space'
                        
                        if action == 'click' and event.mods ➜ ⮐  post∙emit 'dircol.root' choice.path
                        
                        if not choice.open ➜ @openDir(choice) 
                        else @closeDir choice
                        ⮐ 
                        
                    'right'
                    
                        if not choice.open ➜ @openDir(choice)
                        else @selectNextKeepOffset()
                        ⮐ 

                    'left'
                    
                        if choice.open ➜ @closeDir(choice)
                        else @selectPrevKeepOffset()
                        ⮐ 
                        
                    'delete'
                    'esc'
                    
                        @selectOpenSiblingAboveOrParent() if not choice.open
                        @closeDir(choice) if choice.open
                        ⮐ 
                        
                    'doubleclick'
                    'return' ➜ ⮐  post∙emit 'dircol.root' choice.path
                
            'file'

                switch action
                    
                    'left'   ➜ ⮐  @selectPrevKeepOffset()
                    'right'  ➜ ⮐  @selectNextKeepOffset()                             
                    
                    'delete'
                    'esc'    ➜ ⮐  @selectParent()
                    
                    'drag'
                    'space'  ➜ ⮐  post∙emit 'quicky' choice.path
                    
                    'click'
                    'return' ➜ ⮐  post∙emit 'file.open' choice.path
        
        choices.emitAction @ action choice event
        
    #  0000000   00000000   00000000  000   000  0000000    000  00000000   
    # 000   000  000   000  000       0000  000  000   000  000  000   000  
    # 000   000  00000000   0000000   000 0 000  000   000  000  0000000    
    # 000   000  000        000       000  0000  000   000  000  000   000  
    #  0000000   000        00000000  000   000  0000000    000  000   000  
    
    openDir: dirItem opt ->
        
        ⮐  if empty dirItem
        ⮐  if dirItem.open
        
        opt ?= {}
        
        dirItem.open = true
        
        items = @dirItems dirItem.path 'dirtree.openDir'
        
        dirItem.tilde = string.gsub dirItem.tilde icons.dir_close icons.dir_open 
            
        state = ked_session∙get @name {}
        
        depth = dirItem.depth + 1
        for item in items
            item.depth = depth
            @tilde item
            if state.open
                if item.type == 'dir' and state.open[item.path]
                    if empty opt.select and empty opt.index
                        opt.select = dirItem
                    opt.redraw = true
                    @openDir item opt
        
        items∙sort((a b) -> ⮐  @weight(a) < @weight(b))
        
        index = @items∙findWith((i) -> i.path == dirItem.path)
        
        @items∙splice index+1 0 unpack(items)
        
        if opt.index
            index = opt.index
        elif opt.select
            index = @items∙indexof opt.select
        
        @set @items index
        
        ked_session∙set "#{@name}▸open▸#{dirItem.path}" '✔'
        
        # git.status dirItem.path
        @
        
    #  0000000  000       0000000    0000000  00000000  0000000    000  00000000   
    # 000       000      000   000  000       000       000   000  000  000   000  
    # 000       000      000   000  0000000   0000000   000   000  000  0000000    
    # 000       000      000   000       000  000       000   000  000  000   000  
    #  0000000  0000000   0000000   0000000   00000000  0000000    000  000   000  
    
    closeDir: dirItem opt ->
        
        ⮐  if empty dirItem
        
        opt ?= {}
        
        dirItem.open = false
        
        dirItem.tilde = string.gsub dirItem.tilde icons.dir_open icons.dir_close
        
        index = @items∙findWith((i) -> i.path == dirItem.path)
        
        numChildren = 0
        while index+numChildren+1 < @items∙len() and kstr.startsWith(@items[index+numChildren+1].path dirItem.path)
            numChildren += 1
        
        @items∙splice index+1 numChildren
        
        @set @items index
        
        ked_session∙del "#{@name}▸open▸#{dirItem.path}"
        @
                
    # 000  000000000  00000000  00     00   0000000  
    # 000     000     000       000   000  000       
    # 000     000     0000000   000000000  0000000   
    # 000     000     000       000 0 000       000  
    # 000     000     00000000  000   000  0000000   
    
    dirItems: dir info ->
        
        slash.walk dir {recursive:false}

    #  0000000  00000000  000000000  
    # 000       000          000     
    # 0000000   0000000      000     
    #      000  000          000     
    # 0000000   00000000     000     
    
    set: items index=1 -> 
        
        oldTop = @state.s.view[2]
        choices.set @ items 'tilde'
        @state∙setView [1 oldTop]
        @state∙selectLine index
        @state∙setMainCursor 1 index
        
    #  0000000  00000000  000      00000000   0000000  000000000  
    # 000       000       000      000       000          000     
    # 0000000   0000000   000      0000000   000          000     
    #      000  000       000      000       000          000     
    # 0000000   00000000  0000000  00000000   0000000     000     
    
    selectPrevKeepOffset: ->
        
        @selectPrev()
        if @current().type == 'file'
            post∙emit 'quicky' @current().path 
        @state∙setView [1 @state.s.view[1]-1]

    selectNextKeepOffset: ->
    
        if @current().type == 'file' and ked_session∙get('editor▸file') != @current().path
            post∙emit 'quicky' @current().path
            ⮐ 

        @selectNext()
        if @current().type == 'file'
            post∙emit 'quicky' @current().path 
        @state∙setView [1 @state.s.view[2]+1]
        
    selectOpenSiblingAboveOrParent: ->
    
        index = @fuzzied.indexOf @current()
        index -= 1
        while index > 0 and @fuzzied[index].depth >= @current().depth and not @fuzzied[index].open
            index -= 1
        @state.selectLine index
        @state.setMainCursor 1 index
        
    selectParent: ->
    
        index = @fuzzied.indexOf @current()
        index -= 1
        while index > 0 and @fuzzied[index].depth >= @current().depth
            index -= 1
        @state.selectLine index
        @state.setMainCursor 1 index
        
    drawSelections: ->
    
        li = @indexOfOpenFile()
        if li
            bg = theme.gutter.bg
            y  = li-@state.s.view[2]
            if y < @cells.rows and li < @state.s.lines∙len()
                xs = kseg.headCount(@state.s.lines[li] ' ')
                @cells∙set xs-1-@state.s.view[1] y '' bg @color.bg
                for x in xs...@cells.cols
                    @cells∙set_bg x-@state.s.view[1] y bg
        
        choices.drawSelections @
                
    indexOfOpenFile: ->
    
        currentFile = ked_session∙get 'editor▸file'
        
        ⮐  if empty currentFile
        
        for idx item in @fuzzied
            ⮐  idx if item.path == currentFile
            
    itemForPath: p ->
        
        for idx item in @items
            ⮐  item if slash.samePath item.path p

    itemIndexForPath: p ->
        
        for idx item in @items
            ⮐  idx if slash.samePath item.path p
            
    # 000   000  00000000  000   0000000   000   000  000000000  
    # 000 0 000  000       000  000        000   000     000     
    # 000000000  0000000   000  000  0000  000000000     000     
    # 000   000  000       000  000   000  000   000     000     
    # 00     00  00000000  000   0000000   000   000     000     
    
    weight: item ->

        p = slash.parse item.path

        w = 0
        if item.type == 'file'
            w += 10000 
        # w += kstr.weight(p.file)
        w
        
⮐  dirtree
