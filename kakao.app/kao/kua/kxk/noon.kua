###
    ███   ███   ███████    ███████   ███   ███
    ████  ███  ███   ███  ███   ███  ████  ███
    ███ █ ███  ███   ███  ███   ███  ███ █ ███
    ███  ████  ███   ███  ███   ███  ███  ████
    ███   ███   ███████    ███████   ███   ███
###

indstr = "    "
scl = [' ' '#' '|']

class noon

    @: s -> ⮐  noon.stringify s

    #  ███████  █████████  ████████   ███  ███   ███   ███████   ███  ████████  ███   ███
    # ███          ███     ███   ███  ███  ████  ███  ███        ███  ███        ███ ███ 
    # ███████      ███     ███████    ███  ███ █ ███  ███  ████  ███  ██████      █████  
    #      ███     ███     ███   ███  ███  ███  ████  ███   ███  ███  ███          ███   
    # ███████      ███     ███   ███  ███  ███   ███   ███████   ███  ███          ███   

    @toString: o buf ind="" arry=false visited=[] ->
        
        escape = k arry ->
            
            if kstr.find(k '\n') >= 1
            
                sp = [unpack(kstr.split(k '\n'))]
                es = sp∙map((s) -> escape(s arry))
                es∙unshift '...'
                es∙push '...'
                ⮐  es∙join('\n')
                
            if k == '' or k == '...' or scl∙has(k∙sub(1 1)) or scl∙has(k∙sub(k.len k.len)) or arry and kstr.find(k "  ") >= 1
                ⮐  '|' & k & '|'
            k
        
        switch type(o)
            
            "string" ➜ buf += escape(o arry)
            "table"
                if visited∙has o
                    buf += "<v>" 
                else            
                    visited∙push o
                    if o.len > 0 or o is array
                        buf += ind != '' and arry and '.' or ''
                        if o.len and ind != ''
                            buf += '\n' 
                        for i v in o
                            buf += ind
                            noon.toString v buf ind&indstr true visited
                            if i < o.len ➜ buf += ("\n") 
                    else
                        buf += (arry and '.\n') or ((ind != '') and '\n' or '')
                        maxKey = 2
                        
                        keys = []
                        ks = dict.keys o
                        ks∙sort()
                        for k in ks
                            ek = escape k true
                            keys∙push [ek o[k]]
                            maxKey = max maxKey ek.len
                        maxKey = min maxKey 32
                        
                        for k i in keys∙each()
                            buf += ind
                            if type(k[2]) == "table"
                                buf += k[1]
                            else
                                buf += kstr.pad(maxKey k[1])
                                buf += ("  ") 
                            noon.toString k[2] buf ind&indstr false visited
                            if i < keys.len ➜ buf += "\n"
            ➜ 
                buf += tostring(o)
        buf
    
    @stringify: o -> $@toString(o strg())

    # ████████    ███████   ████████    ███████  ████████
    # ███   ███  ███   ███  ███   ███  ███       ███     
    # ████████   █████████  ███████    ███████   ███████ 
    # ███        ███   ███  ███   ███       ███  ███     
    # ███        ███   ███  ███   ███  ███████   ████████
    
    @parse: s ->
    
        s = strg s
        reslt = []
        lines = s∙lines()
        indnt = 0
        for line in lines∙each()
            if line∙number() != nil
                reslt∙push line∙number()
            elif line∙bool() != nil
                reslt∙push line∙bool()
            else
                ind = line∙indent()
                if ind > indnt and ind < line∙len()
                    log "indent" indnt, ind, "▸" & $line & "◂"
                else if ind < indnt and ind < line∙len()
                    log "dedent" indnt, ind, line
                line∙trim()
                ddi = line∙find "  "
                lpi = line∙rfind "|"
                if ddi > 0 and ddi > lpi
                    if not dict.isdict reslt
                        reslt = {}
                    k = $line∙slice 1 ddi-1
                    v = line∙slice ddi+1
                    v = v∙ltrim()
                    if v∙number() != nil
                        v = v∙number()
                    elif v∙bool() != nil
                        v = v∙bool()
                    else
                        v = $v
                    reslt[k] = v
                else
                    if line∙num() > 0
                        if line[line∙num()] == "|"
                            line∙pop()
                        if line[1] == "|"
                            line∙shift()
                        reslt∙push $line
        # for o p in pairs reslt
        #     write "reslt " o " " p
        reslt

⮐  noon

