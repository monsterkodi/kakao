###
    00000000  0000000    000  000000000
    000       000   000  000     000   
    0000000   000   000  000     000   
    000       000   000  000     000   
    00000000  0000000    000     000   
###

class edit
    
    # 000  000   000   0000000  00000000  00000000   000000000
    # 000  0000  000  000       000       000   000     000   
    # 000  000 0 000  0000000   0000000   0000000       000   
    # 000  000  0000       000  000       000   000     000   
    # 000  000   000  0000000   00000000  000   000     000   
    
    @insertTextAtPositions: lines text posl ->
                          
        ⮐  (lines posl) if empty text
        
        if text == '\t'
            pos  = posl[0]
            text = kstr.lpad 4-pos[0]%4 ' '
            
        text   = kstr.detab text
        txtls  = belt.seglsForText text
        newls  = []                
        newpl  = []                
        rngs   = belt.rangesForLinesSplitAtPositions lines posl
        before = []
        
        for idx rng in ipairs rngs
            
            after = belt.seglsForRange lines rng
            
            if empty after or empty after[1]
                if posl[^1][2] >= lines.len
                    before∙push [] 
            
            if idx > 1
                x = posl[idx-1][1]
                # (x y) = posl[idx-1]
                
                if valid before ➜ line = before∙pop()
                else              line = newls∙pop() 
                
                log "x" x, kseg.width(line), line
                
                if x > kseg.width(line) and text != '\n'
                    line += kstr.lpad(x-line.len).split('')
                                        
                if txtls.len > 1
                    
                    if posl.len > 1 and text != '\n'
                        insertLineIndex = (idx-1) % txtls.len
                        before∙push line + txtls[insertLineIndex]
                        newpl∙push [kseg.width(before[^1]) newls.len+before.len-1]
                        before∙push before∙pop() + after∙shift()
                    else
                        posLineIndent = belt.numIndent line
                        indent = kseg(kstr.lpad(posLineIndent))
                        before∙push line + txtls[1]
                        
                        for lidx insl in ipairs txtls∙slice(1)
                            if lidx < txtls.len-2 or valid insl or text == '\n'
                                before∙push indent + insl
                    
                        if x > posLineIndent
                            newpl∙push [kseg.width(before[^1]) newls.len+before.len-1]
                            before∙push before∙pop() + after∙shift()
                        else
                            after∙unshift indent + after∙shift()
                            before∙pop() if text == '\n'
                            newpl∙push [kseg.width(indent) newls.len+before.len] 
                        
                    newls += before
                            
                else
                    newpl∙push [kseg.width(line)+kseg.width(txtls[1]) newls.len+before.len]
                    line  += txtls[1]
                    line  += after∙shift()
                    newls += before      
                    newls∙push line                  
            
            before = after
            
        if posl.len >= 1 and posl[^1][2] <= lines.len
            newls += before 
        log "AFTER INSERT" newls, newpl
        (newls newpl)
        
    #  0000000  000   000  00000000   00000000    0000000   000   000  000   000  0000000    
    # 000       000   000  000   000  000   000  000   000  000   000  0000  000  000   000  
    # 0000000   000   000  0000000    0000000    000   000  000   000  000 0 000  000   000  
    #      000  000   000  000   000  000   000  000   000  000   000  000  0000  000   000  
    # 0000000    0000000   000   000  000   000   0000000    0000000   000   000  0000000    
    
    @insertSurroundAtRanges: lines rngs trigger pair ->
        
        begl = belt.startPositionsOfRanges rngs
        
        (lines begl) = belt.insertTextAtPositions lines pair[0] begl
        
        endl = []
        for pos,idx in begl
            endl∙push [pos[0]+rngs[idx][2]-rngs[idx][0] pos[1]]
            
        (lines endl) = belt.insertTextAtPositions lines pair[1] endl
        
        if pair[0][0] == trigger and pair[0] != pair[1]
            (lines begl)
        else
            (lines endl)

    # 0000000    00000000  000      00000000  000000000  00000000  
    # 000   000  000       000      000          000     000       
    # 000   000  0000000   000      0000000      000     0000000   
    # 000   000  000       000      000          000     000       
    # 0000000    00000000  0000000  00000000     000     00000000  
    
    @deleteLineRangesAndAdjustPositions: lines rngs posl ->
        
        lines = lines∙map((l) -> l)
        # posl  = clone posl
        
        ⮐  (lines posl) if empty rngs
        
        for ri in rngs.len..1
        
            rng = rngs[ri]
            
            if rng[2] > lines.len or rng[4] > lines.len
                log "range out of bounds?" rng
                ⮐  (lines posl)
            
            posl = belt.adjustPositionsForDeletedLineRange posl lines rng
            
            if rng[1] == rng[3] # single line range
                
                if rng[0] == 0 and rng[2] == lines[rng[1]].len
                    lines∙splice rng[1] 1
                else
                    lines∙splice rng[1] 1 lines[rng[1]]∙slice(1 rng[0]) + lines[rng[1]]∙slice(rng[2])
                    
            else
                        
                if rng[2] == lines[rng[3]].len                   
                    lines∙splice rng[3] 1                           
                else
                    lines∙splice rng[3] 1 lines[rng[3]]∙slice(rng[2])
                    partialLast = true                              
                                                                    
                if rng[3]-rng[1] >= 2                               
                    lines∙splice rng[1]+1 rng[3]-rng[1]-1           
                                                                    
                if rng[0] == 0                                      
                    lines∙splice rng[1] 1                           
                else
                    lines∙splice rng[1] 1 lines[rng[1]]∙slice(1 rng[0])
                    
                    if partialLast                                  
                        lines∙splice rng[1] 2 lines[rng[1]] + lines[rng[1]+1]
               
        (lines posl)
        
    @adjustPositionsForDeletedLineRange: posl lines rng ->
        
        ⮐  posl if empty posl
        
        for pi in posl.len..1
            pos = posl[pi]
            
            if belt.isPosTouchingRange pos rng
                pos[1] = rng[1] 
                pos[2] = rng[2]
            elif belt.isPosAfterRange pos rng
                if pos[2] == rng[4]
                    pos[1] -= rng[3] - rng[1]
                    if rng[2] < rng[4]
                        pos[2] -= rng[4] - rng[2]
                else
                    pos[2] -= belt.numFullLinesInRange(lines rng)
            else
                break
                    
        belt.removeDuplicatePositions posl
        
    @moveCursorsInSameLineBy: cursors cursor delta ->
    
        ci = cursors.indexOf cursor
    
        while true
            
            cursors[ci][0] += delta
            ci += 1
            
            ⮐  if ci >= cursors.len
            ⮐  if cursors[ci][1] > cursor[1]
            
    #  0000000   0000000    0000000          00000000    0000000   000   000   0000000   00000000   0000000  
    # 000   000  000   000  000   000        000   000  000   000  0000  000  000        000       000       
    # 000000000  000   000  000   000        0000000    000000000  000 0 000  000  0000  0000000   0000000   
    # 000   000  000   000  000   000        000   000  000   000  000  0000  000   000  000            000  
    # 000   000  0000000    0000000          000   000  000   000  000   000   0000000   00000000  0000000   
    
    @addLinesBelowPositionsToRanges: lines posl rngs ->
    
        newp = []
        newr = rngs #.asMutable()
    
        addLineAtIndex = c i ->

            range = belt.rangeOfLine lines i
            if belt.isEmptyRange range
                range[1] += 1 
            newr∙push range
            newp∙push belt.endOfRange(range)
            
        for c in posl
            if not belt.rangesContainLine(rngs c[1])
                addLineAtIndex c c[1]
            elif c[1] < lines.len-1
                addLineAtIndex c c[1]+1 

        ⮐  (posl rngs) if empty newp
        
        (newp newr)
    
    # 00000000   00000000  00     00   0000000   000   000  00000000     00000000   000   000   0000000    0000000    
    # 000   000  000       000   000  000   000  000   000  000          000   000  0000  000  000        000         
    # 0000000    0000000   000000000  000   000   000 000   0000000      0000000    000 0 000  000  0000  0000000     
    # 000   000  000       000 0 000  000   000     000     000          000   000  000  0000  000   000       000    
    # 000   000  00000000  000   000   0000000       0      00000000     000   000  000   000   0000000   0000000     
    
    @removeLinesAtPositionsFromRanges: lines posl rngs ->
        
        newp = []
        newr = belt.splitLineRanges lines rngs
        
        for pos in posl
            rng = belt.rangeInRangesTouchingPos newr pos
            if rng
                idx = newr.indexOf rng
                if idx > 0
                    newp∙push belt.endOfRange(newr[idx-1])
                else
                    newp∙push belt.endOfRange(newr[idx])
                newr∙splice idx 1
            else
                newp∙push pos
        (newp newr)
        
    @rangeForJoiningLine: lines idx -> [lines[idx].len idx 0 idx+1]
        
    @rangesForJoiningLines: lines idxs -> 
    
        idxs∙map((idx) -> belt.rangeForJoiningLine(lines idx))
    
    # 00     00   0000000   000   000  00000000         000      000  000   000  00000000   0000000  
    # 000   000  000   000  000   000  000              000      000  0000  000  000       000       
    # 000000000  000   000   000 000   0000000          000      000  000 0 000  0000000   0000000   
    # 000 0 000  000   000     000     000              000      000  000  0000  000            000  
    # 000   000   0000000       0      00000000         0000000  000  000   000  00000000  0000000   
    
    @moveLineRangesAndPositionsAtIndicesInDirection: lines rngs posl indices dir ->
        
        if empty indices or dir == 'down' and indices[-1] >= lines.len-1 or dir == 'up' and indices[0] <= 0
            ⮐  [lines rngs posl]
        
        newLines = lines∙map((l) -> l)
        newRngs  = rngs #.asMutable()
        newPosl  = posl #.asMutable()
        
        (rs re) = switch dir
            'down' ➜ [indices.len-1 0]
            'up'   ➜ [0 indices.len-1]

        d = switch dir
            'down' ➜ 1
            'up'   ➜ -1
            
        for ii in rs..re
            index = indices[ii]
            
            switch dir 
                'down' ➜ newLines∙splice index   2 newLines[index+1] newLines[index] 
                'up'   ➜ newLines∙splice index-1 2 newLines[index]   newLines[index-1]
                
            for pos in newPosl
                if pos[1] == index 
                    pos[1] += d

            for rng in newRngs
                if rng[1] == index 
                    rng[1] += d
                    rng[3] += d
        
        [newLines newRngs newPosl]

    #  0000000  000       0000000   000   000  00000000        000      000  000   000  00000000   0000000  
    # 000       000      000   000  0000  000  000             000      000  0000  000  000       000       
    # 000       000      000   000  000 0 000  0000000         000      000  000 0 000  0000000   0000000   
    # 000       000      000   000  000  0000  000             000      000  000  0000  000            000  
    #  0000000  0000000   0000000   000   000  00000000        0000000  000  000   000  00000000  0000000   
    
    @cloneLineBlockRangesAndMoveRangesAndPositionsInDirection: lines blocks rngs posl dir ->
        
        if empty blocks or dir == 'down' and blocks[-1][3] > lines.len-1 or dir == 'up' and blocks[0][1] < 0
            ⮐  [lines rngs posl]
        
        newLines = lines #.asMutable()
        newRngs  = rngs #.asMutable()
        newPosl  = posl #.asMutable()
        
        (rs re) = switch dir
            'down' ➜ [blocks.len-1 0]
            'up'   ➜ [0 blocks.len-1]

        d = switch dir
            'down' ➜ 1
            'up'   ➜ -1
            
        for bi in rs..re
            block = blocks[bi]
  
            text = belt.textForLineRange newLines block
            text += '\n'

            insidx = if dir == 'up' ➜ block[1] ➜ block[3]+1

            (newLines posl) = belt.insertTextAtPositions newLines text [[0 insidx]]

            if dir == 'down'
                d = block[3]-block[1]+1
                
                for pos in newPosl
                    if belt.rangeContainsPos block pos
                        pos[1] += d                
    
                for rng in newRngs
                    if belt.rangeContainsRange block rng
                        rng[1] += d
                        rng[3] += d
        
        (newLines newRngs newPosl)
        
    #  0000000   0000000   00     00  00     00  00000000  000   000  000000000   0000000  
    # 000       000   000  000   000  000   000  000       0000  000     000     000       
    # 000       000   000  000000000  000000000  0000000   000 0 000     000     0000000   
    # 000       000   000  000 0 000  000 0 000  000       000  0000     000          000  
    #  0000000   0000000   000   000  000   000  00000000  000   000     000     0000000   
    
    @toggleCommentsInLineRangesAtIndices: lines rngs posl indices ->
        
        ⮐  (lines rngs posl) if empty indices
        
        newLines = lines #.asMutable()
        newRngs  = rngs #.asMutable()
        newPosl  = posl #.asMutable()
        
        comStart = '#'
        minIndent = Infinity
             
        for index in indices
            
            (indent line) = belt.splitLineIndent newLines[index]
            if not kseg.startsWith(line comStart)
                comment = comStart
                minIndent = min indent.len minIndent
        
        comIndent = ''    
        if comment
            comIndent = kseg.rep(minIndent) 
                
        for _ index in ipairs indices
            
            (indent line) = belt.splitLineIndent newLines[index]
            if comment
                indent  = kseg.rep indent.len-minIndent
                newLine = kseg.join comIndent comment indent ' ' line
            else
                d = if line[comStart.len] == ' ' ➜ 1 ➜ 0
                newLine = kseg.join indent line∙slice(comStart.len+d)
            newLines∙splice index 1 newLine
        
        (newLines newRngs newPosl)
        
    @toggleCommentTypesInLineRangesAtIndices: lines rngs posl indices ->
        
        ⮐  (lines rngs posl) if empty indices
        
        newLines = lines∙map((l) -> l)
        newRngs  = rngs #.asMutable()
        newPosl  = posl #.asMutable()
        
        log "todo: toggleCommentTypes #{indices}"
        
        (newLines newRngs newPosl)
        
    # 0000000    00000000  000  000   000  0000000    00000000  000   000  000000000  
    # 000   000  000       000  0000  000  000   000  000       0000  000     000     
    # 000   000  0000000   000  000 0 000  000   000  0000000   000 0 000     000     
    # 000   000  000       000  000  0000  000   000  000       000  0000     000     
    # 0000000    00000000  000  000   000  0000000    00000000  000   000     000     
    
    @deindentLineRangesAndPositionsAtIndices: lines rngs posl indices ->
        
        ⮐  (lines rngs posl) if empty indices  
        
        newLines = lines∙map((l) -> l)
        newRngs  = rngs #.asMutable()
        newPosl  = posl #.asMutable()
        
        for index in indices
            
            (indent line) = belt.splitLineIndent newLines[index]
        
            if indent.len
                
                sc = min 4 indent.len
                newLines∙splice index 1 kseg.join(indent∙slice(sc) line)
                
                for pos in newPosl
                    if pos[1] == index 
                        pos[0] = max 0 pos[0] - sc
                        
                for rng in newRngs
                    if rng[1] == index 
                        rng[0] = max 0 rng[0] - sc
                        rng[2] = max 0 rng[2] - sc
        
        (newLines newRngs newPosl)

    # 000  000   000  0000000    00000000  000   000  000000000  
    # 000  0000  000  000   000  000       0000  000     000     
    # 000  000 0 000  000   000  0000000   000 0 000     000     
    # 000  000  0000  000   000  000       000  0000     000     
    # 000  000   000  0000000    00000000  000   000     000     
    
    @indentLineRangesAndPositionsAtIndices: lines rngs posl indices ->
        
        ⮐  (lines rngs posl) if empty indices  
        
        newLines = lines∙map((l) -> l)
        newRngs  = rngs #.asMutable()
        newPosl  = posl #.asMutable()
        
        for index in indices
            
            (indent line) = belt.splitLineIndent newLines[index]
        
            newLines[index] = kseg.join kseg.rep(4) newLines[index]
            
            for pos in newPosl
                if pos[1] == index 
                    pos[0] += 4
                    
            for rng in newRngs
                if rng[1] == index 
                    rng[0] += 4
                    rng[2] += 4
        
        (newLines newRngs newPosl)
        
    # 00000000  000   000  000000000  00000000  000   000  0000000          00000000   000   000   0000000    0000000  
    # 000        000 000      000     000       0000  000  000   000        000   000  0000  000  000        000       
    # 0000000     00000       000     0000000   000 0 000  000   000        0000000    000 0 000  000  0000  0000000   
    # 000        000 000      000     000       000  0000  000   000        000   000  000  0000  000   000       000  
    # 00000000  000   000     000     00000000  000   000  0000000          000   000  000   000   0000000   0000000   
    
    @extendLineRangesFromPositionToPosition: lines rngs start pos ->

        if empty rngs ➜ ⮐  [belt.rangeFromStartToEnd(start pos)]

        newRngs = rngs #.asMutable()
        
        rng = belt.rangeInRangesTouchingPos newRngs start
        if rng
            if belt.isPosAfterRange pos rng
                rng[2] = pos[0]
                rng[3] = pos[1]
            elif belt.isPosBeforeRange pos rng
                rng[0] = pos[0]
                rng[1] = pos[1]
        else
            newRngs∙push belt.rangeFromStartToEnd(start pos)

        newRngs
    
    @extendLineRangesByMovingPositionsInDirection: lines rngs posl dir opt ->
        
        newRngs = rngs #.asMutable()
        newPosl = posl #.asMutable()
        
        for pi pos in ipairs newPosl
            
            line = lines[pos[2]]
            rng = [pos[1] pos[2] pos[1] pos[2]]
            newRngs∙push rng
            nc = 0
            switch dir
                'left'  
                'right'     ➜ nc = belt.numCharsFromPosToWordOrPunctInDirection(lines pos dir opt); pos[1] += nc
                'up'        ➜ pos[2] -= 1
                'down'      ➜ pos[2] += 1
                'eol'       ➜ pos[1] = line.len
                'bol'       ➜ pos[1] = 0
                'bof'       ➜ pos[1] = 0; pos[2] = 0
                'eof'       ➜ pos[2] = lines.len-1; pos[1] = lines[lines.len-1].len

                'ind_bol'   ➜ ind = belt.numIndent(line); pos[1] = if pos[1] > ind ➜ ind ➜ 0
                'ind_eol'   ➜ ind = belt.numIndent(line); pos[1] = if pos[1] < ind ➜ ind ➜ line.len
                
            switch dir
                'left'      ➜ rng[1] = rng[1]+nc
                'right'     ➜ rng[3] = rng[3]+nc
                'up'        ➜ rng[2] = max 0 rng[2]-1
                'down'      ➜ rng[3] = min lines.len-1 rng[3]+1
                'eol'       ➜ rng[3] = Infinity
                'bol'       ➜ rng[1] = 0
                'bof'       ➜ rng[2] = 0; rng[1] = 0
                'eof'       ➜ rng[3] = lines.len-1; rng[3] = lines[lines.len-1].len

                'ind_bol'   ➜ ind = belt.numIndent(line); rng[1] = if rng[1] > ind ➜ ind ➜ 0
                'ind_eol'   ➜ ind = belt.numIndent(line); rng[3] = if rng[3] < ind ➜ ind ➜ line.len
            
            if rng[2] < lines.len
                rng[1] = clamp 0 lines[rng[2]].len rng[1] 
            if rng[3] < lines.len
                rng[3] = clamp 0 lines[rng[3]].len rng[3] 
        
        (newRngs newPosl)
        
⮐  edit
