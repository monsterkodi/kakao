###
    00     00   ███████   ███████    ████████  
    000   000  ███   ███  ███   ███  ███       
    000000000  ███   ███  ███   ███  ███████   
    000 0 000  ███   ███  ███   ███  ███       
    000   000   ███████   ███████    ████████  

    the class stores modes per editor state
    the instances handle input and text insertion

    vimple   simple vi
    salter   ascii-header 
    uniko    unicode drawing
    unype    unicode type writer
    record   
###

use ../../kxk ◆ kseg salter

import fonts from '../util/fonts.json' with { type: "json" }

function mode

    @active: {} # maps editor state names to active mode instances
    @modes: {}  # maps mode names to mode subclasses

    @: @name -> # log "mode: #{@name}"
    
    @names: -> Object.keys mode.modes

    #  ███████  █████████   ███████   ████████   █████████
    # ███          ███     ███   ███  ███   ███     ███   
    # ███████      ███     █████████  ███████       ███   
    #      ███     ███     ███   ███  ███   ███     ███   
    # ███████      ███     ███   ███  ███   ███     ███   
    
    @start: state name ->
    
        ⮐ if @isActive state name
        
        log "mode.start #{name}"
        
        @active[state.name] ?= []
        @active[state.name].push new mode.modes[name] state
        
        log "mode.start" @active[state.name].map((m) -> m.name)
            
    @stop: state name ->
        
        log "mode.stop #{name}"
        
        m = @get state name
        m.stop() if m.stop is func
        
        @active[state.name].splice @active[state.name].indexOf(m) 1
        
        log "mode.stop" @active[state.name].map((m) -> m.name)

    @toggle: state name ->
        
        if @isActive state name
            @stop state name
        else
            @start state name
        
    #  ███████    ███████  █████████  ███  ███   ███  ████████
    # ███   ███  ███          ███     ███  ███   ███  ███     
    # █████████  ███          ███     ███   ███ ███   ███████ 
    # ███   ███  ███          ███     ███     ███     ███     
    # ███   ███   ███████     ███     ███      █      ████████
    
    @isActive: state name -> valid @get(state name)
        
    @get: state name ->
        
        for m in @active[state.name]
            ⮐ m if m.name == name

    # 000  000   000   0000000  00000000  00000000   000000000  
    # 000  0000  000  000       000       000   000     000     
    # 000  000 0 000  0000000   0000000   0000000       000     
    # 000  000  0000       000  000       000   000     000     
    # 000  000   000  0000000   00000000  000   000     000     
    
    @insert: state text ->
        
        # log "@mode.insert #{text}"
        
        for mode in @active[state.name]
            # log "active #{mode.name}"
            if mode.insert is func
                # log "and func #{mode.name}"
                text = mode.insert text
        
        text
        
    # 000   000  00000000  000   000  
    # 000  000   000        000 000   
    # 0000000    0000000     00000    
    # 000  000   000          000     
    # 000   000  00000000     000     
    
    @handleKey: state key event ->
        
        for mode in @active[state.name]
            if mode.handleKey is func
                ⮐ if mode.handleKey(key event) != 'unhandled'
        'unhandled'
            
# ███   ███  ███  ██     ██  ████████   ███      ████████
# ███   ███  ███  ███   ███  ███   ███  ███      ███     
#  ███ ███   ███  █████████  ████████   ███      ███████ 
#    ███     ███  ███ █ ███  ███        ███      ███     
#     █      ███  ███   ███  ███        ███████  ████████

function vimpleMode extends mode
    
    @: -> super 'vimple'

#  ███████   ███████   ███      █████████  ████████  ████████ 
# ███       ███   ███  ███         ███     ███       ███   ███
# ███████   █████████  ███         ███     ███████   ███████  
#      ███  ███   ███  ███         ███     ███       ███   ███
# ███████   ███   ███  ███████     ███     ████████  ███   ███

function salterMode extends mode

    @syms: []

    @: @state -> 
    
        super 'salter'
        
        if empty salterMode.syms
            salterMode.syms = Object.keys salter.font
        
        @start()
    
    start: -> 
    
        @state.setMainCursor @state.mainCursor()
        @state.expandCursors 'down'
        @state.expandCursors 'down'
        @state.expandCursors 'down'
        @state.expandCursors 'down'
            
    stop: ->
    
        @state.setMainCursor @state.mainCursor()
    
    handleKey: key event ->
        
        switch key
            'esc' ➜ ⮐ mode.stop @state @name
            'delete'
                @state.delete 'back' 
                @state.delete 'back' 
                @state.delete 'back' 
                @state.delete 'back' 
                @state.delete 'back' 
                @state.delete 'back' 
                @state.delete 'back' 
                @state.delete 'back' 
                @state.delete 'back' 
                @state.delete 'back' 
                ⮐
            
        if @state.s.cursors.length != 5 # need to check for alignment as well?
            mode.stop @state @name
            ⮐  'unhandled'
            
        if salter.font[key]?
            
            salt = salter key char:'█' postfix:'  '
            @state.insert salt
            ⮐
        
        'unhandled'

# ███   ███  ███   ███  ███  ███   ███   ███████ 
# ███   ███  ████  ███  ███  ███  ███   ███   ███
# ███   ███  ███ █ ███  ███  ███████    ███   ███
# ███   ███  ███  ████  ███  ███  ███   ███   ███
#  ███████   ███   ███  ███  ███   ███   ███████ 

function unikoMode extends mode

    @: -> super 'uniko'
    
# ███   ███  ███   ███  ███   ███  ████████   ████████
# ███   ███  ████  ███   ███ ███   ███   ███  ███     
# ███   ███  ███ █ ███    █████    ████████   ███████ 
# ███   ███  ███  ████     ███     ███        ███     
#  ███████   ███   ███     ███     ███        ████████

function unypeMode extends mode
    
    @map: {}
    
    @: -> 
    
        super 'unype'
        
        if empty unypeMode.map
            def = fonts.default.join ' '
            for font,text of fonts
                continue if font=='default'
                unypeMode.map[font] = {}
                for char,idx in kseg(text.join(' '))
                    if def[idx] != ' '
                        unypeMode.map[font][def[idx]] = char
            unypeMode.map['full width'][' '] = '　'
    
    insert: text ->
    
        if repl = unypeMode.map['crazy'][text]
            text = repl
        text
    
function recordMode extends mode

    @: -> super 'record'
    
mode.modes = 
    vimple: vimpleMode
    salter: salterMode
    unype:  unypeMode
    uniko:  unikoMode
    record: recordMode
    
export mode
