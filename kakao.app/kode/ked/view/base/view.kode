###
    ███   ███  ███  ████████  ███   ███ 
    ███   ███  ███  ███       ███ █ ███ 
     ███ ███   ███  ███████   █████████ 
       ███     ███  ███       ███   ███ 
        █      ███  ████████  ██     ██ 

    keeps track of
        - screen cells
        - visibility
        - theme colors
        - view features
        - mouse enter and leave
        - current popup
###

use ../../../kxk ▪ events post
use   ../screen  ◆ cells 

function view extends events

    @popups: ['quicky' 'browse' 'context' 'menu' 'searcher' 'finder']
    @currentPopup: null

    @: @screen @name features ->
        
        @cells = new cells @screen
        @color = {}
        @feats = {}
        @feats[f] = true for f in features
        @isVisible = true
        @focusable = false
                        
        post.on 'view.show' @onViewShow if @name in view.popups
        post.on 'view.hide' @onViewHide if @name in view.popups
        
    setColor: key color => @color[key] = color
        
    onViewHide: viewName =>
    
        if viewName == view.currentPopup
            view.currentPopup = null
            
        if viewName == @name and empty view.currentPopup
            log "onViewHide #{viewName}"
            post.emit 'popup.hide' viewName
        
    onViewShow: viewName =>
    
        if viewName == @name
            log "onViewShow #{viewName}"
            post.emit 'popup.show' viewName
                
        if viewName in view.popups
            view.currentPopup = viewName            
            if viewName != @name and @visible()
                @hide() 
                
    # 000   000  000   0000000  000  0000000    000  000      000  000000000  000   000  
    # 000   000  000  000       000  000   000  000  000      000     000      000 000   
    #  000 000   000  0000000   000  0000000    000  000      000     000       00000    
    #    000     000       000  000  000   000  000  000      000     000        000     
    #     0      000  0000000   000  0000000    000  0000000  000     000        000     
  
    show: -> 
    
        @isVisible = true
        post.emit 'view.show' @name
        @arrange()
        redraw:true
        
    arrange: ->    
    hide:    -> @isVisible=false; post.emit 'view.hide' @name; redraw:true
    hidden:  -> not @visible()
    visible: -> @isVisible #and @cells.rows > 0 and @cells.cols > 0
    toggle:  -> if @hidden() then @show() else @hide()
                
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event => @handleHover event
    onWheel: event => log "view.onWheel #{@name}"
    
    onMouseLeave: => post.emit 'redraw'
    onMouseEnter: => 
        
        if @focusable and @grabFocus is func
            @grabFocus()
        
        if @pointerType 
            post.emit 'pointer' @pointerType
            post.emit 'redraw'
        
    handleHover: event =>
        
        inside = not event.handled and @cells.isInsideEvent event
        if @hover and not inside
            @hover = false
            @onMouseLeave()
        else if inside and not @hover
            @hover = true
            @onMouseEnter()
        @hover
    
    onKey: key event => log "view.onKey #{@name}"
        
    # 0000000    00000000    0000000   000   000  
    # 000   000  000   000  000   000  000 0 000  
    # 000   000  0000000    000000000  000000000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000  00     00  
    
    layout: x y w h -> @cells.layout x y w h 
    
    draw: => 
        
export view
