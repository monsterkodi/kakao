###
     0000000   0000000   0000000    0000000   000000000  00000000  00000000 
    000       000       000   000  000   000     000     000       000   000
    0000000   000       000   000  000   000     000     0000000   0000000  
         000  000       000   000  000   000     000     000       000   000
    0000000    0000000   0000000    0000000      000     00000000  000   000
    
    takes a string and evaluates it as a math expression
    converts the resulting number to a string without scientific notation
    and some constants replaced with unicode symbols (âˆž)
    tries to handle some bigint calculations, but not too hard :)
###

use â—† pepe kstr
use ../kode/kode

descience = str -> # convert scientific/exponential notation
    
    if 'e+' in str
        str = BigInt(Number.parseFloat(str)).toString()
    else if 'e-' in str
        em = parseInt str.split('e-')[1]
        str = Number.parseFloat(str).toFixed(em+2)
        while str[-1] == '0'
            str = str[0...-1]
    str

pow = str ->
    
    splt = str.split '^'
    if splt.length > 1
        str = "pow(#{splt[0]}, #{pow splt[1..].join '^'})"
    str
    
deg = str ->
    
    splt = str.split 'Â°'
    if splt.length > 1
        val = splt[0]
        if val[0] == '(' and val[-1] == ')'
            str = "rad#{val}" + deg splt[1..].join 'Â°'
        else
            for i in val.length-1..0
                if val[i] not in '0.123456789'
                    pre  = val[0..i] 
                    val = val[i+1..-1]
                    break
            pre ?= ''
            str = pre + "rad(#{val})" + deg splt[1..].join 'Â°'
    str
    
build = str ->
    
    pep = pepe str
    
    if pep.length > 1 or not pep[0] is str
        dep = pepe.depepe pep, build
        str = dep        
    
    str = pow str
    str = deg str
    str

#  0000000   0000000   0000000    0000000   000000000  00000000  00000000   
# 000       000       000   000  000   000     000     000       000   000  
# 0000000   000       000   000  000   000     000     0000000   0000000    
#      000  000       000   000  000   000     000     000       000   000  
# 0000000    0000000   0000000    0000000      000     00000000  000   000  

scooter = str ->
    
    ost = str
    str = str.replace /log\(/g, 'Math.log('
    
    str = str.replace /âˆ¡/,  'deg'
    str = str.replace /âˆš/g, 'sqrt'
    str = str.replace /Ï€/g, 'PI'
    str = str.replace /Ï•/g, 'PHI'
    str = str.replace /ð’†/g, 'E'
    str = str.replace /âˆž/g, 'Infinity'
    
    b = """
        {PI, E, sqrt, pow, cos, sin, tan, acos, asin, atan} = Math
        rad = d -> PI*d/180.0
        deg = r -> r*180.0/PI
        PHI = (1+sqrt(5))/2
        
        """
    b += '(' + build(str) + ')'
    
    k = new kode
    r = k.eval b
    # log g2(ost), b5(str), r3(build(str)), g5 r
    # log ost, str, build(str), r
    
    val = kstr r
    val = trim val, ' \n'
    val = val.replace  /Infinity/g, 'âˆž'
    val = val.replace  /NaN/g, ''
    val = descience val
    val
    
scooter.near = a b -> Math.abs(a-b) < 0.000000000000001

export scooter
