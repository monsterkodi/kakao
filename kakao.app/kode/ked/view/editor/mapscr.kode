###
    ██     ██   ███████   ████████    ███████   ███████  ████████   
    ███   ███  ███   ███  ███   ███  ███       ███       ███   ███  
    █████████  █████████  ████████   ███████   ███       ███████    
    ███ █ ███  ███   ███  ███             ███  ███       ███   ███  
    ███   ███  ███   ███  ███        ███████    ███████  ███   ███  

    scrollable source map to the right of the fileeditor
###

use ../../../kxk    ▪ post
use ../../theme     ◆ theme
use ../../util/img  ◆ squares
use ../../edit/tool ◆ belt
use                 ◆ mapview

function mapscr extends mapview

    @: @editor ->
        
        super @editor.screen @editor.state
        
        @state.on 'view.changed' @drawKnob
        
        @pointerType = 'pointer'
        
        @knobId = @imgId+0xeeee
        
        @setColor 'bg'        theme.editor.mapscr
        @setColor 'highlight' theme.highlight.map
        @setColor 'selection' theme.selection.map
        @setColor 'fullysel'  theme.selection.mapfully
        
        @screen.t.on 'preResize' @clearImages
        
        post.on 'popup.show' (name) => @hide() if name in ['searcher' 'finder' 'differ']
        post.on 'greet.show' @hide
        
        post.on 'popup.hide' @show
        post.on 'greet.hide' @show
        
    getSegls:  => @state.segls
    getSyntax: => @state.syntax
    
    # 00000000   00000000   0000000  000  0000000  00000000  
    # 000   000  000       000       000     000   000       
    # 0000000    0000000   0000000   000    000    0000000   
    # 000   000  000            000  000   000     000       
    # 000   000  00000000  0000000   000  0000000  00000000  
    
    onResize: =>
        
        ⮐  if empty @cells.screen.t.pixels
        
        @redraw = true
        
    #  0000000   0000000  00000000    0000000   000      000      000000000   0000000   
    # 000       000       000   000  000   000  000      000         000     000   000  
    # 0000000   000       0000000    000   000  000      000         000     000   000  
    #      000  000       000   000  000   000  000      000         000     000   000  
    # 0000000    0000000  000   000   0000000   0000000  0000000     000      0000000   
    
    scrollToPixel: pixel =>
        
        view = @state.s.view.asMutable()
        
        view[1] = int (pixel[1] - @cells.y * @cells.screen.t.cellsz[1]) / @pixelsPerRow
        
        view[1] -= 6 # offset so that clicking inside a header scrolls it into view
        
        maxY = @state.s.lines.length - @cells.rows
        
        view[1] = min maxY view[1] if maxY > 0
        view[1] = max 0 view[1]
        
        ⮐  if view eql @state.s.view
        
        @state.setView view
        @drawKnob()
        redraw:true
        
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event =>
        
        super event
        
        switch event.type
            
            'press'
            
                if @hover
                    @doDrag = true
                    post.emit 'pointer''grabbing'
                    ⮐  @scrollToPixel event.pixel
                    
            'drag'
            
                if @doDrag
                    post.emit 'pointer''grab'
                    ⮐  @scrollToPixel event.pixel
                    
                @hover = false
                
            'release'
            
                if @doDrag
                    delete @doDrag 
                    post.emit 'pointer''pointer' if @hover
                    ⮐  true
        @hover

###        
     ███  ██     ██   ███████    ███████   ████████   ███████
     ███  ███   ███  ███   ███  ███        ███       ███     
     ███  █████████  █████████  ███  ████  ███████   ███████ 
     ███  ███ █ ███  ███   ███  ███   ███  ███            ███
     ███  ███   ███  ███   ███   ███████   ████████  ███████ 
###

    hide: =>
    
        ⮐  if @hidden()
        
        @cells.screen.t.hideImageOverlay @knobId
        
        super()
        
    # ███████    ████████    ███████   ███   ███
    # ███   ███  ███   ███  ███   ███  ███ █ ███
    # ███   ███  ███████    █████████  █████████
    # ███   ███  ███   ███  ███   ███  ███   ███
    # ███████    ███   ███  ███   ███  ██     ██
    
    draw: =>
    
        ⮐  if @hidden() or @collapsed()
        
        super()
        
        if csz = @cells.screen.t.cellsz
        
            @drawCursors    csz
            @drawHighlights csz

    drawImages: =>
        
        ⮐  if empty @cells.screen.t.pixels or @hidden() or @collapsed()
        
        super()
        
        @drawKnob()
        
    drawKnob: =>
        
        t = @cells.screen.t
        
        ⮐  if empty t.pixels or @hidden() or @collapsed()
        
        y  = @pixelsPerRow*@state.s.view[1]/t.cellsz[1]
        yc = int(y) 
        yr = int((y - yc) * t.cellsz[1])
        
        h  = int @state.cells.rows*@pixelsPerRow
        w  = @cells.cols*t.cellsz[0]
        t.placeImageOverlay @knobId @cells.x @cells.y+yc 0 yr w h
        
    #  ███████  ███   ███  ████████    ███████   ███████   ████████    ███████
    # ███       ███   ███  ███   ███  ███       ███   ███  ███   ███  ███     
    # ███       ███   ███  ███████    ███████   ███   ███  ███████    ███████ 
    # ███       ███   ███  ███   ███       ███  ███   ███  ███   ███       ███
    #  ███████   ███████   ███   ███  ███████    ███████   ███   ███  ███████ 

    drawCursors: csz =>
        
        mw = @cells.cols*csz[0]
    
        for pos,idx in @state.s.cursors
        
            if pos[0]*@pixelsPerCol < mw
            
                sx = @cells.x*csz[0] + pos[0]*@pixelsPerCol
                
                if idx == @state.s.main
                    fg = theme.cursor.main
                    sw = @pixelsPerCol*2
                    sx -= int @pixelsPerCol/2
                else
                    fg = theme.cursor.multi
                    sw = @pixelsPerCol
                    
                sy = @cells.y*csz[1] + pos[1]*@pixelsPerRow
                squares.place sx sy sw @pixelsPerRow fg
                
                if idx == @state.s.main
                    squares.place @cells.x*csz[0]+mw-@pixelsPerCol*4 sy @pixelsPerCol*4 @pixelsPerRow fg 2002

    # 000   000  000   0000000   000   000  000      000   0000000   000   000  000000000   0000000  
    # 000   000  000  000        000   000  000      000  000        000   000     000     000       
    # 000000000  000  000  0000  000000000  000      000  000  0000  000000000     000     0000000   
    # 000   000  000  000   000  000   000  000      000  000   000  000   000     000          000  
    # 000   000  000   0000000   000   000  0000000  000   0000000   000   000     000     0000000   
    
    drawHighlights: csz =>
        
        mw = @cells.cols*csz[0]
        mc = @state.mainCursor()
    
        ppr = @pixelsPerRow
        ppc = @pixelsPerCol
        
        for li in belt.lineIndicesForRanges @state.s.selections
            if @state.isSpanSelectedLine li 
                clr = @color.selection
            else
                clr = @color.fullysel
            sy = @cells.y*csz[1] + li*ppr
            squares.place @cells.x*csz[0]+mw-ppc*16 sy ppc*16 ppr clr 2000
    
        for li in belt.lineIndicesForSpans  @state.s.highlights
        
            sy = @cells.y*csz[1] + li*ppr
            squares.place @cells.x*csz[0]+mw-ppc*8 sy ppc*8 ppr @color.highlight 2001

export mapscr
