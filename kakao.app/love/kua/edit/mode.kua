###
    ██     ██   ███████   ███████    ████████
    ███   ███  ███   ███  ███   ███  ███     
    █████████  ███   ███  ███   ███  ███████ 
    ███ █ ███  ███   ███  ███   ███  ███     
    ███   ███   ███████   ███████    ████████

    stores modes per editor state
    
    modes handle input and text insertion:

    brckts   insert and highlight matching surround 
    replex   replaces certain input strings with others
    salter   ascii-headers
    unype    unicode type writer
###

use edit.mode.unype
use edit.mode.brckts
use edit.mode.replex
use edit.mode.salter
use edit.mode.filepos

class mode

    @active:  {} # maps editor state names to active mode instances
    @modes:   { 
        brckts:brckts
        filepos:filepos
        replex:replex
        salter:salter
        unype:unype
        } 
    
    @names: -> 
    
        n = []
        for k v in pairs mode.modes
            n∙push k
        n
    
    #  0000000   000   000  000000000   0000000    0000000  000000000   0000000   00000000   000000000  
    # 000   000  000   000     000     000   000  000          000     000   000  000   000     000     
    # 000000000  000   000     000     000   000  0000000      000     000000000  0000000       000     
    # 000   000  000   000     000     000   000       000     000     000   000  000   000     000     
    # 000   000   0000000      000      0000000   0000000      000     000   000  000   000     000     
    
    @autoStartForEditor: editor ->
        for _ name in ipairs mode.names()
            log "autostartForEditor" editor.feats[name], name, mode.modes[name]
            if editor.feats[name] and mode.modes[name].autoStart
                mode.start editor.state name

    #  ███████  █████████   ███████   ████████   █████████
    # ███          ███     ███   ███  ███   ███     ███   
    # ███████      ███     █████████  ███████       ███   
    #      ███     ███     ███   ███  ███   ███     ███   
    # ███████      ███     ███   ███  ███   ███     ███   
    
    @start: state name ->
    
        ⮐  if @isActive state name
        
        @active[state.name] ?= []
        @active[state.name]∙push mode.modes[name](state)
        
    @stop: state name ->
        
        m = @get state name
        
        @active[state.name].splice @active[state.name].indexOf(m) 1
        
        m.stop() if m.stop is func
        
    @toggle: state name ->
        
        if @isActive state name
            @stop state name
        else
            @start state name
        
    #  ███████    ███████  █████████  ███  ███   ███  ████████
    # ███   ███  ███          ███     ███  ███   ███  ███     
    # █████████  ███          ███     ███   ███ ███   ███████ 
    # ███   ███  ███          ███     ███     ███     ███     
    # ███   ███   ███████     ███     ███      █      ████████

    @isActive: state name -> valid @get(state name)
        
    @get: state name ->
    
        if @active[state.name]
            for m in @active[state.name]∙each()
                ⮐  m if m.name == name

    @insert: state text ->
        
        if @active[state.name]
            for m in @active[state.name]∙each()
                if m.insert is "function"
                    text = m.insert text
        text
        
    @postInsert: state ->
        
        if @active[state.name]
            for m in @active[state.name]∙each()
                if m.postInsert is "function"
                    m.postInsert()
        
    @deleteSelection: state ->
        
        if @active[state.name]
            for m in @active[state.name]∙each()
                if m.deleteSelection is "function"
                    ⮐  true if m.deleteSelection()
        false        
        
    @handleKey: state key event ->

        if @active[state.name]
            for m in @active[state.name]∙each()
                if m.handleKey is "function"
                    ⮐  if m.handleKey(m key event) != 'unhandled'
        'unhandled'
        
    @cursorsSet: state editor ->

        if @active[state.name]
            for m in @active[state.name]∙each()
                if m.cursorsSet is "function"
                    m∙cursorsSet editor
                
        if not @isActive state 'salter'
            if mode.modes['salter']
                mode.modes['salter']∙checkCursorsSet state
        
    @themeColor: state colorName defaultColor ->
    
        if @active[state.name]
            for m in @active[state.name]∙each()
                if m.themeColor is "function"
                    ⮐  m.themeColor colorName defaultColor
            
        defaultColor
        
    @preDrawLines: state lines ->

        if @active[state.name]
            for m in @active[state.name]∙each()
                if m.preDrawLines is "function"
                    lines = m.preDrawLines(lines)
        lines
        
    @postDraw: state ->
    
        if @active[state.name]
            for m in @active[state.name]∙each()
                if m.postDraw is "function"
                    m.postDraw()
                
    @fileLoaded: state file row col view ->
        
        if @active[state.name]
            for m in @active[state.name]∙each()
                if m.fileLoaded is "function"
                    m.fileLoaded file row col view
        nil
            
# ███   ███  ███  ██     ██  ████████   ███      ████████
# ███   ███  ███  ███   ███  ███   ███  ███      ███     
#  ███ ███   ███  █████████  ████████   ███      ███████ 
#    ███     ███  ███ █ ███  ███        ███      ███     
#     █      ███  ███   ███  ███        ███████  ████████

# class vimple
#     
#     @: state -> @state = state; @name = 'vimple'

# ███   ███  ███   ███  ███  ███   ███   ███████ 
# ███   ███  ████  ███  ███  ███  ███   ███   ███
# ███   ███  ███ █ ███  ███  ███████    ███   ███
# ███   ███  ███  ████  ███  ███  ███   ███   ███
#  ███████   ███   ███  ███  ███   ███   ███████ 

# class uniko
# 
#     @: state -> @state = state; @name = 'uniko'
        
# 00000000   00000000   0000000   0000000   00000000   0000000    
# 000   000  000       000       000   000  000   000  000   000  
# 0000000    0000000   000       000   000  0000000    000   000  
# 000   000  000       000       000   000  000   000  000   000  
# 000   000  00000000   0000000   0000000   000   000  0000000    

# class record
# 
#     @: state -> @state = state; @name = 'record'
    
# mode.loadModules()
    
⮐  mode
