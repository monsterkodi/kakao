###
     ███████   ███████  ████████    ███████   ███      ███      
    ███       ███       ███   ███  ███   ███  ███      ███      
    ███████   ███       ███████    ███   ███  ███      ███      
         ███  ███       ███   ███  ███   ███  ███      ███      
    ███████    ███████  ███   ███   ███████   ███████  ███████  

    scrolls a view
    used by any multiline editor
###

use view.base.view

class scroll extends view

    @: state side='left' -> 
    
        @state = state
        @side  = side
        view.init @, (@state∙owner() & '_scroll')
        
        @pointerType = 'pointer'
        
        @setColor 'bg'    theme.gutter.bg
        @setColor 'dot'   theme.scroll.dot
        @setColor 'knob'  theme.scroll.knob
        @setColor 'hover' theme.scroll.hover
            
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event ->
        
        (col row) = @eventPos event
        
        view.onMouse @ event
        
        switch event.type
            
            'press'
            
                if @hover
                    @doDrag = true
                    post∙emit 'pointer''grabbing' 
                    ⮐  @scrollToPixel event.pixel
                    
            'drag'
            
                if @doDrag
                    @hover = true
                    post∙emit 'pointer''grab' 
                    ⮐  @scrollToPixel event.pixel
                    
                @hover = false
                
            'release'
            
                if @doDrag
                    if @hover
                        post∙emit 'pointer' 'pointer' 
                    @doDrag = nil
                    ⮐  true
        @hover
        
    isActive: ->
        
        @state.s.lines.length > @cells.rows
        
    #  0000000   0000000  00000000    0000000   000      000      000000000   0000000   
    # 000       000       000   000  000   000  000      000         000     000   000  
    # 0000000   000       0000000    000   000  000      000         000     000   000  
    #      000  000       000   000  000   000  000      000         000     000   000  
    # 0000000    0000000  000   000   0000000   0000000  0000000     000      0000000   
    
    scrollToPixel: pixel ->
    
        csz = @screen∙size()

        view = @state.s.view∙arr()
        
        rowf = pixel[2]/csz[2]-@cells.y
        view[2] = floor(rowf * (@state.s.lines∙len() - @cells.rows + 1)  / (@cells.rows-1))
        
        maxY = @state.s.lines∙len() - @cells.rows
        
        if maxY > 1
            view[2] = min maxY view[2] 
        view[2] = max 1 view[2]
        
        ⮐  true if view∙eql @state.s.view
        
        log "SCROLLLVEW" view
        @state∙setView view 
        
        true
        
    # 0000000    00000000    0000000   000   000  
    # 000   000  000   000  000   000  000 0 000  
    # 000   000  0000000    000000000  000000000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000  00     00  
    
    draw: ->

        cw = _G.screen.cw        
        ch = _G.screen.ch        

        xo = (@cells.x-1)*cw
        yo = (@cells.y-1)*ch
        
        love.graphics.setColor @color.bg[1]/255 @color.bg[2]/255 @color.bg[3]/255
        love.graphics.rectangle "fill" xo+1 yo+1 @cells.cols*cw @cells.rows*ch

        rows = @cells.rows
        lnum = @state.s.lines∙len()
        
        if lnum > rows
        
            kh = ((rows*rows) / lnum) * ch
            ky = int((rows*ch-kh) * (@state.s.view[2]-1) / (lnum-rows)) 
            
            fg = if @hover ➜ @color.hover ➜ @color.knob
            love.graphics.setColor fg[1]/255 fg[2]/255 fg[3]/255
            love.graphics.rectangle "fill" xo+1 yo+ky+1 int(cw/2) int(kh)
            # love.graphics.circle    "fill" line_x line_y-ch/2+lr lr
            # love.graphics.circle    "fill" line_x line_y+ch/2-lr lr
            
⮐  scroll
