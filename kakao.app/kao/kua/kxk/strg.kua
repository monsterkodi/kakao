###
     ███████  █████████  ████████    ███████ 
    ███          ███     ███   ███  ███      
    ███████      ███     ███████    ███  ████
         ███     ███     ███   ███  ███   ███
    ███████      ███     ███   ███   ███████ 
###

use kxk.array

strbuff = require "string.buffer"

class strg 

    @: ... ->
    
        @buff = nil
        @frags = []
        for v in ...
            @ += $v
                        
    $: ->
    
        @debuff()
        
        ⮐  "" if @frags.len == 0
        
        if @frags.len == 1 
            if type(@frags[1]) == "string"
                ⮐  @frags[1]
            ⮐  @frags[1]∙join()
                
        b = strbuff∙new()
        for f in @frags∙each()
            if type(f) == 'string'
                b∙put f
            else
                b∙put f∙join()
        b∙get()
        
    len: ->
    
        @debuff()
        ⮐  0 if @frags.len == 0
        l = 0
        for f in @frags∙each()
            if type(f) == 'string'
                l += f.len
            else
                for s in f∙each()
                    l += s.len
        l
                    
    endsWith:   o -> 
    
        @flatten()
        kstr.endsWith $@frags[1] o
        
    startsWith: o -> 
    
        @flatten()
        kstr.startsWith $@frags[1] o
    
    rtrim: c=' ' ->
    
        @flatten() 
        @frags[1] = @frags[1]∙rtrim(c)
        @
        
    ltrim: c=' ' -> 
    
        @flatten() 
        @frags[1] = @frags[1]∙ltrim(c)
        @
        
    trim: c=' ' -> 
    
        @flatten() 
        @frags[1] = @frags[1]∙trim(c)
        @
        
    shift: ->
    
        @flatten()
        @frags[1]∙shift()

    pop: ->
    
        @flatten()
        @frags[1]∙pop()

    number: ->
        
        tonumber $@
        
    indent: ->
    
        @flatten()
        @frags[1]∙indent()

    bool: ->
        
        s = $@
        if s == "true"  ➜ ⮐  true
        if s == "false" ➜ ⮐  false
        nil
        
    num: ->
    
        @flatten()
        @frags[1].len
        
    seg: i ->
    
        @flatten()
        @frags[1][i]
        
    each: -> 
    
        @flatten()
        @frags[1]∙each()
        
    find: c ->
    
        @flatten()
        @frags[1]∙find c

    rfind: c ->
    
        @flatten()
        @frags[1]∙rfind c
        
    indexof: s ->
    
        @flatten()
        if s.class != kseg ➜ s = kseg(s)
        @frags[1]∙indexof s

    rindexof: s ->
    
        @flatten()
        if s.class != kseg ➜ s = kseg(s)
        @frags[1]∙rindexof s
        
    slice: f t ->
    
        @flatten()
        s = strg()
        s.frags∙push @frags[1]∙slice(f t)
        s
                
    flatten: ->
    
        @debuff()
        if @frags.len == 0
            ⮐  @
        if @frags.len == 1 
            if type(@frags[1]) == "string"
                @frags = [kseg(@frags[1])]
            ⮐  @
        seg = kseg()
        for f i in @frags∙each()
            if type(f) == "string"
                f = kseg(f)
            seg += f
        @frags = [seg]
        @
        
    debuff: ->
    
        if @buff
            @frags∙push @buff∙get()
            @buff = nil
        @
            
    lines: ->
    
        @debuff()
        ls = []
        for f in @frags∙each()
            if type(f) != "string"
                f = f∙join()             
            sl = kstr.split f "\n"
            for s in sl∙each()
                ls∙push strg(s)
        ls
            
    __add: s ->
    
        if s
            if not @buff
                @buff = strbuff∙new()
            @buff∙put s
        @
    
    __index: k ->    

        if type(k) == "number"
            ⮐  @seg(k)
            
        rawget @ k
        
⮐  strg

