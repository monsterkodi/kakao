###
000   000  00000000  0000000    
000  000   000       000   000  
0000000    0000000   000   000  
000  000   000       000   000  
000   000  00000000  0000000    
###

use ◆ ttio gutter editor status screen cells state scroll consol
use ./util ◆ logfile util
use ../kxk ▪ karg kstr slash
use ../kxk/nfs

args = karg """
    ked [file]
        options                      **
        version    log version       = false
    """

function KED

    @: ->

        if args.version ➜ log '0.0.2'; process.exit 0
            
        @consolRows = 5
                    
        @t = new ttio
        
        @log = new logfile
                
        @screen = new screen @t
        
        @editor  = new editor @screen
        @consol  = new consol @screen
        @gutter  = new gutter @screen @editor.state
        @scroll  = new scroll @screen @editor.state
        @status  = new status @screen @editor.state
        
        @editor.on 'redraw'     @redraw
        @consol.on 'consolRows' @onConsolRows
        
        @mouseHandlers = [@scroll @consol @editor]
        @keyHandlers   = [@consol @editor]
        
        @t.on 'key'    @onKey
        @t.on 'mouse'  @onMouse
        @t.on 'resize' @redraw
        @t.on 'paste'  @editor.onPaste
        @t.on 'wheel'  @editor.onWheel
                                    
        if valid args.options
            @loadFile args.options[0]
        else
            @editor.state.syntax.ext = 'txt'
            @editor.state.loadLines ['']
            @t.setCursor 0 0
            @redraw()
        
    @run: -> new KED()
    
    # 000       0000000    0000000   0000000    
    # 000      000   000  000   000  000   000  
    # 000      000   000  000000000  000   000  
    # 000      000   000  000   000  000   000  
    # 0000000   0000000   000   000  0000000    
        
    reloadFile: => @loadFile @status.file
        
    loadFile: p ○=>
        
        start = process.hrtime()
        
        if slash.isAbsolute p
            @status.file = slash.tilde  p
        else
            @status.file = slash.normalize p

        text =○ nfs.read slash.untilde(p)
        
        lines = text.split /\r?\n/
        
        @editor.state.syntax.ext = slash.ext p
        @editor.state.loadLines lines
        
        @status.drawTime = kstr.time(BigInt(process.hrtime(start)[1]))
        
        @redraw()
            
    #  0000000   0000000   000   000  00000000  
    # 000       000   000  000   000  000       
    # 0000000   000000000   000 000   0000000   
    #      000  000   000     000     000       
    # 0000000   000   000      0      00000000  
    
    saveFile: ○=>
        
        text = @editor.state.s.lines.asMutable().join '\n'
        
        if valid @status.file
            ○ nfs.write slash.untilde(@status.file) text
            @reloadFile()
                                    
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event col row button mods count =>
        
        for handler in @mouseHandlers
            if handler.onMouse event col row button mods count
                ⮐ @redraw()
            
    # 000   000  00000000  000   000  
    # 000  000   000        000 000   
    # 0000000    0000000     00000    
    # 000  000   000          000     
    # 000   000  00000000     000     
    
    onKey: key =>
        
        lc 'key' key
        
        switch key
            
            'alt+q' 'ctrl+d' 'ctrl+q' ➜ ⮐ @t.quit()
            'alt+r' 'ctrl+r' 'cmd+r'  ➜ ⮐ @reloadFile()
            'ctrl+s' 'cmd+s'          ➜ ⮐ @saveFile()
            
        for handler in @keyHandlers
            if handler.onKey key
                ⮐ @redraw()
                        
    # 00000000   00000000  0000000    00000000    0000000   000   000  
    # 000   000  000       000   000  000   000  000   000  000 0 000  
    # 0000000    0000000   000   000  0000000    000000000  000000000  
    # 000   000  000       000   000  000   000  000   000  000   000  
    # 000   000  00000000  0000000    000   000  000   000  00     00  
    
    onConsolRows: @consolRows => @redraw
    
    redraw: =>
        
        start = process.hrtime()
        
        w = @t.cols()
        h = @t.rows()
        
        c = @consolRows
        g = @editor.state.gutterWidth()
        @status.gutter = g
        
        if false # scrollbar on right side
            @scroll.cells.init w-1 0 1 h-c-1
            @gutter.cells.init 0   0 g h-c-1
            @status.cells.init 0   h-1 w 1
            @editor.cells.init g   0 w-g-1 h-c-1
            @consol.cells.init 0   h-1-c w-g-1 c
        else
            @scroll.cells.init 0   0     1     h-c-1 
            @gutter.cells.init 1   0     g     h-c-1
            @status.cells.init 0   h-1   w     1
            @editor.cells.init g+1 0     w-g-1 h-c-1    
            @consol.cells.init 0   h-1-c w-g-1 c
                        
        @t.hideCursor()
        @screen.init()
        @gutter.draw()
        @scroll.draw()
        @status.draw()
        @editor.draw()
        @consol.draw()
        @screen.render()
        @editor.showCursorIfInView() 
        
        @status.drawTime = kstr.time(BigInt(process.hrtime(start)[1]))
        
export KED.run

