###
00000000  0000000    000  000000000   0000000   00000000 
000       000   000  000     000     000   000  000   000
0000000   000   000  000     000     000   000  0000000  
000       000   000  000     000     000   000  000   000
00000000  0000000    000     000      0000000   000   000
###

use ../../kxk ▪ post
use ../util   ◆ util
use ../view   ◆ scroll gutter mapscr mapview
use           ◆ state draw

function editor extends draw

    @: screen name features ->
        
        super screen name features
        
        @state = new state @cells @name
        
        post.on 'focus' @onFocus
        
        if @name == 'editor'
            post.on 'finder.apply' @onFinderApply 
        
        if @feats.scrllr  ➜ @scroll = new scroll  @screen @state
        if @feats.scroll  ➜ @scroll = new scroll  @screen @state
        if @feats.gutter  ➜ @gutter = new gutter  @screen @state
        if @feats.mapscr  ➜ @mapscr = new mapscr  @screen @state; @mapscr.show()
        if @feats.mapview ➜ @mapscr = new mapview @screen @state
                
    # 000  000   000  000  000000000  
    # 000  0000  000  000     000     
    # 000  000 0 000  000     000     
    # 000  000  0000  000     000     
    # 000  000   000  000     000     
    
    layout: x y w h =>
        
        g = m = s = 0
        sl = sr = 0
        
        if @scroll
            s = 1
            if @feats.scrllr
                sr = s
                @scroll.layout x+w-sr y   s   h 
            else
                sl = s
                @scroll.layout x      y   s   h 
            
        if @gutter
            g = @state.gutterWidth()
            @gutter.layout    x+sl    y   g   h
            
        if @mapscr
            m = @mapscr.visible() ? 10 : 0
            r = @mapscr.visible() ? h : 0
            # lf 'editor.layout.mapscr' @name @mapscr.visible() x+w-sr-10 y m r
            @mapscr.layout x+w-sr-10   y   m   r
            
        @cells.layout x+sl+g  y   w-s-g-m    h
        @state.initView()
                
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event =>
        
        ⮐ true if @mapscr?.onMouse? event
        ⮐ true if @scroll?.onMouse  event
        ⮐ true if super             event
        
        [col row] = @cells.posForEvent event
        
        switch event.type
            
            'press'
            
                if @cells.isOutsideEvent event
                    ⮐ if not @gutter?.cells.isInsideEvent event
                        
                if event.count > 1

                    @state.deselect() if not event.shift
                    
                    x = col + @state.s.view[0]
                    y = row + @state.s.view[1]
                    
                    if event.count == 2
                        if event.alt
                            @state.selectChunk x y
                        else
                            @state.selectWord x y
                    else
                        @state.selectLine y
                        
                    @dragStart = copy @state.s.selections[0]
                    
                    true
                    
                else
                    
                    x = col + @state.s.view[0]
                    y = row + @state.s.view[1]
                    
                    @dragStart = [x y x]
                                        
                    @state.deselect() if not event.shift
                    @state.clearCursors() if not event.alt

                    if event.alt
                        @state.addCursor x y
                    else
                        if event.shift and @state.s.cursors.length == 1
                            @state.setMainCursorAndSelect x y
                        else
                            @state.setMainCursor x y
                    
                    @grabFocus()
                    
                    ⮐ true
                    
            'drag'
            
                if @dragStart
                    
                    x = col + @state.s.view[0]
                    y = row + @state.s.view[1]
                    
                    start = [@dragStart[0] @dragStart[1]]
                    
                    if y < @dragStart[1] 
                        start = [@dragStart[2] @dragStart[1]]
                    
                    if event.shift ➜ @state.addRangeToSelectionWithMainCursorAtEnd util.rangeFromStartToEnd(start [x y])
                    else             @state.select start [x y]
                        
                    ⮐ true
                    
            'release'
            
                delete @dragStart
                
            'move'
                
                if @cells.isInsideEvent event
                    post.emit 'pointer' 'text'
                else if @gutter?.cells.isInsideEvent event
                    post.emit 'pointer' 'vertical-text'
                
        false
                
    # 000   000  000   000  00000000  00000000  000      
    # 000 0 000  000   000  000       000       000      
    # 000000000  000000000  0000000   0000000   000      
    # 000   000  000   000  000       000       000      
    # 00     00  000   000  00000000  00000000  0000000  
    
    onWheel: event =>
        
        ⮐ if event.cell[1] >= @cells.y + @cells.rows
        
        if @name == 'editor' # this sucks! consider a separate main editor class
        
            steps  = 1 # should be 4 if not using a mouse pad -> config
            steps *= 2 if event.shift
            steps *= 2 if event.ctrl
            steps *= 2 if event.alt
            
            if @dragStart
                
                [x y] = @state.mainCursor()
                
                switch event.dir
                    'up'    ➜ y -= steps    
                    'down'  ➜ y += steps    
                    'left'  ➜ x -= 1    
                    'right' ➜ x += 1    
                    
                y = clamp 0 @state.s.lines.length-1 y
                x = clamp 0 @state.s.lines[y].length-1 x
                
                start = [@dragStart[0] @dragStart[1]]
                
                if y < @dragStart[1] 
                    start = [@dragStart[2] @dragStart[1]]
                
                if @state.select start [x y]
                    @redraw()
                ⮐ 
                
        
        [col row] = @cells.posForEvent event
        
                
        ⮐ if not @cells.isInsideEvent event
                
        switch event.dir
            'up' 'down' 'left' 'right' ➜ @state.scrollView event.dir steps
            
        @redraw()

    #  0000000  000   000  00000000    0000000   0000000   00000000   
    # 000       000   000  000   000  000       000   000  000   000  
    # 000       000   000  0000000    0000000   000   000  0000000    
    # 000       000   000  000   000       000  000   000  000   000  
    #  0000000   0000000   000   000  0000000    0000000   000   000  
            
    isCursorInEmpty: cursor =>
        
        cursor ?= @state.mainCursor()
        
        util.isLinesPosOutside @state.s.lines cursor
        
    isCursorVisible: cursor =>
        
        cursor ?= @state.mainCursor()
                
        v = @state.s.view
        
        v[0] <= cursor[0] < v[0] + @cells.cols and v[1] <= cursor[1] < v[1] + @cells.rows
        
    # 00000000   0000000    0000000  000   000   0000000  
    # 000       000   000  000       000   000  000       
    # 000000    000   000  000       000   000  0000000   
    # 000       000   000  000       000   000       000  
    # 000        0000000    0000000   0000000   0000000   
    
    grabFocus: =>
    
        post.emit 'focus' @name
        @redraw()
        
    hasFocus: ->
        
        @state.hasFocus
        
    onFocus: name =>
        
        @state.hasFocus = (name == @name)
        
    # 00000000   00000000  0000000    00000000    0000000   000   000  
    # 000   000  000       000   000  000   000  000   000  000 0 000  
    # 0000000    0000000   000   000  0000000    000000000  000000000  
    # 000   000  000       000   000  000   000  000   000  000   000  
    # 000   000  00000000  0000000    000   000  000   000  00     00  
    
    redraw: => post.emit 'redraw'
    
    onFinderApply: text => 
    
        @state.highlightText text
        @state.moveCursorToNextHighlight()
                
    # 000   000  00000000  000   000  
    # 000  000   000        000 000   
    # 0000000    0000000     00000    
    # 000  000   000          000     
    # 000   000  00000000     000     
    
    onKey: key event =>
        
        ⮐ if not @hasFocus()
        
        if @state.s.cursors.length == 1
            
            switch key
                                
                'ctrl+alt+up'               ➜ ⮐ @state.moveCursors 'up'    count:8
                'ctrl+alt+down'             ➜ ⮐ @state.moveCursors 'down'  count:8
                'ctrl+alt+left'             ➜ ⮐ @state.moveCursors 'left'  count:8
                'ctrl+alt+right'            ➜ ⮐ @state.moveCursors 'right' count:8
        
                'shift+ctrl+alt+up'         ➜ ⮐ @state.moveCursors 'up'    count:16
                'shift+ctrl+alt+down'       ➜ ⮐ @state.moveCursors 'down'  count:16
                'shift+ctrl+alt+left'       ➜ ⮐ @state.moveCursors 'left'  count:16
                'shift+ctrl+alt+right'      ➜ ⮐ @state.moveCursors 'right' count:16
        
        switch key

            'up'                        ➜ ⮐ @state.moveCursors 'up'   
            'down'                      ➜ ⮐ @state.moveCursors 'down' 
            'left'                      ➜ ⮐ @state.moveCursors 'left' 
            'right'                     ➜ ⮐ @state.moveCursors 'right'
            
            'cmd+left'  'ctrl+left'     ➜ ⮐ @state.moveCursors ['bos' 'ind_bol']
            'cmd+right' 'ctrl+right'    ➜ ⮐ @state.moveCursors ['eos' 'ind_eol']
            
            'alt+left'                  ➜ ⮐ @state.moveCursors 'left'  jump:['ws' 'word' 'empty' 'punct']
            'alt+right'                 ➜ ⮐ @state.moveCursors 'right' jump:['ws' 'word' 'empty' 'punct']
            'shift+alt+right'           ➜ ⮐ @state.moveCursorsAndSelect 'right' jump:['ws' 'word' 'empty' 'punct']
            'shift+alt+left'            ➜ ⮐ @state.moveCursorsAndSelect 'left'  jump:['ws' 'word' 'empty' 'punct']
            
            'shift+up'                  ➜ ⮐ @state.moveCursorsAndSelect 'up'
            'shift+down'                ➜ ⮐ @state.moveCursorsAndSelect 'down'
            'shift+left'                ➜ ⮐ @state.moveCursorsAndSelect 'left'
            'shift+right'               ➜ ⮐ @state.moveCursorsAndSelect 'right'
            'shift+cmd+right'           ➜ ⮐ @state.moveCursorsAndSelect 'ind_eol'
            'shift+cmd+left'            ➜ ⮐ @state.moveCursorsAndSelect 'ind_bol'
            'shift+ctrl+h'              ➜ ⮐ @state.moveCursorsAndSelect 'bof'
            'shift+ctrl+j'              ➜ ⮐ @state.moveCursorsAndSelect 'eof'
                        
            'shift+alt+cmd+up'          ➜ ⮐ @state.moveMainCursorInDirection 'up'    keep:true # 'paint' cursors
            'shift+alt+cmd+down'        ➜ ⮐ @state.moveMainCursorInDirection 'down'  keep:true # 'paint' cursors
            'shift+alt+cmd+left'        ➜ ⮐ @state.moveMainCursorInDirection 'left'  keep:true # 'paint' cursors
            'shift+alt+cmd+right'       ➜ ⮐ @state.moveMainCursorInDirection 'right' keep:true # 'paint' cursors
            
            'alt+up'                    ➜ ⮐ @state.moveSelectionOrCursorLines 'up'
            'alt+down'                  ➜ ⮐ @state.moveSelectionOrCursorLines 'down'
            'shift+alt+up'              ➜ ⮐ @state.cloneSelectionAndCursorLines 'up'
            'shift+alt+down'            ➜ ⮐ @state.cloneSelectionAndCursorLines 'down'

            'cmd+up'   'ctrl+up'               ➜ ⮐ @state.expandCursors   'up'
            'cmd+down' 'ctrl+down'             ➜ ⮐ @state.expandCursors   'down'
            'shift+cmd+up'   'shift+ctrl+up'   ➜ ⮐ @state.contractCursors 'up'
            'shift+cmd+down' 'shift+ctrl+down' ➜ ⮐ @state.contractCursors 'down'

            'pageup'                    ➜ ⮐ @state.singleCursorPage 'up'
            'pagedown'                  ➜ ⮐ @state.singleCursorPage 'down'

            'home'                      ➜ ⮐ @state.singleCursorAtIndentOrStartOfLine()
            'end'                       ➜ ⮐ @state.singleCursorAtEndOfLine()
            
            'ctrl+h'                    ➜ ⮐ @state.setMainCursor 0 0
            'ctrl+j'                    ➜ ⮐ @state.setMainCursor @state.s.lines[@state.s.lines.length-1].length @state.s.lines.length-1
            
            'alt+d'                     ➜ ⮐ @state.delete 'next' true
            'shift+ctrl+k' 'entf'       ➜ ⮐ @state.delete 'next'
            'ctrl+k'                    ➜ ⮐ @state.delete 'eol'
            'delete'                    ➜ ⮐ @state.delete 'back'
            'ctrl+delete'               ➜ ⮐ @state.delete 'back' true
            'cmd+delete'                ➜ ⮐ @state.delete 'back' true
            
            'shift+tab'                 ➜ ⮐ @state.deindentSelectedOrCursorLines()
            'tab'                       ➜ ⮐ @state.insert '\t'
                                    
            'alt+x' 'cmd+x' 'ctrl+x'    ➜ ⮐ @state.cut()     
            'alt+c' 'cmd+c' 'ctrl+c'    ➜ ⮐ @state.copy()        
            'alt+v' 'cmd+v' 'ctrl+v'    ➜ ⮐ @state.paste()
                           
            'cmd+z' 'ctrl+z'            ➜ ⮐ @state.undo()
            'shift+cmd+z'
            'cmd+y' 'ctrl+y'            ➜ ⮐ @state.redo()
                        
            'cmd+a'       'ctrl+a'       ➜ ⮐ @state.selectAllLines()
            'cmd+j'       'ctrl+j'       ➜ ⮐ @state.joinLines()
            'cmd+l'       'ctrl+l'       ➜ ⮐ @state.selectMoreLines()
            'shift+cmd+l' 'shift+ctrl+l' ➜ ⮐ @state.selectLessLines()

            
            'cmd+e' 'ctrl+e'             ➜ ⮐ @state.highlightWordAtCursor_deselectCursorHighlight_moveCursorToNextHighlight()
            'cmd+d' 'ctrl+d'             ➜ ⮐ @state.selectWordAtCursor_highlightSelection_addNextHighlightToSelection()
            'cmd+g' 'ctrl+g'             ➜ ⮐ @state.selectWordAtCursor_highlightSelection_selectNextHighlight()
            
            'shift+cmd+e' 'shift+ctrl+e' ➜ ⮐ @state.highlightWordAtCursor_deselectCursorHighlight_moveCursorToPrevHighlight()
            'shift+cmd+d' 'shift+ctrl+d' ➜ ⮐ @state.selectWordAtCursor_highlightSelection_addPrevHighlightToSelection()
            'shift+cmd+g' 'shift+ctrl+g' ➜ ⮐ @state.selectWordAtCursor_highlightSelection_selectPrevHighlight()

            'alt+cmd+d' 'alt+ctrl+d'    ➜ ⮐ @state.selectWordAtCursor_highlightSelection_selectAllHighlights()

            'cmd+/' 'ctrl+/'            ➜ ⮐ @state.toggleCommentAtSelectionOrCursorLines()
            
            'esc'                       ➜ ⮐ @state.clearCursorsHighlightsAndSelections()

        if valid event.char
            @state.insert event.char
        else
            if key not in ['shift' 'ctrl' 'alt' 'cmd'] 
                lfc 'editor.onKey?' key

export editor
