###
000   000  00000000  0000000    
000  000   000       000   000  
0000000    0000000   000   000  
000  000   000       000   000  
000   000  00000000  0000000    

    bun --watch js/ked/ked.js
###

use ../kxk        ▪ karg kstr kseg slash post
use ../kxk        ◆ nfs
use ./index       ◆ prjcts indexer
use ./util        ◆ logfile session help frecent watcher git
use ./edit/tool   ◆ belt
use ./edit        ◆ mode
use ./view/base   ◆ view
use ./view/status ◆ status 
use ./view/editor ◆ fileeditor
use ./view/screen ◆ screen ttio
use ./view/menu   ◆ context menu macro finder searcher differ 
use ./view/colmns ◆ browse dircol funcol quicky

global.int = parseInt # this should go into kode instead

function KED

    @: ->

        @version = '0.8.0'
        @args = karg """
            
            ked [file]
                options                                       **
                new        start with empty buffer          = false
                fresh      don't load previous session      = false
                timeout    session save timeout in ms       = 1000
                version    log version                      = false
                
            """ preHelp:help.header() version:@version
        
        process.on 'uncaughtException' @quit
            
        @session = new session @args
        @logfile = new logfile @session.name
        
        global.ked_session = @session
        
        @session.on 'loaded' @onSessionLoaded
        
        @t = new ttio
        
        @screen   = new screen @t
                  
        @indexer  = new indexer
        @git      = new git
        
        @menu     = new menu       @screen        
        @macro    = new macro      @screen        
        @quicky   = new quicky     @screen 
        @browse   = new browse     @screen 
        @editor   = new fileeditor @screen 'editor'
        @dircol   = new dircol     @screen 'dircol'  ['scroll' 'knob']
        @funcol   = new funcol     @screen 'funcol'  ['scroll' 'knob']
        @finder   = new finder     @screen @editor.state
        @searcher = new searcher   @screen @editor.state
        @differ   = new differ     @screen #@editor.state
        @status   = new status     @screen @editor.state
        @context  = new context    @screen
        
        log w2("┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━ #{b8 @session.name} ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓")

        @editor.state.hasFocus = true
        
        post.on 'redraw'        @redraw
        post.on 'window.focus'  @redraw
        post.on 'window.blur'   @redraw
        post.on 'view.size'     @onViewSize
        post.on 'quicky'        @onQuicky
        post.on 'file.new'      @newFile
        post.on 'file.reload'   @reloadFile
        post.on 'file.open'     @openFile
        post.on 'quit'          @quit
        post.on 'file.change'   @onFileChange
        post.on 'focus'         (name) -> # log "focus: '#{name}'"
        
        @contextHandlers = [                                                                 @editor         @dircol @funcol ]
        @mouseHandlers   = [ @context @finder @searcher @differ @quicky @browse @menu @macro @editor @status @dircol @funcol ]
        @wheelHandlers   = [          @finder @searcher @differ @quicky @browse       @macro @editor         @dircol @funcol ]
        @keyHandlers     = [ @context @finder @searcher @differ @quicky @browse @menu @macro @editor         @dircol @funcol ]
        
        @t.on 'key'    @onKey
        @t.on 'mouse'  @onMouse
        @t.on 'wheel'  @onWheel
        @t.on 'resize' @onResize
        @t.on 'paste'  @onPaste
                                    
        # log 'ked args:' @args
        
        ked_session.set 'ked▸args' @args
        
        if valid @args.options
            @loadFile @args.options[0]
        else if @args.fresh
            @hideEditor()
            @menu.show true
        else if @args.new
            @newFile()
            
    showEditor: -> 
    
        @editor.show()
        @status.show()
        @dircol.show()
        @funcol.show()

    hideEditor: -> 
    
        @editor.hide()
        @status.hide()
        @dircol.hide()
        @funcol.hide()
        
    # 00000000   000   000  000   000  
    # 000   000  000   000  0000  000  
    # 0000000    000   000  000 0 000  
    # 000   000  000   000  000  0000  
    # 000   000   0000000   000   000  
    
    @run: -> new KED()

    #  ███████  ████████   ███████   ███████  ███   ███████   ███   ███
    # ███       ███       ███       ███       ███  ███   ███  ████  ███
    # ███████   ███████   ███████   ███████   ███  ███   ███  ███ █ ███
    #      ███  ███            ███       ███  ███  ███   ███  ███  ████
    # ███████   ████████  ███████   ███████   ███   ███████   ███   ███

    onSessionLoaded: =>
    
        ⮐  if @args.fresh
        ⮐  if @args.new
        
        if empty @currentFile and empty @loadingFile
            if file = ked_session.get "editor▸file"
                # log 'no current or loading file ... loading last session file'
                @loadFile file
            else
                @hideEditor()
                @menu.show true
                
    #  ███████   ████████   ████████    ███████   ███   ███   ███████   ████████
    # ███   ███  ███   ███  ███   ███  ███   ███  ████  ███  ███        ███     
    # █████████  ███████    ███████    █████████  ███ █ ███  ███  ████  ███████ 
    # ███   ███  ███   ███  ███   ███  ███   ███  ███  ████  ███   ███  ███     
    # ███   ███  ███   ███  ███   ███  ███   ███  ███   ███   ███████   ████████

    arrange: =>
        
        w = @t.cols()
        h = @t.rows()
        
        dcw = @dircol.cells.cols
        fcw = @funcol.cells.cols
        
        dcw = 0 if @dircol.hidden() or not @dircol.active
        fcw = 0 if @funcol.hidden() or not @funcol.active
        
        if @viewSizeDelta
            switch @viewSizeDelta.name
                @dircol.name ➜ dcw = @dircol.cells.cols + @viewSizeDelta.delta
                @funcol.name ➜ fcw = @funcol.cells.cols + @viewSizeDelta.delta
            delete @viewSizeDelta
        
        @dircol.layout  0     0  dcw        h    if @dircol.visible() and @dircol.active
        @funcol.layout  w-fcw 1  fcw        h-1  if @funcol.visible() and @funcol.active
        @status.layout  dcw   0  w-dcw      1
        @editor.layout  dcw   1  w-dcw-fcw  h-1
    
    #  ███████   ███   ███  ███  █████████
    # ███   ███  ███   ███  ███     ███   
    # ███ ██ ██  ███   ███  ███     ███   
    # ███ ████   ███   ███  ███     ███   
    #  █████ ██   ███████   ███     ███   

    quit: msg ○=>
        
        clearImmediate @redrawId
        
        @quitting = true
        
        ○ @session.save()
        
        log w2("┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━ #{b8 @session.name} ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛")

        if msg?
            error msg.stack if msg.stack
            error "#{y5 msg}"
        
        @t?.quit()
        
        @logfile.close ->
            
            process.stderr.write "#{r4 msg.stack}\n" if msg?.stack
            process.stderr.write "#{y5 msg}" if msg?
            process.exit msg? ? 1 : 0
            
    # 000   000  00000000  000   000  
    # 0000  000  000       000 0 000  
    # 000 0 000  0000000   000000000  
    # 000  0000  000       000   000  
    # 000   000  00000000  00     00  
    
    newFile: =>
        
        delete @currentFile

        @status.setFile ''
    
        @editor.state.syntax.ext = 'txt'
        @editor.state.loadLines ['']
        @t.setCursor 0 0
        @t.setTitle 'kėd'
        
        @editor.mapscr?.reload()

        @showEditor()
        @redraw()
            
    # ████████  ███  ███      ████████   ███████  ███   ███   ███████   ███   ███   ███████   ████████
    # ███       ███  ███      ███       ███       ███   ███  ███   ███  ████  ███  ███        ███     
    # ██████    ███  ███      ███████   ███       █████████  █████████  ███ █ ███  ███  ████  ███████ 
    # ███       ███  ███      ███       ███       ███   ███  ███   ███  ███  ████  ███   ███  ███     
    # ███       ███  ███████  ████████   ███████  ███   ███  ███   ███  ███   ███   ███████   ████████

    onFileChange: event =>
    
        if event.path == @currentFile
            switch event.change
                'delete' ➜ @newFile() # todo: load current text into new file
                'change'
                'rename' ➜ @reloadFile()
        
    # ████████   ████████  ███       ███████    ███████   ███████  
    # ███   ███  ███       ███      ███   ███  ███   ███  ███   ███
    # ███████    ███████   ███      ███   ███  █████████  ███   ███
    # ███   ███  ███       ███      ███   ███  ███   ███  ███   ███
    # ███   ███  ████████  ███████   ███████   ███   ███  ███████  

    reloadFile: => 
        
        if valid @currentFile
            @loadFile @currentFile
        else
            @newFile()
            
    #  0000000   00000000   00000000  000   000  
    # 000   000  000   000  000       0000  000  
    # 000   000  00000000   0000000   000 0 000  
    # 000   000  000        000       000  0000  
    #  0000000   000        00000000  000   000  
    
    openFile: path row col view =>
        
        @loadFile path row col view
        @editor.grabFocus()
        
    #  0000000   000   000  000   0000000  000   000  000   000  
    # 000   000  000   000  000  000       000  000    000 000   
    # 000 00 00  000   000  000  000       0000000      00000    
    # 000 0000   000   000  000  000       000  000      000     
    #  00000 00   0000000   000   0000000  000   000     000     
    
    onQuicky: path ○=>
        
        ⮐  if valid @loadingFile
        
        if slash.isAbsolute path
            file = path
        else
            file = slash.absolute path slash.dir(@currentFile)
        
        ⮐  if slash.samePath(file @currentFile)
        
        ○ @loadFile file
                                
    # ███       ███████    ███████   ███████          ████████  ███  ███      ████████
    # ███      ███   ███  ███   ███  ███   ███        ███       ███  ███      ███     
    # ███      ███   ███  █████████  ███   ███        ██████    ███  ███      ███████ 
    # ███      ███   ███  ███   ███  ███   ███        ███       ███  ███      ███     
    # ███████   ███████   ███   ███  ███████          ███       ███  ███████  ████████

    loadFile: p row col view ○=>
    
        # log "loadFile #{p} #{row} #{col} #{noon view}"
        
        start = process.hrtime()
        
        if slash.isAbsolute p
            absFile = slash.absolute p
        else
            absFile = slash.path process.cwd() p
            
        ⮐ if slash.samePath absFile @loadingFile
        
        @loadingFile = absFile
        
        exists =○ nfs.fileExists @loadingFile
        
        if not exists
            log "file doesn't exist! #{@loadingFile}"
            delete @loadingFile
            ⮐ 
        
        @loadingFile =○ nfs.resolveSymlink @loadingFile
        
        text =○ nfs.readText @loadingFile
        
        @currentFile = @loadingFile

        delete @loadingFile
        
        @status.setFile slash.tilde(@currentFile)
        
        global.ked_editor_file = @currentFile
        
        if text == undefined
            text = '○ binary ○'
        
        [colors segls] = belt.colorSeglsForText text
        
        if valid colors
            @editor.state.syntax.setColors colors
        else
            @editor.state.syntax.setExt slash.ext(@currentFile)
            
        @editor.state.loadSegls segls
        
        @status.time = process.hrtime(start)[1]
        
        @editor.mapscr?.reload()
        
        ked_session.set "editor▸file" @currentFile
        
        mode.fileLoaded @editor.state @currentFile row col view
        
        post.emit 'file.loaded' @currentFile
        
        @showEditor()
        @redraw()
        
        @indexer.index @currentFile
        prjcts.index   @currentFile
        
        watcher.watch  @currentFile
                
        @t.setTitle slash.name(@status.file)
        
        @saveSessionFile @currentFile 'loaded'        
        
        if gitDir =○ git.dir @currentFile
            watcher.watch slash.path(gitDir '.git/refs/heads') recursive:false
            
        @
                    
    #  0000000   0000000   000   000  00000000  
    # 000       000   000  000   000  000       
    # 0000000   000000000   000 000   0000000   
    #      000  000   000     000     000       
    # 0000000   000   000      0      00000000  
    
    saveFile: ○=>
        
        text = kseg.str @editor.state.s.lines
        
        if valid @currentFile
            ○ nfs.write @currentFile text
            @editor.state.clearHistory() # lazy way to 'undirty' :> no undo after save :(
            @redraw()
            @saveSessionFile @currentFile 'saved'
            
    saveSessionFile: file type =>
        
        frecent.fileAction file type
                
        @session.set 'files▸recent' frecent.store('file')
            
    saveAs: => log 'todo: saveAs'
    
    # 00000000    0000000    0000000  000000000  00000000  
    # 000   000  000   000  000          000     000       
    # 00000000   000000000  0000000      000     0000000   
    # 000        000   000       000     000     000       
    # 000        000   000  0000000      000     00000000  
    
    onPaste: text => 
    
        @editor.state.insert text
        @redraw()
                                    
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event =>

        if event.type == 'wheel'

            for handler in @wheelHandlers
                if ret = handler.onWheel event
                    if ret.redraw == true
                        @redraw()
                    ⮐ 
            ⮐ 

        redraw = false
        
        for handler in @mouseHandlers
        
            if ret = handler.onMouse event
                event.handled = true
                if ret.redraw == true
                    redraw = true
                break if event.type != 'move' or handler.isPopup

        if event.button == 'right' and event.type == 'press' and event.count == 1
            
            for handler in @contextHandlers
                continue if not handler.hover
                if ret = handler.onContext event
                    if ret.redraw == true
                        redraw = true
                    break

        @redraw() if redraw
        
    showFinderOrSearcher: =>
    
        if @finder.visible() and valid @finder.input.current()
            @searcher.show @finder.input.current()
        else
            @finder.show()
                
    # 000   000  00000000  000   000  
    # 000  000   000        000 000   
    # 0000000    0000000     00000    
    # 000  000   000          000     
    # 000   000  00000000     000     
    
    onKey: key event =>
            
        switch key
            
            'alt+q' 'ctrl+q' 'cmd+esc'   ➜ ⮐  @quit()
            'alt+r' 'ctrl+r' 'cmd+r'     ➜ ⮐  @reloadFile()
            'ctrl+s' 'cmd+s'             ➜ ⮐  @saveFile()
            'shift+cmd+s' 'shift+ctrl+s' ➜ ⮐  @saveAs()
            'cmd+n' 'ctrl+n'             ➜ ⮐  @newFile()
            'alt+.' 'alt+m'              ➜ ⮐  @menu.show()
            'cmd+p' 'ctrl+p'             ➜ ⮐  @quicky.showProjectFiles @currentFile
            'cmd+f' 'ctrl+f'             ➜ ⮐  @showFinderOrSearcher()
            'shift+cmd+f' 'shift+ctrl+f' ➜ ⮐  @searcher.show()
            'alt+o'                      ➜ ⮐  @editor.jumpToCounterpart()
            'cmd+o' 'ctrl+o'
            'cmd+m' 'cmd+;' 'ctrl+;'     ➜ ⮐  @macro.show()
            'cmd+.' 'ctrl+.'             ➜ ⮐  @browse.gotoDir slash.dir(@currentFile)
            "cmd+'" "ctrl+'"             ➜ ⮐  post.emit 'funcol.toggle'
            "cmd+\\" "ctrl+\\"           ➜ ⮐  post.emit 'dircol.toggle'
            
        for handler in @keyHandlers
            # log "#{handler.name}"
            # ●▸ on key
            continue if handler.hidden()
            break if result = handler.onKey key event
            # ●▪ on key
                
        @redraw() if result?.redraw != false
        
    #  0000000  000  0000000  00000000  
    # 000       000     000   000       
    # 0000000   000    000    0000000   
    #      000  000   000     000       
    # 0000000   000  0000000  00000000  
    
    onViewSize: name side delta =>
        
        @viewSizeDelta = name:name side:side delta:delta
        @redraw()
        
    onResize: cols rows size => 
    
        mcw = int cols/6
        
        if mcw >= 16
            @dircol.cells.cols = mcw
            @funcol.cells.cols = mcw
            @dircol.show()
            @funcol.show()
        else
            @dircol.hide()
            @funcol.hide()
        
        @redraw()
        @editor.mapscr?.onResize()
    
    # ███████    ████████    ███████   ███   ███
    # ███   ███  ███   ███  ███   ███  ███ █ ███
    # ███   ███  ███████    █████████  █████████
    # ███   ███  ███   ███  ███   ███  ███   ███
    # ███████    ███   ███  ███   ███  ██     ██

    redraw: =>    
        
        ⮐  if @quitting

        clearImmediate @redrawId
        @redrawId = setImmediate @draw
                                      
    draw: =>
            
        ⮐  if @quitting
        
        # ● sum
        
        start = process.hrtime()
        
        @status.gutter = @editor.state.gutterWidth()
        
        @screen.init()
        
        @arrange()
        
        # ●▸ draw
        if @menu.greet.hidden()
            @editor.draw()
            @status.draw()
            @dircol.draw()
            @funcol.draw()
            
        @menu.draw()
        @macro.draw()
        @quicky.draw()
        @browse.draw()
        @finder.draw()
        @searcher.draw()
        @differ.draw()
        @context.draw()
        # ●▪ draw
        # ●▸ render
        @screen.render()
        # ●▪ render
        
        @status.time = process.hrtime(start)[1]
        
export KED.run

◆main

    KED.run()

