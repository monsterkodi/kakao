###
000   000   0000000   00000000   000      0000000  
000 0 000  000   000  000   000  000      000   000
000000000  000   000  0000000    000      000   000
000   000  000   000  000   000  000      000   000
00     00   0000000   000   000  0000000  0000000  
###

use ../kxk ▪ $ drag stopEvent prefs post fade randInt

use ◆ gee tweaky tube

COL_SHADOW  = [0 0 0 0.1]
COL_BG      = [0.15 0.15 0.15 1]
COL_GRID    = [0 0 0 0.5]
COL_TUBE    = [0.5 0.5 0.5 1]
COL_EGG     = [1 1 1 0.5]
COL_CRITTER = [1 0.5 0 0.5]

threshMold  = p n m ->
    
    pi = parseInt p/m
    pn = parseInt n/m
    
    pi != pn

function world

    @: -> 
        
        @main =$ 'main'
        @pause = false
        
        @ws = 10
        
        @main.focus()
        
        @tubes    = []
        @eggs     = []
        @critters = []
        
        @critterMaxAge    = 127
        @critterEggPeriod = 19
        @eggMaxAge        = 7
        
        @addEgg @ws/2 @ws/2
        
        @g = new gee @main
        
        @g.camScale = 0.08
        @g.camPosX  = 1 / @g.camScale
        @g.camPosY  = 1 / @g.camScale
        
        @main.addEventListener 'mousemove' @onMouseMove
        
        @g.updateCamera()
        
        # @tweaky = new tweaky @main
#         
        # @tweaky.init 
            # side:      min: 10    max:200    step:1           value:@g.side,     cb: @g.setSide
            
        window.addEventListener 'wheel'       @onWheel
        @main.addEventListener  'contextmenu' @onContextMenu
        
        s = 82/4096
        @tubeUV = [
            [ s*1 s*2 s*2 s*3 ] # right / left
            [ s*2 s*0 s*3 s*1 ] # right down / up left
            [ s*2 s*1 s*3 s*2 ] # down / up
            [ s*2 s*2 s*3 s*3 ] # down left / right up
            [ s*0 s*2 s*1 s*3 ] # down right / left up
            [ s*0 s*0 s*1 s*1 ] # up right / left down
            ]
            
        @quadUV    = [ (4096-80)/4096, (4096-80)/4096, (4096-2)/4096, (4096-2)/4096 ]
        
        @eggUV     = [ s*3.5, s*0.5, s*5.5, s*2.5 ]
        @critterUV = [ (4096-80)/4096, (4096-80)/4096, (4096-2)/4096, (4096-2)/4096 ]
        
        @circleUV = [
            [ s*3.5, s*0.5, s*4.5, s*1.5 ]
            [ s*4.5, s*0.5, s*5.5, s*1.5 ]
            [ s*4.5, s*1.5, s*5.5, s*2.5 ]
            [ s*3.5, s*1.5, s*4.5, s*2.5 ]
        ]
                
        @drag = new drag 
            target:  @g.canvas
            onStart: @onDragStart
            onMove:  @onDragMove
            onStop:  @onDragStop
            cursor: 'pointer'
        
    # 000   000  000   000  00000000  00000000  000      
    # 000 0 000  000   000  000       000       000      
    # 000000000  000000000  0000000   0000000   000      
    # 000   000  000   000  000       000       000      
    # 00     00  000   000  00000000  00000000  0000000  
    
    onWheel: event =>

        if event.ctrlKey or event.metaKey
            
            # log '●'  @g.camPosX, @g.camPosY
            
            @g.camScale -= event.deltaY/(event.metaKey ? 40000 : 4000)
            @g.camScale = clamp 0.01 0.2 @g.camScale
            
        else
            
            @g.camPosX += event.deltaX / (4000 * @g.camScale) 
            @g.camPosY -= event.deltaY / (4000 * @g.camScale) 
            
        @g.camPosX = clamp 0, @ws, @g.camPosX
        @g.camPosY = clamp 0, @ws, @g.camPosY
        
        @g.updateCamera()
        
        # log '◂' @g.camPosX, @g.camPosY
        
    eventPos: event => x:event.clientX - @g.br.left, y:event.clientY - @g.br.top
        
    onMouseMove: event =>
        
        winPos = @eventPos event
        
        @mouse = 
            grid: @win2Grid winPos
            pos:  @win2Pos winPos
            win:  winPos
        
        # log @mouse
        
    # 0000000    00000000    0000000    0000000   
    # 000   000  000   000  000   000  000        
    # 000   000  0000000    000000000  000  0000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000   0000000   
    
    win2Pos: winPos =>
        
        x = (((winPos.x - @g.br.left) / @g.br.width - 0.5) *  2 ) / (@g.camScale * @g.aspect) + @g.camPosX
        y = (((winPos.y - @g.br.top) / @g.br.height - 0.5) * -2 ) /  @g.camScale + @g.camPosY
        
        [x,y]
    
    win2Grid: winPos =>
        
        [x, y] = @win2Pos winPos
        
        x = clamp 0 @ws-1, Math.round x
        y = clamp 0 @ws-1, Math.round y
        
        [x,y]
        
    mouseInWorld: => @isInWorld @mouse.pos
        
    isInWorld: p => p[0] >= 0 and p[1] >= 0 and p[0] < @ws and p[1] < @ws
    isEmpty:   p =>
        
        for o in @critters ➜ ⮐ false if o.x == p[0] and o.y == p[1] 
        for o in @eggs     ➜ ⮐ false if o.x == p[0] and o.y == p[1] 
        true
        
    emptyNeighbor: c =>
        
        for x in -1..1
            for y in -1..1
                continue if x == y == 0
                c = [c.x+x, c.y+y]
                continue if not @isInWorld c
                if @isEmpty c
                    ⮐ x:c[0], y:c[1]
        null
    
    onDragStart: drag event =>
        
        ⮐ if not @mouseInWorld()
        
        log @mouse.pos
        
        @dragPath = [@win2Grid(drag.pos), @win2Grid(drag.pos)]
        
    onDragMove: drag event =>
        
        ⮐ if not @dragPath
        
        l = @dragPath[-1]
        p = @dragPath[-2]
        g = @win2Grid drag.pos
        
        if g[0] == l[0] == p[0] and g[1] == l[1] == p[1] and @dragPath.length > 2
            @dragPath.pop()
        else if l[0] == p[0] == g[0]
            l[1] = g[1]
        else if l[1] == p[1] == g[1]
            l[0] = g[0]
        else
            @dragPath.push g
            if l[0] != g[0] and l[1] != g[1]
                if l[0] == p[0]
                    l[1] = g[1]
                else
                    l[0] = g[0]
                    
    onDragStop: drag event =>
        
        if @dragPath
            tube.path @dragPath, @addTube
            delete @dragPath
            
    onContextMenu: (event) => stopEvent event
        
    togglePause:  => @pause = not @pause; post.emit 'pause'
    toggleValues: => 
        
    #  0000000  000  00     00  000   000  000       0000000   000000000  00000000  
    # 000       000  000   000  000   000  000      000   000     000     000       
    # 0000000   000  000000000  000   000  000      000000000     000     0000000   
    #      000  000  000 0 000  000   000  000      000   000     000     000       
    # 0000000   000  000   000   0000000   0000000  000   000     000     00000000  
    
    simulate: tickInfo ->
        
        ⮐ if @pause and not @oneStep
        ⮐ if isNaN tickInfo.delta
        
        # ● simulate
        
        delete @oneStep
        
        sec = tickInfo.delta / 1000
        
        # log sec, @eggs
        
        for e in @eggs
            e.age += sec
            if e.age > @eggMaxAge
                @eggs.splice @eggs.indexOf(e), 1
                @addCritter e.x, e.y
                
        for c in @critters
            c.age += sec
            if c.age > @critterMaxAge
                @critters.splice @critters.indexOf(c), 1
                continue
                
            if threshMold c.age-sec, c.age, @critterEggPeriod
                if n = @emptyNeighbor c
                    @addEgg n.x, n.y
                else
                    log 'no place for egg'

            moveTarget = [c.x + randInt(3)-1, c.y + randInt(3)-1]
            if @isInWorld(moveTarget) and @isEmpty(moveTarget)
                c.x = moveTarget[0]
                c.y = moveTarget[1]
                
    singleStep: => @oneStep = true; @pause = true; post.emit 'pause'
    
    addEgg: x y => @eggs.push x:x, y:y, age:0
    
    addCritter: x y => @critters.push x:x, y:y, age:0
    
    # 000000000  000   000  0000000    00000000  
    #    000     000   000  000   000  000       
    #    000     000   000  0000000    0000000   
    #    000     000   000  000   000  000       
    #    000      0000000   0000000    00000000  
    
    addTube: x y idx =>
        
        @tubes.push [x, y, idx]
    
    drawTube: x y idx =>

        @g.addQuad x+0.2, y-0.2,  1 1   COL_SHADOW, @tubeUV[idx], 0 0
        @g.addQuad x,     y,      1 1   COL_TUBE,   @tubeUV[idx], 0 1
        
    drawEgg: e =>
        
        s = fade 0.2 1 e.age / @eggMaxAge
        @g.addQuad e.x, e.y, s, s, COL_EGG, @eggUV, 0 1

    drawCritter: c =>
        
        s = fade 0.2 1 c.age / @critterMaxAge
        @g.addQuad c.x, c.y, s, s, COL_CRITTER, @critterUV, 0 1
        
    roundedQuadRect: x0 y0 x1 y1 color layer=0 =>
        
        [x0, x1] = [min(x0, x1), max(x0, x1)]
        [y0, y1] = [min(y0, y1), max(y0, y1)]
        
        @g.addQuad x0, y0, 1 1  color, @circleUV[3], 0 layer
        @g.addQuad x1, y0, 1 1  color, @circleUV[2], 0 layer
        @g.addQuad x1, y1, 1 1  color, @circleUV[1], 0 layer
        @g.addQuad x0, y1, 1 1  color, @circleUV[0], 0 layer
        
        @g.addQuad (x0+x1)/2, (y0+y1)/2, (x1-x0)-1, (y1-y0)+1, color, @quadUV, 0 layer 
        @g.addQuad  x0,       (y0+y1)/2, 1,         (y1-y0)-1, color, @quadUV, 0 layer 
        @g.addQuad  x1,       (y0+y1)/2, 1,         (y1-y0)-1, color, @quadUV, 0 layer 
        
    gridQuadRect: x0 y0 x1 y1 color layer=0 w=0.02 =>
        
        [x0, x1] = [min(x0, x1), max(x0, x1)]
        [y0, y1] = [min(y0, y1), max(y0, y1)]
        
        sx = x1 - x0
        sy = y1 - y0
        
        for x in 0..sx
            @g.addQuad x, sx/2, w, sx, [0 0 0 0.15], @quadUV, 0 layer

        for y in 0..sy
            @g.addQuad sy/2, y, sy, w, [0 0 0 0.15], @quadUV, 0 layer 
        
    # 000000000  000   0000000  000   000  
    #    000     000  000       000  000   
    #    000     000  000       0000000    
    #    000     000  000       000  000   
    #    000     000   0000000  000   000  
    
    tick: @tickInfo ->
        
        @simulate @tickInfo
                
        @roundedQuadRect  0    -0.5  @ws-0.5  @ws-1,   COL_SHADOW
        @roundedQuadRect -0.25 -0.25 @ws-0.75 @ws-0.75 COL_BG
        @gridQuadRect     0     0    @ws-1,   @ws-1,   COL_GRID
                
        if @dragPath ➜ tube.path @dragPath, @drawTube
            
        for t in @tubes    ➜ @drawTube t[0], t[1], t[2]
        for c in @critters ➜ @drawCritter c
        for e in @eggs     ➜ @drawEgg e
                    
        @g.draw @tickInfo.time
        
export world
