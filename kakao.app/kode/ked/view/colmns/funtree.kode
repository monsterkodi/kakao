#    ████████  ███   ███  ███   ███  █████████  ████████   ████████  ████████  
#    ███       ███   ███  ████  ███     ███     ███   ███  ███       ███       
#    ██████    ███   ███  ███ █ ███     ███     ███████    ███████   ███████   
#    ███       ███   ███  ███  ████     ███     ███   ███  ███       ███       
#    ███        ███████   ███   ███     ███     ███   ███  ████████  ████████  

use ../../../kxk ▪ post slash
use ../../../kxk ◆ nfs
use ../../theme  ◆ color theme icons
use ../menu      ◆ choices 

###
     ███████  ███   ███  ███   ███  █████████   ███████   ███   ███
    ███        ███ ███   ████  ███     ███     ███   ███   ███ ███ 
    ███████     █████    ███ █ ███     ███     █████████    █████  
         ███     ███     ███  ████     ███     ███   ███   ███ ███ 
    ███████      ███     ███   ███     ███     ███   ███  ███   ███
###

function funSyntax

    @: @tree ->
    
        @color =
            class: theme.funtree_class
            async: theme.funtree_async
            bound: theme.funtree_bound
            func:  theme.funtree_func
        
    clear: =>

    setLines: lines => log 'setLines'
    setSegls: segls =>
        
    #  ███████   ███████   ███       ███████   ████████ 
    # ███       ███   ███  ███      ███   ███  ███   ███
    # ███       ███   ███  ███      ███   ███  ███████  
    # ███       ███   ███  ███      ███   ███  ███   ███
    #  ███████   ███████   ███████   ███████   ███   ███

    getColor: x y =>
        
        item = @tree.items[y]
        name = item.name
        char = name[x]
        if char == ' ' ➜ ⮐ '#000'
            
        if 
            not item.async? ➜ clr = @color.class
            item.async      ➜ clr = @color.async
            item.bound      ➜ clr = @color.bound
                            ➜ clr = @color.func
        if 
            item.static     ➜ clr = color.brighten clr 0.2
            char == '@' and empty name[x+1]
                              clr = @color.class
        if 
            char == '@'     ➜ clr = color.darken clr 0.5
            char in '▸■'    ➜ clr = color.darken clr 0.2
        
        clr
        
    getChar: x y char => char

###    
    ████████  ███   ███  ███   ███  █████████  ████████   ████████  ████████
    ███       ███   ███  ████  ███     ███     ███   ███  ███       ███     
    ██████    ███   ███  ███ █ ███     ███     ███████    ███████   ███████ 
    ███       ███   ███  ███  ████     ███     ███   ███  ███       ███     
    ███        ███████   ███   ███     ███     ███   ███  ████████  ████████
###

function funtree extends choices

    @: screen name features ->
        
        super screen name features
        
        @state.syntax = new funSyntax @
        
        post.on 'file.loaded'     @clear
        post.on 'indexer.indexed' @onIndex
        
    #  ███████   ███   ███        ███  ███   ███  ███████    ████████  ███   ███
    # ███   ███  ████  ███        ███  ████  ███  ███   ███  ███        ███ ███ 
    # ███   ███  ███ █ ███        ███  ███ █ ███  ███   ███  ███████     █████  
    # ███   ███  ███  ████        ███  ███  ████  ███   ███  ███        ███ ███ 
    #  ███████   ███   ███        ███  ███   ███  ███████    ████████  ███   ███

    onIndex: path info =>
    
        clssl = clone info.classes
        funcs = clone info.funcs
        
        for clss in clssl
            clss.file ?= path
            clss.name = ' ■ ' + clss.name
            
        for func in funcs
            if func.async
                if func.bound
                    symbol = '○'
                else
                    symbol = '●'
            else
                    symbol = '▸'
            name = func.name
            name = '@' + name if func.static
            func.name = '   ' + symbol + ' ' + name
        
        items = clssl.concat funcs
        items.sort (a b) -> a.line - b.line
        
        @set items 'name'
        
    #  0000000    0000000  000000000  000   0000000   000   000  
    # 000   000  000          000     000  000   000  0000  000  
    # 000000000  000          000     000  000   000  000 0 000  
    # 000   000  000          000     000  000   000  000  0000  
    # 000   000   0000000     000     000   0000000   000   000  
    
    emitAction: action arg event  ->
        
        c = arg
        
        switch action
        
            'right'
            
                post.emit 'goto.line' c.line-1 'ind'
                ⮐  
                
            'click'
            'return'
            
                post.emit 'goto.line' c.line-1 'ind'
                post.emit 'focus' 'editor'
                ⮐  
                
            'drag'
            
                post.emit 'goto.line' c.line-1 'ind'
                ⮐  
                 
        super action arg event
        
export funtree
