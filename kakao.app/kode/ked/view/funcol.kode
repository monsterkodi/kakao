###
00000000  000   000  000   000   0000000   0000000   000    
000       000   000  0000  000  000       000   000  000    
000000    000   000  000 0 000  000       000   000  000    
000       000   000  000  0000  000       000   000  000    
000        0000000   000   000   0000000   0000000   0000000
###

use ../../kxk ▪ post slash
use ../util   ◆ color theme util
use           ◆ view knob crumbs dirtree

function funcol extends view

    @: screen name features ->
        
        super screen name features
        
        @pointerType = 'pointer'
        
        @knob    = new knob    screen "#{@name}_knob"
        @crumbs  = new crumbs  screen "#{@name}_crumbs"
        @dirtree = new dirtree screen "#{@name}_dirtree" ['scroll']       
        
        @crumbs.on 'action' @onCrumbsAction
        
        @dirtree.color.bg           = theme.funcol
        @dirtree.color.empty        = @dirtree.color.bg
        @dirtree.color.cursor_main  = @dirtree.color.bg
        @dirtree.color.cursor_empty = @dirtree.color.bg
        @dirtree.scroll.color.bg    = @dirtree.color.bg
        
        post.on 'funcol.resize' @onFuncolResize
        post.on 'funcol.toggle' @onFuncolToggle
        post.on 'cwd'           @setRoot
        
        @setRoot process.cwd()
        
    onCrumbsAction: action path =>

        if action == 'click'
            @setRoot path
        
    setRoot: path =>
    
        path = slash.tilde path
        @crumbs.set path
        @dirtree.setRoot path redraw:true
        
    layout: x y w h ->
        
        @crumbs.layout  x     y   w   1
        @dirtree.layout x     y+1 w h-1
        @knob.layout    x+w-1 y+1 1 h-1
        
        super x y w h
        
    draw: ->
        
        ⮐ if @hidden()
        
        @cells.fill_rect 0 0 -1 -1 ' ' null theme.funcol
        
        @crumbs.draw()
        @dirtree.draw()
        @knob.draw()
    
        super()
        
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onFuncolResize: => @knob.doDrag = true
    onFuncolToggle: => 
        
        post.emit 'view.size' @name [(@hidden() ? int(@knob.maxWidth/3) : 0) 0]
        
    onMouse: event =>
        
        ret = super event            ; ⮐ ret if ret?.redraw
        ret = @knob.onMouse event    ; ⮐ ret if ret?.redraw
        ret = @crumbs.onMouse event  ; ⮐ ret if ret?.redraw
        ret = @dirtree.onMouse event ; ⮐ ret if ret?.redraw
        
    # 000   000  000   000  00000000  00000000  000      
    # 000 0 000  000   000  000       000       000      
    # 000000000  000000000  0000000   0000000   000      
    # 000   000  000   000  000       000       000      
    # 00     00  000   000  00000000  00000000  0000000  
    
    onWheel: event =>
        
        ⮐ if @hidden()
        @dirtree.onWheel event
        
    # 000   000  00000000  000   000  
    # 000  000   000        000 000   
    # 0000000    0000000     00000    
    # 000  000   000          000     
    # 000   000  00000000     000     
    
    onKey: key event => 
        
        ⮐ if @hidden()
        @dirtree.onKey key event

export funcol
