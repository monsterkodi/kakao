
# ███   ███  ███   ███  ███   ███
# ███  ███    ███ ███   ███  ███ 
# ███████      █████    ███████  
# ███  ███    ███ ███   ███  ███ 
# ███   ███  ███   ███  ███   ███

_G.iter = from to step=1 ->

    if step != 0
        step = math.abs step
        if from > to ➜ step = -step
        i = from - step
        ⮐  ->
            i = i + step
            if i >= to and step < 0 or i <= to and step > 0
                ⮐  i
    -> nil
    
_G.empty = v ->

    switch type(v)
    
        "table"  ➜ ⮐  v.len == 0 or v.len == nil and v == {}
        "string" ➜ ⮐  v == ""
        "nil"    ➜ ⮐  true
        
    false

_G.slice = a first last ->

    switch type(a)
    
        "string" ➜ ⮐  string.sub a first last
        "table"
            if last == nil ➜ last = a.len
            s = {}
            for i in first..last
                s[s.len+1] = a[i]
            ⮐  s
        
_G.sleep = s ->

    t = os.clock()
    while os.clock() - t <= s ➜ _ = 1
    
_G.is = v t ->

    vt = type(v)
    tt = type(t)
    ⮐  false if vt != tt
    ⮐  true  if vt != "table"
    ⮐  true  if vt == t and tt == "string"
    ⮐  true  if type(v.is) == "function" and v∙is(t)
    false
    
_G.write = ... ->

    s = table.concat {...} ""
    s = s & "\27[0m\n"
    ffi.C.write 1 s s.len
    
timers = {}
    
_G.profileStart = msg ->
    if not timers[msg]
        timers[msg] = os.clock()
    else
        log "[WARNING] Duplicate profileStart for '#{msg}'"

_G.profileStop = msg ->
    
    if not timers[msg]
        log "[ERROR] profileStop for unknown label '#{msg}'"
        ⮐  

    tick = os.clock()
                
    write ◌b msg " " ◌m tick-timers[msg]
    timers[msg] = nil

_G.strbuff  = require "string.buffer"
_G.class    = require "kxk.class"
_G.inspect  = require "kxk.inspect"
_G.slash    = require "kxk.slash"
_G.array    = require "kxk.array"
_G.kstr     = require "kxk.kstr"
_G.kseg     = require "kxk.kseg"
_G.test     = require "kxk.test"

⮐  {}
