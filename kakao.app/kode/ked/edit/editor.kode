###
00000000  0000000    000  000000000   0000000   00000000 
000       000   000  000     000     000   000  000   000
0000000   000   000  000     000     000   000  0000000  
000       000   000  000     000     000   000  000   000
00000000  0000000    000     000      0000000   000   000
###

use ../../kxk ▪ post
use ../util   ◆ util
use ../view   ◆ scroll gutter mapscr mapview
use           ◆ state draw complete

function editor extends draw

    @: screen name features ->
        
        super screen name features
        
        @state = new state @cells @name
        
        post.on 'focus' @onFocus
        
        if @name == 'editor'
            post.on 'finder.apply' @onFinderApply 
        
        if @feats.scrllr   ➜ @scroll   = new scroll   @screen @state 'right'
        if @feats.scroll   ➜ @scroll   = new scroll   @screen @state 'left'
        if @feats.gutter   ➜ @gutter   = new gutter   @screen @state
        if @feats.mapscr   ➜ @mapscr   = new mapscr   @screen @state; @mapscr.show()
        if @feats.mapview  ➜ @mapscr   = new mapview  @screen @state
        if @feats.complete ➜ @complete = new complete @
                
    # 000  000   000  000  000000000  
    # 000  0000  000  000     000     
    # 000  000 0 000  000     000     
    # 000  000  0000  000     000     
    # 000  000   000  000     000     
    
    layout: x y w h =>
        
        g = m = s = 0
        sl = sr = 0
        
        if @scroll
            s = 1
            if @feats.scrllr
                sr = s
                @scroll.layout x+w-sr y   s   h 
            else
                sl = s
                @scroll.layout x      y   s   h 
            
        if @gutter
            g = @state.gutterWidth()
            @gutter.layout    x+sl    y   g   h
            
        if @mapscr
            m = @mapscr.visible() ? 10 : 0
            r = @mapscr.visible() ? h : 0
            @mapscr.layout x+w-sr-10   y   m   r
            
        @cells.layout x+sl+g  y   w-s-g-m    h
        
        @complete?.onEditorLayout()
        
        @state.initView()
                
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event =>
        
        ⮐ true if @mapscr?.onMouse?  event
        ⮐ true if @scroll?.onMouse   event
        ⮐ true if @complete?.onMouse event
        ⮐ true if super              event
        
        [col row] = @cells.posForEvent event
        
        switch event.type
            
            'press'
            
                if @cells.isOutsideEvent event
                    ⮐ if not @gutter?.cells.isInsideEvent event
                        
                if event.count > 1

                    @state.deselect() if not event.shift
                    
                    x = col + @state.s.view[0]
                    y = row + @state.s.view[1]
                    
                    if event.count == 2
                        if event.alt
                            @state.selectChunk x y
                        else
                            @state.selectWord x y
                    else
                        @state.selectLine y
                        
                    @dragStart = copy @state.s.selections[0]
                    
                    ⮐ true
                    
                else
                    
                    x = col + @state.s.view[0]
                    y = row + @state.s.view[1]
                    
                    @dragStart = [x y x]
                                        
                    @state.deselect() if not event.shift
                    @state.clearCursors() if not event.alt

                    if event.alt
                        @state.addCursor x y
                    else
                        if event.shift and @state.s.cursors.length == 1
                            @state.setMainCursorAndSelect x y
                        else
                            @state.setMainCursor x y
                    
                    @grabFocus()
                    
                    ⮐ true
                    
            'drag'
            
                if @dragStart
                    
                    x = col + @state.s.view[0]
                    y = row + @state.s.view[1]
                    
                    start = [@dragStart[0] @dragStart[1]]
                    
                    if y < @dragStart[1] 
                        start = [@dragStart[2] @dragStart[1]]
                    
                    if event.shift ➜ @state.addRangeToSelectionWithMainCursorAtEnd util.rangeFromStartToEnd(start [x y])
                    else             @state.select start [x y]
                        
                    ⮐ true
                    
            'release'
            
                delete @dragStart
                
            'move'
                
                if @cells.isInsideEvent event
                    post.emit 'pointer' 'text'
                else if @gutter?.cells.isInsideEvent event
                    post.emit 'pointer' 'vertical-text'
                
        false
                
    # 000   000  000   000  00000000  00000000  000      
    # 000 0 000  000   000  000       000       000      
    # 000000000  000000000  0000000   0000000   000      
    # 000   000  000   000  000       000       000      
    # 00     00  000   000  00000000  00000000  0000000  
    
    onWheel: event =>
        
        ⮐ if event.cell[1] >= @cells.y + @cells.rows
        
        if @name == 'editor' # this sucks! consider a separate main editor class
        
            steps  = 1 # should be 4 if not using a mouse pad -> config
            steps *= 2 if event.shift
            steps *= 2 if event.ctrl
            steps *= 2 if event.alt
            
            if @dragStart
                
                [x y] = @state.mainCursor()
                
                switch event.dir
                    'up'    ➜ y -= steps    
                    'down'  ➜ y += steps    
                    'left'  ➜ x -= 1    
                    'right' ➜ x += 1    
                    
                y = clamp 0 @state.s.lines.length-1 y
                x = clamp 0 @state.s.lines[y].length-1 x
                
                start = [@dragStart[0] @dragStart[1]]
                
                if y < @dragStart[1] 
                    start = [@dragStart[2] @dragStart[1]]
                
                if @state.select start [x y]
                    @redraw()
                ⮐ 
        
        [col row] = @cells.posForEvent event
        
        ⮐ if not @cells.isInsideEvent event

        if @complete?
            ⮐ true if @complete.onWheel event
        
        switch event.dir
            'up' 'down' 'left' 'right' ➜ @state.scrollView event.dir steps
            
        true
            
    #  0000000  000   000  00000000    0000000   0000000   00000000   
    # 000       000   000  000   000  000       000   000  000   000  
    # 000       000   000  0000000    0000000   000   000  0000000    
    # 000       000   000  000   000       000  000   000  000   000  
    #  0000000   0000000   000   000  0000000    0000000   000   000  
            
    isCursorInEmpty: cursor =>
        
        cursor ?= @state.mainCursor()
        
        util.isLinesPosOutside @state.s.lines cursor
        
    isCursorVisible: cursor =>
        
        cursor ?= @state.mainCursor()
                
        v = @state.s.view
        
        v[0] <= cursor[0] < v[0] + @cells.cols and v[1] <= cursor[1] < v[1] + @cells.rows
        
    # 00000000   0000000    0000000  000   000   0000000  
    # 000       000   000  000       000   000  000       
    # 000000    000   000  000       000   000  0000000   
    # 000       000   000  000       000   000       000  
    # 000        0000000    0000000   0000000   0000000   
    
    grabFocus: =>
    
        post.emit 'focus' @name
        @redraw()
        
    hasFocus: ->
        
        @state.hasFocus
        
    onFocus: name =>
        
        @state.hasFocus = (name == @name)
        
    # 00000000   00000000  0000000    00000000    0000000   000   000  
    # 000   000  000       000   000  000   000  000   000  000 0 000  
    # 0000000    0000000   000   000  0000000    000000000  000000000  
    # 000   000  000       000   000  000   000  000   000  000   000  
    # 000   000  00000000  0000000    000   000  000   000  00     00  
    
    redraw: => post.emit 'redraw'
    
    onFinderApply: text =>
    
        @state.highlightText text
        @state.moveCursorToNextHighlight()
        @grabFocus()
                
    # 000   000  00000000  000   000  
    # 000  000   000        000 000   
    # 0000000    0000000     00000    
    # 000  000   000          000     
    # 000   000  00000000     000     
    
    onKey: key event =>
        
        ⮐ if not @hasFocus()
        
        if @complete?
            ⮐ if @complete.handleKey(key event) != 'unhandled'

        if @state.handleKey(key event) != 'unhandled'
            @complete?.hide()
            ⮐ 

        if valid event.char
            @state.insert event.char
            if @complete?
                if empty @state.chunkAfterCursor()
                    log "complete #{@state.chunkBeforeCursor()}"
                    @complete.word @state.chunkBeforeCursor() 
        else
            if key not in ['shift' 'ctrl' 'alt' 'cmd'] 
                log 'editor.onKey?' key

export editor
