###
 0000000  00000000   0000000   00000000    0000000  000   000  00000000  00000000 
000       000       000   000  000   000  000       000   000  000       000   000
0000000   0000000   000000000  0000000    000       000000000  0000000   0000000  
     000  000       000   000  000   000  000       000   000  000       000   000
0000000   00000000  000   000  000   000   0000000  000   000  00000000  000   000
###

use ../../../kxk    ▪ post kseg slash
use ../../../kxk    ◆ nfs
use ../../edit/tool ◆ belt
use ../../util      ◆ prjcts
use                 ◆ finder

function searcher extends finder

    @: @screen @state ->

        super @screen @state 'searcher'
        
    #  0000000  000   000   0000000   000   000  
    # 000       000   000  000   000  000 0 000  
    # 0000000   000000000  000   000  000000000  
    #      000  000   000  000   000  000   000  
    # 0000000   000   000   0000000   00     00  
    
    show: text ○->
        
        text = @searchText text
        
        # log g4("#{r6 'searcher'} '#{text}'")
        
        if empty text
            @layout()
            @input.grabFocus()
            post.emit 'redraw'
            ⮐ 
        
        editorFile = ked_session.get 'editor▸file'
        
        dir   = prjcts.dir   editorFile
        files = prjcts.files editorFile
        
        @choices.state.setMainCursor 0 0
        @choices.set [] 'line'
        
        @layout()
        @input.grabFocus()
        post.emit 'redraw'
        
        for file,idx in files
            
            filet =○ nfs.readText file
            
            segls = kseg.segls filet
            spans = belt.lineSpansForText segls text
            
            continue if empty spans
            
            front = belt.frontmostSpans spans

            items = @choices.items ? []            
            
            items.push line:''
            items.push
                line:  slash.relative(file dir)
                type:  'file'
                path:  file
                row:   0
                col:   0
                
            for span in front
                if items[-1].row != span[1]-1
                    items.push line:''
                items.push 
                    line:  kseg.str(segls[span[1]])
                    path:  file
                    row:   span[1]
                    col:   span[2]
            
            ⮐  if @hidden()
            
            selectedIndex = @choices.state.s.cursors[0][1]
            @choices.set items 'line'
            @choices.state.highlightText text
            @choices.select selectedIndex
            
            @layout()
            post.emit 'redraw'
        
    # ███  ███   ███  ████████   ███   ███  █████████       ███████    ███████  █████████  ███   ███████   ███   ███
    # ███  ████  ███  ███   ███  ███   ███     ███         ███   ███  ███          ███     ███  ███   ███  ████  ███
    # ███  ███ █ ███  ████████   ███   ███     ███         █████████  ███          ███     ███  ███   ███  ███ █ ███
    # ███  ███  ████  ███        ███   ███     ███         ███   ███  ███          ███     ███  ███   ███  ███  ████
    # ███  ███   ███  ███         ███████      ███         ███   ███   ███████     ███     ███   ███████   ███   ███

    onInputAction: action text ->
    
        switch action
            
            'submit' ➜ ⮐  @show text
            'change' ➜ ⮐  
            
        super action text

export searcher
