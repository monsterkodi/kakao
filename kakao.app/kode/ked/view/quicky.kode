###
 0000000   000   000  000   0000000  000   000  000   000
000   000  000   000  000  000       000  000    000 000
000 00 00  000   000  000  000       0000000      00000
000 0000   000   000  000  000       000  000      000
 00000 00   0000000   000   0000000  000   000     000
###

use ../../kxk ▪ kstr slash post noon
use ../../kxk ◆ nfs
use ../util ◆ prjcts walker util
use ..      ◆ editor theme
use         ◆ cells input choices crumbs

import rgxs from './quicky.json' with { type: "json" }

int = parseInt

function quicky

    @: @screen ->

        @name    = 'quicky'

        @cells   = new cells   @screen
        @input   = new input   @screen 'quicky_input'
        @crumbs  = new crumbs  @screen 'quicky_crumbs'
        @choices = new choices @screen 'quicky_choices'

        @choices.mapscr.hide()
        @choices.state.syntax.setRgxs rgxs

        @input.on 'changed' @onInputChanged
        
        post.on 'quicky.dir' @gotoDirectory

    #  0000000  000   000   0000000   000   000
    # 000       000   000  000   000  000 0 000
    # 0000000   000000000  000   000  000000000
    #      000  000   000  000   000  000   000
    # 0000000   000   000   0000000   00     00

    show: =>

        @layout()
        @input.grabFocus()

    layout: =>

        x = int @screen.cols/4
        y = int @screen.rows/4
        w = int @screen.cols/2
        h = int @screen.rows/2-4

        cs = min h @choices.num()
        cs = h if @crumbs.visible()
        
        ch = @crumbs.visible() ? 1 : 0
        cd = ch == 1 ? 1 : 0

        @input.init    x+2   y+1     w-4  1
        @crumbs.init   x+2   y+3     w-4  ch
        @choices.init  x+2   y+3+cd  w-3  cs
        @cells.init    x     y       w    cs+4+cd

        # lf 'layout' c @choices.items.length

    hide: => 
        
        @choices.mapscr.hide()
        @cells.rows = 0 
        post.emit 'focus' 'editor' 
        # post.emit 'redraw'
        redraw:true
        
    hidden: -> @cells.rows <= 0
    visible: -> @cells.rows > 0

    #  0000000   00000000   00000000  000   000
    # 000   000  000   000  000       0000  000
    # 000   000  00000000   0000000   000 0 000
    # 000   000  000        000       000  0000
    #  0000000   000        00000000  000   000

    toggle: currentFile -> if @hidden() then @open(currentFile) else @hide()
    open:   currentFile -> @gotoFile currentFile
        
    #  0000000    0000000   000000000   0000000   00000000  000  000      00000000  
    # 000        000   000     000     000   000  000       000  000      000       
    # 000  0000  000   000     000     000   000  000000    000  000      0000000   
    # 000   000  000   000     000     000   000  000       000  000      000       
    #  0000000    0000000      000      0000000   000       000  0000000  00000000  
    
    gotoFile: @currentFile ->

        # lf 'quicky.gotoFile' @currentFile

        items = prjcts.files @currentFile
        @currentDir = slash.dir @currentFile
        items = items.map (i) => slash.relative i @currentDir
        
        @crumbs.hide()

        ccol = int(@screen.cols/2) - 5

        maxind = 0
        indents = []
        for item in items
            indent = slash.dir(item).length
            indent += 1 if indent
            maxind = max maxind indent
            indents.push indent

        items = items.map (i n) => rpad ccol lpad(maxind-indents[n])+i

        weight = item ->

            p = slash.parse item

            w = 0
            w += item.split('/').length * 256
            w += kstr.weight p.name
            w

        items.sort (a b) -> weight(a) - weight(b)

        @input.set '' # previously opened file?
        @choices.set items
        @choices.state.selectLine 0
        @choices.state.setMainCursor @choices.state.s.lines[0].length 0
        @choices.state.setView [0 0]
        @show()
        
    #  0000000    0000000   000000000   0000000   0000000    000  00000000   
    # 000        000   000     000     000   000  000   000  000  000   000  
    # 000  0000  000   000     000     000   000  000   000  000  0000000    
    # 000   000  000   000     000     000   000  000   000  000  000   000  
    #  0000000    0000000      000      0000000   0000000    000  000   000  
    
    gotoDirectory: dir select ○=>
        
        ⮐ if empty dir

        dir = slash.untilde dir
        lf 'quicky.gotoDirectory' dir
        
        try
            items =○ nfs.list dir recursive:false
        catch err
            lf 'list error' err
            ⮐ 

        @currentDir = dir
        @crumbs.show @currentDir
        
        for item in items
            item.tilde = slash.relative item.path @currentDir
            item.tilde = ((item.type == 'dir') ? ' ' : '  ') + item.tilde
            
        parent = slash.dir @currentDir
        if valid parent
            items.push
                type: 'dir'
                file: slash.name(parent)
                path: parent
                tilde: ' ..'

        weight = item ->

            p = slash.parse item.path

            w = 0
            ⮐ w if item.tilde == ' ..'
            w += 10000 if item.type == 'file'
            w += 1000  if item.tilde.startsWith ' .'
            w += kstr.weight(p.file)
            w

        items.sort (a b) -> weight(a) - weight(b)

        @input.set ''

        selectIndex = valid parent and items.length > 1 ? 1 : 0        
        
        if select
            for item,idx in items
                if slash.samePath item.path select
                    selectIndex = idx
                    break
                    
        @preview items[selectIndex].path
        
        @choices.set items 'tilde'
        @choices.state.selectLine selectIndex
        @choices.state.setMainCursor @choices.state.s.lines[selectIndex].length selectIndex
        @choices.state.setView [0 0]
        @show()
        @choices.grabFocus()
        
    # 00000000  000  000      00000000   0000000   00000000   0000000    000  00000000   
    # 000       000  000      000       000   000  000   000  000   000  000  000   000  
    # 000000    000  000      0000000   000   000  0000000    000   000  000  0000000    
    # 000       000  000      000       000   000  000   000  000   000  000  000   000  
    # 000       000  0000000  00000000   0000000   000   000  0000000    000  000   000  
    
    gotoFileOrDirectory: path ○=>
        
        # lf 'gotoFileOrDirectory' path
        
        isDir =○ nfs.dirExists path
        
        if isDir
            ○ @gotoDirectory path
        else
            isFile =○ nfs.fileExists path
            if isFile
                @openFileInEditor path
            
    openFileInEditor: file =>
        
        @hide()
        post.emit 'quicky' file
        redraw:false
        
    # 000  000   000  00000000   000   000  000000000  
    # 000  0000  000  000   000  000   000     000     
    # 000  000 0 000  00000000   000   000     000     
    # 000  000  0000  000        000   000     000     
    # 000  000   000  000         0000000      000     
    
    onInputChanged: text =>

        @choices.filter text
        @choices.state.selectLine 0
        @choices.state.setMainCursor @choices.state.s.lines[0].length 0
        @layout()

    # 00000000    0000000    0000000  000000000  00000000   00000000   0000000  000   000  000      000000000  
    # 000   000  000   000  000          000     000   000  000       000       000   000  000         000     
    # 00000000   000   000  0000000      000     0000000    0000000   0000000   000   000  000         000     
    # 000        000   000       000     000     000   000  000            000  000   000  000         000     
    # 000         0000000   0000000      000     000   000  00000000  0000000    0000000   0000000     000     
    
    postResult: ->
        
        # lf ">#{@input.current()}<" 
        
        switch @input.current()
            
            '/'  ➜ ⮐ @gotoDirectory '/'
            '~'  ➜ ⮐ @gotoDirectory '~'
            '.'  ➜ ⮐ @gotoDirectory @currentDir
            '..' ➜ ⮐ @gotoDirectory slash.dir(@currentDir)
            
        current = @choices.current()
        lf 'current choice' current
        
        if empty current and valid @input.current()
            
            ⮐ @gotoFileOrDirectory @input.current()
        
        if current.path
            
            ⮐ @gotoFileOrDirectory current.path
    
        @returnToEditor()
                     
    returnToEditor: ->

        @hide()

        if @choices.num()
            post.emit 'quicky' @currentChoice()
            redraw:false
        else
            redraw:true
            
    currentChoice: -> trim @choices.current()

    # 0000000    00000000    0000000   000   000
    # 000   000  000   000  000   000  000 0 000
    # 000   000  0000000    000000000  000000000
    # 000   000  000   000  000   000  000   000
    # 0000000    000   000  000   000  00     00

    draw: ->

        ⮐ if @hidden()

        fg = theme.quicky_frame_fg
        bg = theme.quicky_frame_bg

        @cells.draw_frame 0 0 -1 -1 fg:fg bg:bg hdiv:[2]

        @input.draw()
        @crumbs.draw()
        @choices.draw()        

    #  0000000  00000000  000      00000000   0000000  000000000
    # 000       000       000      000       000          000
    # 0000000   0000000   000      0000000   000          000
    #      000  000       000      000       000          000
    # 0000000   00000000  0000000  00000000   0000000     000

    moveSelection: dir ->
        
        lf 'moveSelection' dir

        switch dir
            'down'  ➜ @choices.selectNext()
            'up'    ➜ @choices.selectPrev()

        @choices.mapscr.hide()
        
        if @choices.current().path
            @input.set ''
            @preview @choices.current().path
        else
            @input.set @choices.state.selectedText()
        @input.selectAll()
        @choices.grabFocus()
        
    # 00000000   00000000   00000000  000   000  000  00000000  000   000  
    # 000   000  000   000  000       000   000  000  000       000 0 000  
    # 00000000   0000000    0000000    000 000   000  0000000   000000000  
    # 000        000   000  000          000     000  000       000   000  
    # 000        000   000  00000000      0      000  00000000  00     00  
    
    preview: file ○->
        
        # lf 'preview' walker.sourceFileExtensions
        
        if slash.ext(file) in walker.sourceFileExtensions

            text =○ nfs.read file
            lines = util.linesForText text
            @choices.mapscr.show()
            @choices.mapscr.setSyntaxLines slash.ext(file) lines 
        else
            @choices.mapscr.hide()
        
    moveFocus: ->
        
        if @choices.hasFocus()
            @input.grabFocus()
        else
            @choices.grabFocus()

    # 000   000  00000000  000   000
    # 000  000   000        000 000
    # 0000000    0000000     00000
    # 000  000   000          000
    # 000   000  00000000     000

    onKey: key event =>

        ⮐ if @hidden()

        switch event.combo
            'tab'       ➜ ⮐ @moveFocus()
            'esc'       ➜ ⮐ @hide()
            'return'    ➜ ⮐ @postResult()
            'up' 'down' ➜ ⮐ @moveSelection event.combo
            
        if @choices.hasFocus()
            
            current = @choices.current()
            lf 'current' current key
            switch event.combo
                'right' 
                    if current.path
                        if current.tilde == ' ..'
                            ⮐ @moveSelection 'down'
                        if current.type == 'file'
                            ⮐ post.emit 'quicky' current.path
                        else
                            @choices.mapscr.hide()
                            ⮐ @gotoFileOrDirectory current.link ? current.path
                'left'  
                    if current.path
                        upDir = slash.dir @currentDir
                        @choices.mapscr.hide()
                        ⮐ @gotoDirectory upDir @currentDir
                'space'            
                    if current.path and current.type == 'file'
                        ⮐ post.emit 'quicky' current.path
                    
            @input.grabFocus()

        ⮐ true if @input.onKey key event
        ⮐ true if @choices.onKey key event
        true

    onMouse: type sx sy event =>

        ⮐ if @hidden()
        ⮐ true if @input.onMouse type sx sy event
        ⮐ true if @choices.onMouse type sx sy event
        true

    onWheel: event =>

        ⮐ if @hidden()

        ⮐ true if @input.onWheel event
        ⮐ true if @choices.onWheel event
        true

export quicky
