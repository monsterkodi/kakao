
###
    00     00   0000000   00000000   000   000  000  00000000  000   000  
    000   000  000   000  000   000  000   000  000  000       000 0 000  
    000000000  000000000  00000000    000 000   000  0000000   000000000  
    000 0 000  000   000  000           000     000  000       000   000  
    000   000  000   000  000            0      000  00000000  00     00  
###

use ../../kxk ▪ kstr post
use ../util ◆ prof syntax color util
use ..      ◆ theme 
use         ◆ cells

{floor, pow} = Math

function mapview

    @: screen @state ->
        
        @cells = new cells screen
        @imgId = kstr.hash @state.name
        
        @pixelsPerRow = 4
        @pixelsPerCol = 2
        
    init: x y w h -> @cells.init x y w h
    
    show:   doShow=true => if doShow == false ➜ @hide() else @cells.cols = 10
    hide:   => @cells.cols = 0
    hidden: -> @cells.cols <= 0
    visible: -> @cells.cols > 0
        
    clearImages: =>
        
        @cells.screen.t.write "\x1b_Gq=1,a=d,d=i,i=#{@imgId}\x1b\\" # delete all images with id        
        
    reload: => @clearImages() ; @reallocBuffer()
                
    # 0000000    000   000  00000000  00000000  00000000  00000000   
    # 000   000  000   000  000       000       000       000   000  
    # 0000000    000   000  000000    000000    0000000   0000000    
    # 000   000  000   000  000       000       000       000   000  
    # 0000000     0000000   000       000       00000000  000   000  
    
    getLines:  => @lines
    getSyntax: => @syntax
    setSyntaxLines: ext @lines => 
        
        @syntax = new syntax 
        @syntax.setExt ext 
        @syntax.setLines @lines
        @reallocBuffer()
    
    reallocBuffer: =>
        
        # prof.start 'mapscr'
        
        t = @cells.screen.t
        ⮐ if empty t.cellsz
        
        [w h] = [@cells.cols * t.cellsz[0] @cells.rows * t.cellsz[1]]
        
        bytes = w*h*3
        
        if bytes <= 0
            @clearImages()
            ⮐ 
        
        data = Buffer.alloc bytes
        # prof.time 'mapscr' 'alloc'
        
        lines = @getLines()
        syntax = @getSyntax()
        
        for y in 0...min(h lines.length*@pixelsPerRow)
            li = parseInt y/@pixelsPerRow
            line = lines[li]
            for x in 0...min(w line.length*@pixelsPerCol)
                if x == 0
                    data[(y*w+x)*3+0] = 55 
                    data[(y*w+x)*3+1] = 55 
                    data[(y*w+x)*3+2] = 55 
                    
                xi = parseInt x/@pixelsPerCol              
                ch = line[xi]
                
                if valid ch and ch != ' '
                
                    clss = syntax.getClass xi li
                    
                    if 'header' in clss
                        if 'triple' in clss
                            rgb = [27 207 14]
                        else
                            rgb = [9 140 0]
                    else
                        f = 0.7
                        rgb = color.rgb syntax.getColor(xi li)
                        rgb = rgb.map (v) -> clamp(0 255 parseInt(f*v))
                    
                    [r g b] = rgb
                    
                    data[(y*w+x)*3+0] = r
                    data[(y*w+x)*3+1] = g
                    data[(y*w+x)*3+2] = b
                
        # prof.time 'mapscr' 'fill'        
        
        # q=1  don't send OK q=2 don't send OK and failure
        # i=n  image id 
        # p=n  placement id
        # f=24 3 bytes per pixel
        # s=n  width v=n  height
        # m=1  image chunk not larger than 4096 bytes
        # m=0  last chunk of image
        # a=p  placement action

        base64 = data.toString 'base64'
        
        if base64.length > 4096 # and 'kitty' in process.env.TERM
            chunk = base64[0...4096]
            t.write "\x1b_Gq=1,i=#{@imgId},p=#{@imgId},f=24,s=#{w},v=#{h},m=1;#{chunk}\x1b\\"
            chunks = Math.ceil base64.length / 4096
            for i in 1...chunks
                chunk = base64[i*4096...Math.min((i+1)*4096, base64.length)]
                t.write "\x1b_Gq=1,m=#{(i == chunks - 1) ? 0 : 1};#{chunk}\x1b\\"
        else            
            t.write "\x1b_Gq=1,i=#{@imgId},p=#{@imgId},f=24,s=#{w},v=#{h};#{base64}\x1b\\"
            
        # prof.end 'mapscr' 'send'
        
        @draw()
                        
    # 0000000    00000000    0000000   000   000  
    # 000   000  000   000  000   000  000 0 000  
    # 000   000  0000000    000000000  000000000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000  00     00  
    
    draw: ->

        t = @cells.screen.t
        
        ⮐ if empty t.pixels or @cells.rows <= 0
        
        t.setCursor @cells.x @cells.y
        t.write "\x1b_Gq=1,a=p,i=#{@imgId},p=#{@imgId},C=1\x1b\\"
        @
                        
export mapview
