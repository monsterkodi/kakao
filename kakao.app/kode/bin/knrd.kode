###
    000   000  000   000  00000000   0000000    
    000  000   0000  000  000   000  000   000  
    0000000    000 0 000  0000000    000   000  
    000  000   000  0000  000   000  000   000  
    000   000  000   000  000   000  0000000    

    transpiles kode, noon, styl and pug files
###

use ../kode/kode
use ../kxk/fs
use ../kxk/slash
use ../kxk/noon

use ../../lib/lib_kakao â–ª pug stylus

__dirname = import.meta.dirname

knrd = (files = [], opt = {}) â—‹->
       
    if files is str
        files = [files]
    
    opt.rerunWhenDirty ?= true
    opt.logVerbose ?= false
    
    â— ðŸ”¨
    
    kodeDir = slash.path __dirname + '/../../kode'
    pugDir  = slash.path __dirname + '/../../pug'
    jsDir   = slash.path __dirname + '/../../js'
    
    rules =
        kode: 
            tgtExt:  'js'
            srcDir:  kodeDir
            tgtDir:  jsDir
            compile: (srcText, srcFile) -> k0de = new kode header:false; k0de.compile srcText, srcFile
        styl:
            tgtExt:  'css'
            srcDir:  pugDir
            tgtDir:  jsDir + '/css'
            compile: (srcText, srcFile) -> stylus srcText
        pug:
            tgtExt:  'html'
            srcDir:  pugDir
            tgtDir:  jsDir
            compile: (srcText, srcFile) -> pug srcText
        noon:
            tgtExt:  'json'
            srcDir:  kodeDir
            tgtDir:  jsDir
            compile: (srcText, srcFile) -> JSON.stringify noon.parse(srcText), null, '  '
    
    if empty files
        
        list  = await fs.list kodeDir
        list  = list.concat await fs.list pugDir
        list  = list.filter (item) -> item.type == 'file'
        files = list.map (item) -> item.path
        
    log 'ðŸ”¨ ' files.length
    
    transpiled = 0
        
    for file in files
        
        if rule = rules[slash.ext file]
            
            srcFile = file
            tgtFile = slash.swapExt srcFile.replace(rule.srcDir, rule.tgtDir), rule.tgtExt
            
            srcText = await fs.read srcFile
            tgtText = await fs.read tgtFile
            
            compText = rule.compile srcText, srcFile
                
            if empty compText
                log y5('âœ˜ '), r5(srcFile), r4('transpiles to empty!')
            else
                if tgtText != compText
                    transpiled++
                    # log m3('â–¶ '), m4 slash.tilde srcFile
                    await fs.write tgtFile, compText
                    log b5('âœ” '), g5 slash.tilde tgtFile
                else 
                    fs.touch tgtFile
                    if opt.logVerbose
                        log g2('âœ” '), m3 slash.tilde srcFile
        else
            # if slash.ext(file) not in ['css' 'html' 'json']
            error 'unknown file type' file
            
        null
            
    if opt.rerunWhenDirty and transpiled
        
        knrd files, rerunWhenDirty:false
        
export knrd
