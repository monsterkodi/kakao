###
000  000   000  00000000   0000000
000  0000  000  000       000   000
000  000 0 000  000000    000   000
000  000  0000  000       000   000
000  000   000  000        0000000
###

use ../../kxk ▪ pretty ffs elem slash $
use ../tools  ◆ File

# 000  00     00   0000000    0000000   00000000
# 000  000   000  000   000  000        000
# 000  000000000  000000000  000  0000  0000000
# 000  000 0 000  000   000  000   000  000
# 000  000   000  000   000   0000000   00000000

image = (file) ->

    img = elem 'img' class:'browserImage' src:slash.fileUrl file
    cnt = elem class:'browserImageContainer' child:img
    cnt.addEventListener 'dblclick' -> kore.set 'view|file' file #open file

    table = elem 'table' class:"fileInfoData"

    img.onload = ○->

        img =$ '.browserImage'
        br = img.getBoundingClientRect()
        x = img.clientX
        width  = parseInt br.right - br.left - 2
        height = parseInt br.bottom - br.top - 2

        img.style.opacity   = '1'
        img.style.maxWidth  = '100%'

        info =○ ffs.info file
        
        [size, unit] = pretty.bytes(info.size).split ' '

        html  = "<tr><th colspan=2>#{width}<span class='punct'>x</span>#{height}</th></tr>"
        html += "<tr><th>#{size}</th><td>#{unit}</td></tr>"
        html += "<tr><th>#{pretty.age info.modified}</th><td>ago</td></tr>"

        table.innerHTML = html

    info = elem class:'browserFileInfo' children: [
        elem 'div' class:"fileInfoFile #{slash.ext file}" html:File.span file
        table
    ]

    cnt.appendChild info
    cnt

# 00000000  000  000      00000000
# 000       000  000      000
# 000000    000  000      0000000
# 000       000  000      000
# 000       000  0000000  00000000

file = (file) ->

    table = elem 'table' class:"fileInfoData"

    ffs.fileExists(file).then (stat) ○->

        if not stat
            return error "file #{file} doesn't exist?"

        info =○ ffs.info file

        [size, size_unit] = pretty.bytes(info.size).split ' '
        [time, time_unit] = pretty.age(info.modified).split ' '
        table.innerHTML = "<tr><th>#{size}</th><td>#{size_unit}</td></tr><tr><th>#{time}</th><td>#{time_unit}</td></tr>"

    info = elem class:'browserFileInfo' children: [
        elem class:'fileInfoSpacer'
        elem class:"fileInfoIcon #{slash.ext file} #{File.iconClassName file}"
        elem class:"fileInfoFile #{slash.ext file}" html:File.span file
        table
        elem class:'fileInfoSpacer'
    ]

    info

export
    file:   file
    image:  image
