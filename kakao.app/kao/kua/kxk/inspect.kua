
# ███  ███   ███   ███████  ████████   ████████   ███████  █████████
# ███  ████  ███  ███       ███   ███  ███       ███          ███   
# ███  ███ █ ███  ███████   ████████   ███████   ███          ███   
# ███  ███  ████       ███  ███        ███       ███          ███   
# ███  ███   ███  ███████   ███        ████████   ███████     ███   

# Copyright (c) 2022 Enrique García Cota

# use kxk/kxk

rawpairs = t -> (next t nil)

smartQuote = str ->
    if str:match('"') and not str:match("'")
       ⮐  "'" & str & "'"
    '"' & string.gsub(str '"' '\\"') & '"'
    
shortEscapes = {"\a":"\\a" "\b":"\\b" "\f":"\\f" "\n":"\\n" "\r":"\\r" "\t":"\\t" "\v":"\\v" "\127":"\\127"}
longEscapes  = {"\127":"\127"}

for i in 0..31
    ch = string.char(i)
    if not shortEscapes[ch]
        shortEscapes[ch] = "\\" & i
        longEscapes[ch] = string.format("\\%03d" i)

escape = str -> (string.gsub(string.gsub(string.gsub(str "\\" "\\\\") "(%c)%f[0-9]" longEscapes) "%c" shortEscapes))

luaKeywords = {
   'and':       true
   'break':     true
   'do':        true
   'else':      true
   'elseif':    true
   'end':       true
   'false':     true
   'for':       true
   'function':  true
   'goto':      true
   'if':        true
   'in':        true
   'local':     true
   'nil':       true
   'not':       true
   'or':        true
   'repeat':    true
   'return':    true
   'then':      true
   'true':      true
   'until':     true
   'while':     true
}

isIdentifier = str ->

    type(str) == "string" and str:match("^[_%a][_%a%d]*$") and not luaKeywords[str]

isSequenceKey = k sequenceLength ->

    type(k) == "number" and math.floor(k) == k and 1 <= (k) and k <= sequenceLength

typeOrders = { 'number':1 'boolean':2 'string':3 'table':4 'function':5 'userdata':6 'thread':7 }

sortKeys = a b ->

    ta = type(a)
    tb = type(b)
    
    if ta == tb and (ta == 'string' or ta == 'number')
       ⮐  (a) < (b)
    
    dta = typeOrders[ta] or 100
    dtb = typeOrders[tb] or 100
    
    dta == dtb and ta < tb or dta < dtb

getKeys = t ->

    seqLen = 1
    while t[seqLen] != nil
        seqLen = seqLen + 1
    seqLen = seqLen - 1
    
    (keys keysLen) = ({} 0)
    for k in rawpairs(t)
        if not isSequenceKey(k seqLen)
            keysLen = keysLen + 1
            keys[keysLen] = k
    table.sort(keys, sortKeys)
    (keys keysLen seqLen)

countCycles = x cycles ->

    if type(x) == "table"
    
        if cycles[x]
            cycles[x] = cycles[x] + 1
        else
            cycles[x] = 1
            for k v in rawpairs(x)
                countCycles k cycles
                countCycles v cycles
            countCycles getmetatable(x) cycles

class Inspector

    depth   : math.huge
    newline : '\n'
    indent  : "    "
    
    @: root ->
    
        @ids     = {}
        @cycles  = {}
        @level   = 0
        countCycles(root, @cycles)
        
        @buf = strbuff:new()
        @putValue root
        buf = @buf
        # don't return self here
        ⮐  buf:get()

    getId: v ->
    
        id = @ids[v]
        ids = @ids
        if not id then
            tv = type(v)
            id = (ids[tv] or 0) + 1
            (ids[v] ids[tv]) = (id id)
        tostring(id)

    putValue: v ->
    
        tabify = -> buf=@buf ; buf:put(@newline & string.rep(@indent @level))
    
        tv = type(v)
        buf = @buf
        if tv == 'string'
            buf:put smartQuote(escape(v))
        elif tv == 'number' or tv == 'boolean' or tv == 'nil' or tv == 'cdata' or tv == 'ctype' 
            buf:put tostring(v)
        elif tv == 'table' and not @ids[v]
            t = v
            
            if @level >= @depth
                buf:put '{...}'
            else
                @level = @level + 1
                
                if @cycles[t] > 1 
                    tabify()
                    buf:put string.format('<%d>' @getId(t))
                
                (keys keysLen seqLen) = getKeys(t)
                
                for i in 1..seqLen + keysLen 
                    if i <= seqLen
                        buf:put ' '
                        @putValue(t[i])
                    else
                        tabify()
                        k = keys[i - seqLen]
                        if isIdentifier k
                            buf:put k
                            if k.len < 12
                                buf:put string.rep(" " 12-k.len)
                        else
                            buf:put "["
                            @putValue k
                            buf:put "]"
                        buf:put '  '
                        @putValue t[k]
                
                mt = getmetatable(t)
                if type(mt) == 'table'
                    tabify()
                    buf:put '<meta> '
                    @putValue(mt)
                
                @level = @level - 1
                
                if seqLen > 0
                    buf:put ' '
        else
            if tv == "function"
                buf:put "->"
            elif tv == "table"
                buf:put "<" & @getId(v) & ">"
            else
                buf:put string.format('<%s %d>' tv @getId(v))

⮐  (root) -> Inspector(root)
