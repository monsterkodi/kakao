###
    ██     ██   ███████   ████████    ███████   ███████  ████████   
    ███   ███  ███   ███  ███   ███  ███       ███       ███   ███  
    █████████  █████████  ████████   ███████   ███       ███████    
    ███ █ ███  ███   ███  ███             ███  ███       ███   ███  
    ███   ███  ███   ███  ███        ███████    ███████  ███   ███  

    scrollable source map to the right of the fileeditor
###

use ../../../kxk    ▪ post
use ../../theme     ◆ theme
use ../../util/img  ◆ squares
use ../../edit/tool ◆ belt
use                 ◆ mapview

function mapscr extends mapview

    @: @editor ->
        
        super @editor.screen @editor.state
        
        @state.on 'view.changed' @drawKnob
        
        @pointerType = 'pointer'
        
        @knobId = @imgId+0xeeee
        
        @topLine    = 0 # index of first visible line
        @botLine    = 0 # index of last visible line
        @knobHeight = 0 # height of the knob in pixels
        @mapWidth   = 0 # width of the map in pixels
        @mapHeight  = 0 # height of the map in pixels
        @linesInMap = 0 # number of lines that fit into map
        
        @setColor 'bg'        theme.editor.mapscr
        @setColor 'highlight' theme.highlight.map
        @setColor 'selection' theme.selection.map
        @setColor 'fullysel'  theme.selection.mapfully
        
        @screen.t.on 'preResize' @clearImages
        
        post.on 'popup.show' (name) => @hide() if name in ['searcher' 'finder' 'differ']
        post.on 'greet.show' @hide
        
        post.on 'popup.hide' @show
        post.on 'greet.hide' @show
        
        @calcView()
        
    getSegls:  => @state.segls
    getSyntax: => @state.syntax
    
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event =>
        
        super event
        
        switch event.type
            
            'press'
            
                if @hover
                    @doDrag = true
                    post.emit 'pointer''grabbing'
                    ⮐  @scrollToPixel event.pixel
                    
            'drag'
            
                if @doDrag
                    post.emit 'pointer''grab'
                    ⮐  @scrollToPixel event.pixel
                    
                @hover = false
                
            'release'
            
                if @doDrag
                    delete @doDrag 
                    post.emit 'pointer''pointer' if @hover
                    ⮐  true
        @hover
    
    # 00000000   00000000   0000000  000  0000000  00000000  
    # 000   000  000       000       000     000   000       
    # 0000000    0000000   0000000   000    000    0000000   
    # 000   000  000            000  000   000     000       
    # 000   000  00000000  0000000   000  0000000  00000000  
    
    onResize: =>
        
        ⮐  if empty @cells.screen.t.pixels
        
        @csz = @cells.screen.t.cellsz
        @calcView()
        @redraw = true
        
    #  0000000   0000000  00000000    0000000   000      000      000000000   0000000   
    # 000       000       000   000  000   000  000      000         000     000   000  
    # 0000000   000       0000000    000   000  000      000         000     000   000  
    #      000  000       000   000  000   000  000      000         000     000   000  
    # 0000000    0000000  000   000   0000000   0000000  0000000     000      0000000   
    
    scrollToPixel: pixel =>
        
        # log 'scrollToPixel'
        
        view = @state.s.view.asMutable()
        
        view[1] = int (pixel[1] - @cells.y * @cells.screen.t.cellsz[1]) / @pixelsPerRow
        
        view[1] -= 6 # offset so that clicking inside a header scrolls it into view
        
        maxY = @state.s.lines.length - @cells.rows
        
        view[1] = min maxY view[1] if maxY > 0
        view[1] = max 0 view[1]
        
        ⮐  if view eql @state.s.view
        
        @state.setView view
        
        @calcView()
        @drawKnob()
        
        redraw:true
        
    #  ███████   ████████  ████████   ███████  ████████  █████████
    # ███   ███  ███       ███       ███       ███          ███   
    # ███   ███  ██████    ██████    ███████   ███████      ███   
    # ███   ███  ███       ███            ███  ███          ███   
    #  ███████   ███       ███       ███████   ████████     ███   

    calcView: ->
    
        @mapHeight  = @cells.rows * @csz[1]
        @linesInMap = int @mapHeight / @pixelsPerRow
        
        @mapWidth   = @cells.cols*@csz[0]
        @knobHeight = @state.cells.rows*@pixelsPerRow
        
        @topLine    = 0 
        @botLine    = min @state.s.lines.length-1 @topLine+@linesInMap 
        
        log "calcView top #{@topLine} bot #{@botLine}"

    lineOffset: y -> (y-@topLine)*@pixelsPerRow
        
    pixelPos: pos -> 
       [
           @cells.x*@csz[0] + pos[0]*@pixelsPerCol
           @cells.y*@csz[1] + @lineOffset(pos[1])
       ]
    
    ###
    ███████    ████████    ███████   ███   ███
    ███   ███  ███   ███  ███   ███  ███ █ ███
    ███   ███  ███████    █████████  █████████
    ███   ███  ███   ███  ███   ███  ███   ███
    ███████    ███   ███  ███   ███  ██     ██
    ###
    
    draw: =>
    
        ⮐  if @hidden() or @collapsed()
        
        super()
        
        if @csz
        
            @drawCursors()    
            @drawHighlights() 

    # ███  ██     ██   ███████    ███████   ████████   ███████
    # ███  ███   ███  ███   ███  ███        ███       ███     
    # ███  █████████  █████████  ███  ████  ███████   ███████ 
    # ███  ███ █ ███  ███   ███  ███   ███  ███            ███
    # ███  ███   ███  ███   ███   ███████   ████████  ███████ 

    drawImages: =>
        
        t = @cells.screen.t
        
        ⮐  if empty t.pixels or @hidden() or @collapsed()
        
        for y in @topLine..@botLine
            id = @images[y]
            t.placeLineImage id @cells.x @cells.y @lineOffset(y) @pixelsPerRow
        
        @drawKnob()
                
    # ███   ███  ███   ███   ███████   ███████  
    # ███  ███   ████  ███  ███   ███  ███   ███
    # ███████    ███ █ ███  ███   ███  ███████  
    # ███  ███   ███  ████  ███   ███  ███   ███
    # ███   ███  ███   ███   ███████   ███████  

    drawKnob: =>
        
        t = @cells.screen.t
        
        ⮐  if empty t.pixels or @hidden() or @collapsed()
        
        y  = @pixelsPerRow*@state.s.view[1]/@csz[1]
        yc = int(y) 
        yr = int((y - yc) * @csz[1])
        
        t.placeImageOverlay @knobId @cells.x @cells.y+yc yr @mapWidth @knobHeight

    hide: =>
    
        ⮐  if @hidden()
        
        @cells.screen.t.hideImageOverlay @knobId
        
        super()
        
    #  ███████  ███   ███  ████████    ███████   ███████   ████████    ███████
    # ███       ███   ███  ███   ███  ███       ███   ███  ███   ███  ███     
    # ███       ███   ███  ███████    ███████   ███   ███  ███████    ███████ 
    # ███       ███   ███  ███   ███       ███  ███   ███  ███   ███       ███
    #  ███████   ███████   ███   ███  ███████    ███████   ███   ███  ███████ 

    drawCursors: =>
        
        mw = @cells.cols*@csz[0]
    
        for pos,idx in @state.s.cursors
        
            if pos[0]*@pixelsPerCol < mw
            
                [sx sy] = @pixelPos pos 
            
                if idx == @state.s.main
                    fg = theme.cursor.main
                    sw = @pixelsPerCol*2
                    sx -= int @pixelsPerCol/2
                else
                    fg = theme.cursor.multi
                    sw = @pixelsPerCol
                    
                squares.place sx sy sw @pixelsPerRow fg
                
                if idx == @state.s.main
                    squares.place @cells.x*@csz[0]+mw-@pixelsPerCol*4 sy @pixelsPerCol*4 @pixelsPerRow fg 2002

    # 000   000  000   0000000   000   000  000      000   0000000   000   000  000000000   0000000  
    # 000   000  000  000        000   000  000      000  000        000   000     000     000       
    # 000000000  000  000  0000  000000000  000      000  000  0000  000000000     000     0000000   
    # 000   000  000  000   000  000   000  000      000  000   000  000   000     000          000  
    # 000   000  000   0000000   000   000  0000000  000   0000000   000   000     000     0000000   
    
    drawHighlights: =>
        
        mw = @cells.cols*@csz[0]
        mc = @state.mainCursor()
        
        rgtx = int @cells.cols*@csz[0]/@pixelsPerCol
        selw = @pixelsPerCol*16
        hlw  = @pixelsPerCol*8
    
        for li in belt.lineIndicesForRanges @state.s.selections
        
            if @state.isSpanSelectedLine li 
                clr = @color.selection
            else
                clr = @color.fullysel
                
            [sx sy] = @pixelPos [rgtx li] 
            squares.place sx-selw sy selw @pixelsPerRow clr 2000
    
        for li in belt.lineIndicesForSpans @state.s.highlights
        
            [sx sy] = @pixelPos [rgtx li] 
            squares.place sx-hlw sy hlw @pixelsPerRow @color.highlight 2001

export mapscr
