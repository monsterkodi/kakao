
#  ███████   ████████   ████████    ███████   ███   ███
# ███   ███  ███   ███  ███   ███  ███   ███   ███ ███ 
# █████████  ███████    ███████    █████████    █████  
# ███   ███  ███   ███  ███   ███  ███   ███     ███   
# ███   ███  ███   ███  ███   ███  ███   ███     ███   

class array

    @: ... ->
    
        for v in ...
            @insert v

    $: -> noon(@)

    str: -> $@
    len: -> @len
    
    @num: a -> 
        if type(a) == "table"
            if
                a.len > 0 
                    ⮐  a.len 
                type(a["len"]) == "function"
                    ⮐  a∙len()
        0
    
    @from: a -> 
        
        if a.class == immutable
            ⮐  a∙arr()
        array unpack(a)
        
    @str:  a -> noon(a)
    arr:  -> array.from @
    each: -> i=0; -> i+=1; (@[i] i)
    @each: a -> i=0; -> i+=1; (a[i] i)
    
    join: c="" -> @concat c
    
    map: f ->
    
        t = @.class()
        for i v in @
            t[i] = f v
        t
        
    @map: a f ->
    
        t = []
        n = array.num a
        if n > 0 
            for i in 1..n
                t[i] = f(a[i])
        t
    
    filter: f ->
    
        t = @.class()
        for i v in @
            if f v i
                t∙push v
        t
        
    shift: -> 

        if @len > 0 ➜ @remove 1
        
    pop: -> 

        if @len > 0 ➜ @remove @len
        
    push: ... -> 

        for v in ...
            @[@len+1] = v
        @
        
    unshift: ... -> 

        l = select("#", ...)        
        if l > 0                    
            for i in l..1
                v = select(i, ...)     
                @insert 1 v
        @
        
    splice: (i n ...) ->
        
        if n > 0
            for d in 1..n
                @remove i
        a = @.class(...)
        while a.len > 0
            @insert i a∙pop()
        @
        
    slice: first last ->

        s = @.class()
            
        ⮐  s if first > @len
        if last == nil ➜ last = @len
        else last = min @len last
        ⮐  s if last < 1 or last < first
        for i in first...last+1
            s∙push @[i]
        s
        
    @slice: a first last ->
    
        s = []
        if a.class and a.class != immutable
            s = a.class()

        ⮐  s if first > a∙len()
        if last == nil ➜ last = a∙len()
        else last = min a∙len() last
        ⮐  s if last < 1 or last < first
        for i in first...last+1
            s∙push a[i]
        s
        
    keydict: key ->

        t = {}
        for i v in @
            t[v[key]] = v
        t
    
    indexdict: ->

        t = {}
        for i v in @
            t[v] = i
        t
        
    indexof: a s=1 -> array.indexof @ a s
                    
    @indexof: t a s=1 ->
    
        al = array.num a
        sl = array.num t
        
        if sl > 0 and al > 0 and s <= sl-al+1
            for i in s..sl
                match = true
                for j w in a
                    if t[i+j-1] != w 
                        match = false
                        break
                if match
                    ⮐  i
        -1
        
    rindexof: a ->
    
        al = array.num a
        if @len > 0 and al > 0 and al <= @len 
            for i in @len-al+1..1
                match = true
                for w j in array.each(a)
                    if @[i+j-1] != w 
                        match = false
                        break
                if match
                    ⮐  i
        -1

    find: e ->
    
        for i v in @
            if v == e ➜ ⮐  i 
            if v is array and v∙eql(e) ➜ ⮐  i
        -1

    rfind: e ->
    
        if @len > 0
            for i in @len..1
                if @[i] == e ➜ ⮐  i 
                if @[i] is array and @[i]∙eql(e) ➜ ⮐  i
        -1
        
    eql: o ->
        ⮐  false if @len != array.num(o)
        for i v in @
            if v != o[i]
                if v is array
                    if not v∙eql o[i]
                        ⮐  false
                else
                    ⮐  false 
        true
        
    rnd: -> @[math.random(@len)]
    
    sort: f ->
    
        @sort f
        @
    
    swap: i j -> 
    
        ⮐  if i < 1 or i > @len
        ⮐  if j < 1 or j > @len
        ⮐  if i == j 
        (@[i] @[j]) = (@[j] @[i])
        
    shuffle: ->
    
        for i in 1..@len
            @swap i math.random(@len)
        
    has:      e -> @find(e)    >= 1
    contains: e -> @indexof(e) >= 1
    
    __add: a ->
    
        s = @.class.from @
        for o in a
            table.insert s o
        s

    @isarr: a ->
        
        ⮐  false if type(a) != "table"
        a.len > 0

array∙include table

⮐  array