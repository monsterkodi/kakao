###
    000   000  000   000    
    000  000   000  000     
    0000000    0000000      
    000  000   000  000     
    000   000  000   000    
    
    commandline tool that builds the application
###

import os       from '../lib/kxk/os.js'
import karg     from '../lib/kxk/karg.js'
import slash    from '../lib/kxk/slash.js'
import knrd     from './knrd.js'
import build    from './build.js'
import process  from 'process'
import childp   from 'child_process'
import path     from 'path'
import fs       from 'fs/promises'

args = karg """
    kk
        options                                  **
        info       show build status             = false
        knrd       transpile kode, styl, pug     = false
        build      build application executable  = false
        rebuild    rebuild all targets           = false -R
        test       run tests                     = false
        run        run application executable    = false
        clean      remove transpilated files     = false 
        verbose    log more                      = false
        quiet      log nothing                   = false
        debug      log debug                     = false
    """

class kk
    
    @run: ○->
        
        sleep = (ms) ○-> await new Promise((r) => setTimeout(r, ms)); true
        
        while not os.loaded # todo: move this check somewhere else!
            await sleep 150
            
        if not (args.info or args.test or args.knrd or args.build or args.run or args.clean or args.rebuild)
            # no command given? assume all of them for now :)
            args.info  = true
            args.knrd  = true
            args.build = true
            args.test  = true
            args.run   = true
        
        if args.info    ➜ await kk.info()
        if args.knrd    ➜ await knrd args.options; delete args.options
        if args.build   ➜ await build()
        if args.test    ➜ await kk.test()
        if args.run     ➜ await kk.spawn()
        if args.clean   ➜ await kk.clean()
        if args.rebuild ➜ await kk.rebuild()                            
            
        if valid args.options
            log 'leftover options' args.options
                                                
    @rebuild: ○->
        
        await knrd()
        await build()
        kk.spawn()
        
    @spawn: ->
        
        cmd = slash.path import.meta.dirname, '../../Contents/MacOS/kakao'
        opt = shell:true detached:true
        
        childp.spawn cmd, [], opt
        
    @appPath: -> slash.path import.meta.dirname, '../../'
        
    @appName: -> slash.name kk.appPath()
        
    @info: ○->
        
        log 'info' kk.appPath()
        
    @test: ○->
        
        cmd = "node js/test/test.js"
        opt = shell:true cwd:kk.appPath() # build inside the kokoa.app/src folder
        
        new Promise (resolve, reject) ->
        
            childp.exec cmd, opt, (err, stdout, stderr) -> 
            
                if err 
                    error 'ERROR' err
                    reject err
                else
                    log stdout if valid stdout
                    resolve()
                    
    @clean: ○->
        
        jsDir = slash.path import.meta.dirname, '../../js'
        appExe = slash.path import.meta.dirname, '../../Contents/MacOS/kakao'
        await fs.rm jsDir, recursive:true, force:true 
        await fs.unlink appExe
            
global['kk'] = kk
    
export kk.run
