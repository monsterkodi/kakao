###
000   000  00000000  0000000    
000  000   000       000   000  
0000000    0000000   000   000  
000  000   000       000   000  
000   000  00000000  0000000    
###

use ◆ ttio linenr cells
use ../kxk ▪ karg

args = karg """
    ked [file]
        options                                   **
        version    log version                    = false
    """

function KED

    @: ->

        @t = new ttio
        
        @cells  = new cells @t
        @linenr = new linenr @cells
        
        @t.on 'key'    @onKey
        @t.on 'mouse'  @onMouse
        @t.on 'wheel'  @onWheel
        @t.on 'resize' @onResize
        @t.on 'focus'  -> 
        @t.on 'blur'   -> 
            
        if args.version ➜ log '0.0.1'; process.exit 0
                        
        if valid args.options
            log 'file(s):' args.options
            
        @t.setCursor 4 0
        @onResize @t.cols() @t.rows()
        
    @run: -> new KED()
    
    onMouse: event col row button =>
        
        switch event
            'press' ➜ ⮐ @t.write "\x1b[#{row};#{col}H"
        
        # log 'mouse' event col row button
        
    onWheel: dir =>
        
        switch dir
            'up'        
            'down'      
            'left'      
            'right'     ➜ ⮐ @t.moveCursor dir
    
    onKey: key => 
        
        switch key
            'up'        
            'down'      
            'left'      
            'right'     ➜ ⮐ @t.moveCursor key
            'ctrl+a'    ➜ ⮐ @t.write '\x1b[0G'
            'ctrl+e'    ➜ ⮐ @t.write "\x1b[#{@t.cols()}G"
            'ctrl+h'    ➜ ⮐ @t.write '\x1b[H'
            'ctrl+k'    ➜ ⮐ @t.write '\x1b[0K'
            'return'    ➜ ⮐ @t.write '\n'
            'space'     ➜ ⮐ @t.write ' '
            'delete'    ➜ ⮐ @t.write '\x1b[D\x1b[P'
            'tab'       ➜ ⮐ @t.write '    '
            'ctrl+c'    ➜ ⮐ process.exit 0
            'ctrl+q'    ➜ ⮐ process.exit 0
            
        @t.write key
        
    onResize: cols rows =>
        
        @t.hideCursor()
        @cells.init()
        @linenr.draw()
        @cells.render()
        @t.showCursor()
        
export KED.run

