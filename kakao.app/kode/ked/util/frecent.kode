###
    00000000  00000000   00000000   0000000  00000000  000   000  000000000  
    000       000   000  000       000       000       0000  000     000     
    000000    0000000    0000000   000       0000000   000 0 000     000     
    000       000   000  000       000       000       000  0000     000     
    000       000   000  00000000   0000000  00000000  000   000     000     
###

use ../../kxk ▪ kstr pretty post

function frecent

    @buckets: file:{}
    
    @now: -> Date.now()
        
    @frecent: item -> item.rank / (1+@now()-item.time)
        
    @sample: bucket key delta ->
        
        @buckets[bucket][key] ?= rank:1 time:@now()
        
        rec = @buckets[bucket][key]
        
        rec.rank += delta
        rec.time  = @now()
        # log b8('sampled') + ' ' + r5(key)
        for bi in @list(bucket)
            item = @buckets[bucket][bi]
            # log bi item pretty.age(item.time) @frecent(item)

    @score: bucket ->
        for pth,rec of @buckets[bucket]
            rec.score = @frecent rec
            
    @list: bucket ->
        
        @score bucket
        bkt = @buckets[bucket]
        lst = Object.keys bkt
        lst.sort (a b) => bkt[b].score - bkt[a].score
        lst
        
    @store: bucket ->
        
        lst = {}
        for i in @list bucket
            lst[i] = @buckets[bucket][i]
        lst
        
    @fileAction: file action ->
        
        delta = switch action
            'loaded' ➜ 1
            'saved'  ➜ 10
        
        @sample 'file' file delta 
        @score  'file'
        
# 00     00  00000000  00000000    0000000   00000000  
# 000   000  000       000   000  000        000       
# 000000000  0000000   0000000    000  0000  0000000   
# 000 0 000  000       000   000  000   000  000       
# 000   000  00000000  000   000   0000000   00000000  

post.on 'session.merge' (recent) ->
    
    if valid recent.files
        if valid recent.files.recent
            # log """frecent.on session.merge #{g5 "#{noon(recent.files.recent)}"}"""
            maxRecent = 60
            newRecent = {}
            for file in Object.keys(recent.files.recent)[0...maxRecent]
                newRecent[file] = recent.files.recent[file]
            frecent.buckets['file'] = newRecent
            recent.files.recent = frecent.list 'file'

export frecent
