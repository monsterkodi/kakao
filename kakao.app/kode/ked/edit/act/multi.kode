###
00     00  000   000  000      000000000  000    
000   000  000   000  000         000     000    
000000000  000   000  000         000     000    
000 0 000  000   000  000         000     000    
000   000   0000000   0000000     000     000    
###

use ../../../kxk ▪ kseg
use ../../util   ◆ util

export

    #  0000000   000      000      
    # 000   000  000      000      
    # 000000000  000      000      
    # 000   000  000      000      
    # 000   000  0000000  0000000  
    
    allCursors: -> @s.cursors.asMutable()
    
    # 00000000  000   000  00000000    0000000   000   000  0000000    
    # 000        000 000   000   000  000   000  0000  000  000   000  
    # 0000000     00000    00000000   000000000  000 0 000  000   000  
    # 000        000 000   000        000   000  000  0000  000   000  
    # 00000000  000   000  000        000   000  000   000  0000000    
    
    expandCursors: dir ->
        
        cursors = @allCursors()

        dy = dir == 'up' ? -1 : 1
        
        newCursors = []
        for c in cursors
            newCursors.push c
            newCursors.push [c[0] c[1]+dy]

        mc = util.traversePositionsInDirection newCursors @mainCursor() dir
        @setCursors newCursors main:mc
        
    contractCursors: dir ->
        
        cursors = @allCursors()

        newCursors = []
        for c in cursors
            nbup = util.positionsContain cursors util.positionInDirection(c 'down')
            nbdn = util.positionsContain cursors util.positionInDirection(c 'up')
            solo = not (nbup or nbdn)
            add = switch dir
                'up'   ➜ nbup or solo
                'down' ➜ nbdn or solo
            
            newCursors.push c if add

        @setCursors newCursors
        
    #  0000000   0000000    0000000    
    # 000   000  000   000  000   000  
    # 000000000  000   000  000   000  
    # 000   000  000   000  000   000  
    # 000   000  0000000    0000000    
    
    addCursor: x y ->
        
        pos = util.pos x y
        cursors = @allCursors()
        cursors.push pos
        @setCursors cursors main:-1
        
    addCursors: cursors ->
        
        @setCursors @allCursors().concat(cursors)
        
    delCursorsInRange: rng ->
        
        outside = util.positionsOutsideRange @allCursors() rng
        outside.push util.endOfRange(rng)
        @setCursors outside main:-1
        
    # 00     00   0000000   000   000  00000000  
    # 000   000  000   000  000   000  000       
    # 000000000  000   000   000 000   0000000   
    # 000 0 000  000   000     000     000       
    # 000   000   0000000       0      00000000  
    
    moveCursors: dir opt ->
        
        if dir is arr

            switch dir[0]
                'bos' ➜ ⮐ if @moveCursorsToStartOfSelections(); dir = dir[1..]
                'eos' ➜ ⮐ if @moveCursorsToEndOfSelections();   dir = dir[1..]
                    
            dir = dir[0]
        
        opt ?= {}
        opt.count ?= 1
        opt.jumpWords ?= false
        
        if @s.highlights.length
            @deselect()
        
        cursors = @allCursors()
        lines   = @allLines()
        
        for c in cursors
            
            line = lines[c[1]]
            
            switch dir
                'left'  
                'right'     ➜ c[0] += util.numCharsFromPosToWordOrPunctInDirection(lines c dir opt)
                'up'        ➜ c[1] -= opt.count
                'down'      ➜ c[1] += opt.count
                'eol'       ➜ c[0] = kseg.width(@s.lines[c[1]])
                'bol'       ➜ c[0] = 0
                'bof'       ➜ c[0] = 0; c[1] = 0
                'eof'       ➜ c[1] = @s.lines.length-1; c[0] = kseg.width(line)
                'ind'       ➜ c[0] = util.numIndent(line)
                'ind_eol'   ➜ ind  = util.numIndent(line); c[0] = c[0] < ind ? ind : kseg.width(line)
                'ind_bol'   ➜ ind  = util.numIndent(line); c[0] = c[0] > ind ? ind : 0
 
        @setCursors cursors main:@s.main adjust:'topBotDelta'
        true
        
    #  0000000  000   000  00000000    0000000   0000000   00000000   
    # 000       000   000  000   000  000       000   000  000   000  
    # 000       000   000  0000000    0000000   000   000  0000000    
    # 000       000   000  000   000       000  000   000  000   000  
    #  0000000   0000000   000   000  0000000    0000000   000   000  
        
    setMainCursor: x y ->
        
        [x y] = util.pos x y
        
        y = clamp 0 @s.lines.length-1 y
        x = max 0 x
                        
        @setCursors [[x y]]
        
    moveMainCursorInDirection: dir opt={} ->
        
        mc = util.positionInDirection @mainCursor() dir
        
        if opt.keep
            @addCursor mc
        else
            @moveMainCursor mc
                        
    moveMainCursor: x y ->
                
        [x y] = util.pos x y
        
        y = clamp 0 @s.lines.length-1 y
        x = max 0 x
        
        mainCursor = @mainCursor()
        
        ⮐ if mainCursor eql [x y]

        cursors = @allCursors()

        cursors.splice util.indexOfPosInPositions(mainCursor cursors) 1
        
        main = util.indexOfPosInPositions [x y] cursors
        if main < 0
            cursors.push [x y]
            main = cursors.length-1
        
        @setCursors cursors main:main
                
    mainCursor: -> @s.cursors[@s.main].asMutable()
    
    singleCursorAtEndOfLine: ->
        
        rng = util.lineRangeAtPos @allLines() @mainCursor()
        mc  = util.endOfRange rng
        
        @deselect()
        @setCursors [mc]
        
    singleCursorAtIndentOrStartOfLine: ->
        
        lines = @allLines()
        mc    = @mainCursor()
        
        rng   = util.lineRangeAtPos  lines mc
        ind   = util.lineIndentAtPos lines mc
        
        if ind < mc[0]
            mc[0] = ind
        else
            mc  = util.startOfRange rng
        
        @deselect()
        @setCursors [mc]

    singleCursorPage: dir ->

        mc = @mainCursor()
        
        switch dir
            'up'   ➜  mc[1] -= @cells.rows
            'down' ➜  mc[1] += @cells.rows

        @deselect()
        @setCursors [mc]
        
    moveCursorsToStartOfSelections: ->
        
        selections = @allSelections()
        
        ⮐ if empty selections
        
        rngs = util.splitLineRanges @allLines() selections false

        @setCursors util.startPositionsOfRanges(rngs)
        
        true
        
    moveCursorsToEndOfSelections: ->
        
        selections = @allSelections()
         
        ⮐ if empty selections
         
        rngs = util.splitLineRanges @allLines() selections false

        @setCursors util.endPositionsOfRanges(rngs)
        
        true
        
    moveCursorsToEndOfLines: ->
        
        cursors = @allCursors()
        lines   = @allLines()
        
        for cur in cursors
            cur[0] = util.lineRangeAtPos(lines cur)[2]
        
        @setCursors cursors
        
        true
                
    isAnyCursorInLine: y ->
        
        for c in @allCursors()
            ⮐ true if c[1] == y
            
    wordAtCursor:      -> util.wordAtPos      @s.lines @mainCursor()
    chunkBeforeCursor: -> util.chunkBeforePos @s.lines @mainCursor()
    chunkAfterCursor:  -> util.chunkAfterPos  @s.lines @mainCursor()
        
    #  0000000  00000000  000      00000000   0000000  000000000  
    # 000       000       000      000       000          000     
    # 0000000   0000000   000      0000000   000          000     
    #      000  000       000      000       000          000     
    # 0000000   00000000  0000000  00000000   0000000     000     
    
    setMainCursorAndSelect: x y ->
        
        @setSelections util.extendLineRangesFromPositionToPosition @allLines() @allSelections() @mainCursor() [x y]
        @setCursors [[x y]] adjust:'topBotDelta'
    
    moveCursorsAndSelect: dir opt ->
        
        [selections cursors] = util.extendLineRangesByMovingPositionsInDirection @allLines() @allSelections() @allCursors() dir opt
        
        @setSelections selections
        @setCursors cursors adjust:'topBotDelta'

