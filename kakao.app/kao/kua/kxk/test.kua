
# █████████  ████████   ███████  █████████
#    ███     ███       ███          ███   
#    ███     ███████   ███████      ███   
#    ███     ███            ███     ███   
#    ███     ████████  ███████      ███   

use os

rawpairs = t -> (next t nil)

_G.testFail = 0

class test

    $: -> ⮐  "▸" & @suite
    
    @: s t ->
        @suite = s  
        _G.testStack ?= {}
        _G.testIndex = 0
        table.insert testStack @suite
        if _G.testStack.len == 1
            write ◌g- "▸ " ◌g @suite
        else
            write "  " string.rep("    " testStack.len-1) ◌g- @suite
        t()
        table.remove testStack
        if testStack.len == 0
            # log "exit" _G.testFail
            os.exit _G.testFail
    
test.static.cmp = a b ->

    _G.testIndex += 1
    
    fail = msg ->
        write ◌r "✘ " ◌r- "[" _G.testIndex "] " ◌y msg
        _G.testFail += 1
        ⮐  false
    
    if type(a) != type(b)
        ⮐  fail ◌d- & "type mismatch " & ◌b- & "◇" & ◌b & type(a) & ◌r & " != " & ◌b- & "◇" & ◌b & type(b) & ◌d & " (" & $a & " != " & $b & ")"
        
    switch type(a) 
        "table"
            for k v in rawpairs a
                if type(v) != "function" and (type(k) != "string" or k∙sub(1 2) != "__")
                    # log "cmp" k, type(v)
                    if not test.static.cmp v b[k]
                        key = k
                        if type(k) == "number"
                            key = kstr.index k
                        ⮐  fail ◌d- & "table mismatch at " & ◌b & $key & "\n" & ◌g & $[unpack(a)] & ◌r & "!=\n" & ◌y &  $[unpack(b)]
        "number"
            if math.abs(a - b) > 1e-10
                ⮐  fail ◌d- & "number mismatch " & ◌g & $a & ◌r & " != " & ◌y & $b
        ➜  
            if a != b ➜ ⮐  fail $a & " != " & $b # & " ◇" & type(a)
    true
    
test.static.run = files ->

    # log "test.run" array.str files
    success = true
    for f _ in pairs files
        # log "test" f
        (output ok exitcode) = slash.shell "luajit" f
        if ok
            # log output
            # log output, ok, exitcode
            # log "✔ " f
            a = 1
        else
            log output
            write ◌r "✘ " ◌b slash.tilde(slash.dir(f)) "/" ◌aaf slash.name(f) ◌66f "." slash.ext(f)
            success = false
    success
        
⮐  test
