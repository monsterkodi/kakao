###
 0000000   0000000   00     00  00000000   000      00000000  000000000  00000000
000       000   000  000   000  000   000  000      000          000     000     
000       000   000  000000000  00000000   000      0000000      000     0000000 
000       000   000  000 0 000  000        000      000          000     000     
 0000000   0000000   000   000  000        0000000  00000000     000     00000000
###

use ../../kxk ▪ kseg kutil post
use ../theme  ◆ theme 
use ./tool    ◆ belt
use           ◆ specs

function complete

    @: @editor ->
        
        @name = @editor.name + '_complete'
        
        @choices = new choices_class @editor.cells.screen "#{@name}_choices" ['scrllr']
        @choices.focusable = false
        @choices.rounded   = false
        
        @color = bg: theme.editor_selection
        
        @choices.setColor 'bg' theme.editor_complete_choices
        
        @choices.scroll.handle = '▐'
        @choices.scroll.setColor 'bg'   theme.editor_complete_choices
        @choices.scroll.setColor 'knob' theme.editor_complete_choices_scroll
        @choices.scroll.setColor 'dot'  theme.editor_complete_choices_scroll
        
        @choices.on 'action' @onChoicesAction
        
        @visible = false

    #  0000000   0000000   00     00  00000000   000      00000000  000000000  00000000  
    # 000       000   000  000   000  000   000  000      000          000     000       
    # 000       000   000  000000000  00000000   000      0000000      000     0000000   
    # 000       000   000  000 0 000  000        000      000          000     000       
    #  0000000   0000000   000   000  000        0000000  00000000     000     00000000  
    
    complete: -> 
    
        before = @editor.state.chunkBeforeCursor()
        if tct = kseg.tailCountTurd before
            before = before[before.length-1..]
        else if tcw = kseg.tailCountWord before
            if tcw < before.length
                before = before[before.length-tcw..]
        @word before
        
    # 000   000   0000000   00000000   0000000    
    # 000 0 000  000   000  000   000  000   000  
    # 000000000  000   000  0000000    000   000  
    # 000   000  000   000  000   000  000   000  
    # 00     00   0000000   000   000  0000000    
    
    word: @turd ->
    
        if empty @turd
            @hide()
            ⮐ 
                
        @words = kseg.chunks(@editor.state.s.lines).map (chunk) -> chunk.chunk        
        @words = belt.prepareWordsForCompletion @turd @words
        
        if inserts = specs.trigger[@turd]  # prepend special completions, eg ~O 
            @words = inserts.concat @words

        @visible = valid @words

        ⮐ if empty @words
        
        mc = @editor.state.mainCursor()
        
        head = @words[0]
        
        cx = mc[0] - @editor.state.s.view[0]     
        cy = mc[1] - @editor.state.s.view[1]
        
        for ch,ci in head[@turd.length..]
            @editor.cells.set cx+ci cy ch '#fff' theme.selection
        
        if @words.length <= 1
            @choices.clear()                    
        else
            mlw = max 1 belt.widthOfLines(@words)
            
            h = min 8 @words.length
            x  = @editor.cells.x+cx-@turd.length
            y  = @editor.cells.y+cy+1
            
            @choices.layout x y mlw+3 h
            @choices.set @words.map((w) -> ' '+w)
            @choices.selectFirst()
        
    # 000   000  000  0000000    00000000  
    # 000   000  000  000   000  000       
    # 000000000  000  000   000  0000000   
    # 000   000  000  000   000  000       
    # 000   000  000  0000000    00000000  
    
    hide: -> 
        ⮐  if not @visible
        @editor.state.syntax.setSegls @editor.state.s.lines # 𝜏𝖍𝚒𝖘 𝚒𝖘 𝕒 𝔟𝚒𝜏 𝔟ɼ𝓊𝜏𝕒⟅, 𝚒𝖘∩'𝜏 𝚒𝜏?
        @visible = false
        
    hidden: -> not @visible
        
    onEditorLayout: =>
    
        ⮐  if @hidden()
        
    # ███   ███  ████████  ███   ███
    # ███  ███   ███        ███ ███ 
    # ███████    ███████     █████  
    # ███  ███   ███          ███   
    # ███   ███  ████████     ███   

    handleKey: key event ->
        
        ⮐ 'unhandled' if @hidden() or empty @words
        
        switch key
            'tab'
            'right'
            'return' ➜ ⮐ @apply()
            'esc'    ➜ ⮐ @hide()
            'up'
            'down' 
                if @words.length > 1
                    ⮐ @moveSelection key
        
        'unhandled'
    
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event =>
        
        ⮐ if @hidden()
        
        cret = @choices.onMouse event
        
        if not cret and event.type in ['press' 'drag' 'release']
            @hide()
            ⮐ 
        
        cret

    onWheel: event => 
        
        ⮐ if @hidden()
        
        @choices.onWheel event
        
    #  0000000  00000000  000      00000000   0000000  000000000  
    # 000       000       000      000       000          000     
    # 0000000   0000000   000      0000000   000          000     
    #      000  000       000      000       000          000     
    # 0000000   00000000  0000000  00000000   0000000     000     
    
    moveSelection: dir ->
        
        @choices.moveSelection dir
        
    #  0000000   00000000   00000000   000      000   000  
    # 000   000  000   000  000   000  000       000 000   
    # 000000000  00000000   00000000   000        00000    
    # 000   000  000        000        000         000     
    # 000   000  000        000        0000000     000     
    
    apply: ->
    
        word = @currentWord()
        
        if specs.inserts[word]
            @editor.state.delete 'back'
            @editor.state.insert word
        else
            @editor.state.insert word[@turd.length..]
                    
        post.emit 'focus' 'editor'
        @complete()
        
    #  ███████  ███   ███   ███████   ███   ███████  ████████    ███████          ███████    ███████  █████████  ███  ███   ███
    # ███       ███   ███  ███   ███  ███  ███       ███        ███              ███   ███  ███          ███     ███  ████  ███
    # ███       █████████  ███   ███  ███  ███       ███████    ███████          █████████  ███          ███          ███ █ ███
    # ███       ███   ███  ███   ███  ███  ███       ███             ███         ███   ███  ███          ███          ███  ████
    #  ███████  ███   ███   ███████   ███   ███████  ████████   ███████          ███   ███   ███████     ███          ███   ███

    onChoicesAction: action choice=>
        
        switch action
            'click' ➜ ⮐ @apply()
            
    #  ███████  ███   ███  ████████   ████████   ████████  ███   ███  █████████      ███   ███   ███████   ████████   ███████  
    # ███       ███   ███  ███   ███  ███   ███  ███       ████  ███     ███         ███ █ ███  ███   ███  ███   ███  ███   ███
    # ███       ███   ███  ███████    ███████    ███████   ███ █ ███     ███         █████████  ███   ███  ███████    ███   ███
    # ███       ███   ███  ███   ███  ███   ███  ███       ███  ████     ███         ███   ███  ███   ███  ███   ███  ███   ███
    #  ███████   ███████   ███   ███  ███   ███  ████████  ███   ███     ███         ██     ██   ███████   ███   ███  ███████  

    currentWord: ->
        
        word = @choices.current trim:'front'
        if empty word
            word = @words[0]
        word
        
    preDrawLines: lines ->
    
        ⮐  lines if @hidden() or empty @words
        ● pre draw
        word  = @currentWord()
        lines = lines.asMutable()
        for pos in @editor.state.s.cursors
            kutil.replace lines[pos[1]] pos[0] 0 kseg(word[@turd.length..])
        @editor.state.syntax.setSegls kseg.segls(lines) # 𝜏𝖍𝚒𝖘 𝚒𝖘 𝕒 𝔟𝚒𝜏 𝔟ɼ𝓊𝜏𝕒⟅, 𝚒𝖘∩'𝜏 𝚒𝜏?
        lines
        
    # 0000000    00000000    0000000   000   000  
    # 000   000  000   000  000   000  000 0 000  
    # 000   000  0000000    000000000  000000000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000  00     00  
    
    draw: ->
        
        ⮐  if @hidden() or empty @words

        word = @currentWord()
        
        # log "complete #{word} #{@choices.current()} #{@words.length}" @words
        
        for pos in @editor.state.s.cursors # highlight the insertions
            
            cx = pos[0] - @editor.state.s.view[0]
            cy = pos[1] - @editor.state.s.view[1]
            
            for ci in 0...word.length-@turd.length
                @editor.cells.set_bg cx+ci cy @color.bg

        ⮐  if @words.length <= 1
        
        # draw the popup below the main cursor
        
        mc = @editor.state.mainCursor()
         
        cx = mc[0] - @editor.state.s.view[0]
        cy = mc[1] - @editor.state.s.view[1]
        
        fx = cx-@turd.length-1
        x  = fx+1+@editor.cells.x
        y  = cy+@editor.cells.y+2
        w  = @choices.cells.cols+1
        h  = @choices.cells.rows
        
        fy = cy+1

        @editor.cells.draw_frame fx fy fx+w+1 fy+h+1 fg:theme.editor_complete_choices bg:'#000'
        
        @choices.layout x y w h
        @choices.draw()

export complete
