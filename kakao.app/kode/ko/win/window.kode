###
000   000  000  000   000  0000000     0000000   000   000
000 0 000  000  0000  000  000   000  000   000  000 0 000
000000000  000  000 0 000  000   000  000   000  000000000
000   000  000  000  0000  000   000  000   000  000   000
00     00  000  000   000  0000000     0000000   00     00
###

use ../../kakao

# { klog, post, prefs, scheme, stash, stopEvent, store, win } = require 'kxk'

use ../../kxk/post
use ../../kxk/stash
use ../../kxk/store
use ../../kxk/prefs

use ./split
use ./info
use ./tabs
use ./terminal
use ./titlebar
use ./filehandler
use ../tools/watcher
use ../tools/fps
use ../tools/scheme
use ../editor/editor
use ../commandline/commandline
use ../editor/fileeditor
use ../tools/cwd
use ../tools/projects
use ../main/navigate

# 000   000  000  000   000  0000000     0000000   000   000  
# 000 0 000  000  0000  000  000   000  000   000  000 0 000  
# 000000000  000  000 0 000  000   000  000   000  000000000  
# 000   000  000  000  0000  000   000  000   000  000   000  
# 00     00  000  000   000  0000000     0000000   00     00  

function Window #extends win

    onWindowAnimationTick:  (win, tickInfo) -> #log 'onWindowAnimationTick' win.id
    onWindowResize:         (win, event)    -> log 'onWindowResize'        win.id
    onWindowFocus:          (win)           -> #log 'onWindowFocus'         win.id
    onWindowBlur:           (win)           -> #log 'onWindowBlur'          win.id
    onWindowKeyDown:        (win, keyInfo)  -> #log 'onWindowKeyDown'       win.id, keyInfo
    onWindowKeyUp:          (win, keyInfo)  -> #log 'onWindowKeyUp'         win.id, keyInfo
    
    @: ->
        
        @menuIcon = kakao.bundle.img 'menu_ko.png'
        @menuNoon = kakao.bundle.res 'menu_ko.noon'
        
        post.on 'menuAction' @onMenuAction
        
        window.aboutImage = kakao.bundle.img 'about_ko.png'
        
    onWindowCreated:        (win)           => 
        
        @id = win.id
        # log 'onWindowCreated' win.id
        
        window.stash = new stash "win/#{@id}" separator:'|'
            
        @filehandler = window.filehandler = new filehandler
        @filewatcher = window.filewatcher = new watcher
        @tabs        = window.tabs        = new tabs window.titlebar.elem
        @titlebar    =                      new titlebar
        @navigate    = window.navigate    = new navigate()
        @split       = window.split       = new split()
        @terminal    = window.terminal    = new terminal 'terminal'
        @editor      = window.editor      = new fileeditor 'editor'
        @commandline = window.commandline = new commandline 'commandline-editor'
        @info        = window.info        = new info @editor
        @fps         = window.fps         = new fps()
        @cwd         = window.cwd         = new cwd()
    
        log 'split' @split
        
        window.textEditor = window.focusEditor = @editor
        window.setLastFocus @editor.name

        scheme.set prefs.get 'scheme' 'dark'

        @terminal.on 'fileSearchResultChange' (file, lineChange) -> # sends changes to all windows
            post.toWins 'fileLineChanges' file, [lineChange]
    
        @editor.on 'changed' (changeInfo) ->
            return if changeInfo.foreign
            if changeInfo.changes.length
                # post.toOtherWins 'fileLineChanges' @editor.currentFile, changeInfo.changes
                if changeInfo.deletes == 1
                    window.navigate.delFilePos file: window.editor.currentFile, pos: [0 changeInfo.changes[0].oldIndex]
                else
                    window.navigate.addFilePos file: window.editor.currentFile, pos: window.editor.cursorPos()
    
        s = window.stash.get 'fontSize' prefs.get 'editorFontSize' 19
        @editor.setFontSize s if s
    
        if window.stash.get 'centerText'
            @editor.centerText true 0
    
        post.emit 'restore'
        
        window.split.resized()
        window.info.reload()
        
        @editor.focus()

    onMoved: (bounds) => window.stash.set 'bounds' bounds
        
    # 00     00  00000000  000   000  000   000      0000000    0000000  000000000  000   0000000   000   000
    # 000   000  000       0000  000  000   000     000   000  000          000     000  000   000  0000  000
    # 000000000  0000000   000 0 000  000   000     000000000  000          000     000  000   000  000 0 000
    # 000 0 000  000       000  0000  000   000     000   000  000          000     000  000   000  000  0000
    # 000   000  00000000  000   000   0000000      000   000   0000000     000     000   0000000   000   000
    
    onMenuAction: (name, opts) =>
        
        log 'onMenuAction' name#, opts
        
        if action = editor.actionWithName name
            
            log 'editor.actionWithName' name
            
            if action.key? and window.focusEditor[action.key] is func
                window.focusEditor[action.key] opts.actarg
                return 
    
        if 'unhandled' != window.commandline.handleMenuAction name, opts
            return 
            
        switch name
    
            'doMacro'               ➜ return window.commandline.commands.macro.execute opts.actarg
            'Undo'                  ➜ return window.focusEditor.do.undo()
            'Redo'                  ➜ return window.focusEditor.do.redo()
            'Cut'                   ➜ return window.focusEditor.cut()
            'Copy'                  ➜ return window.focusEditor.copy()
            'Paste'                 ➜ return window.focusEditor.paste()
            'New Tab'               ➜ return post.emit 'newEmptyTab'
            'New Window'            ➜ return post.toMain 'newWindowWithFile' @editor.currentFile
            'Cycle Windows'         ➜ return post.toMain 'activateNextWindow' window.winID
            'Arrange Windows'       ➜ return post.toMain 'arrangeWindows'
            'Toggle Scheme'         ➜ return scheme.toggle()
            'Toggle Center Text'    ➜ return toggleCenterText()
            'Toggle Tab Pinned'     ➜ return toggleTabPinned()
            'Increase'              ➜ return changeFontSize +1
            'Decrease'              ➜ return changeFontSize -1
            'Reset'                 ➜ return resetFontSize()
            'Open Window List'      ➜ return @titlebar.showList()
            'Navigate Backward'     ➜ return @navigate.backward()
            'Navigate Forward'      ➜ return @navigate.forward()
            'Maximize Editor'       ➜ return @split.maximizeEditor()
            'Add to Shelf'          ➜ return addToShelf()
            'Toggle History'        ➜ return window.filebrowser.shelf.toggleHistory()
            'Activate Next Tab'     ➜ return window.tabs.navigate 'right'
            'Activate Previous Tab' ➜ return window.tabs.navigate 'left'
            'Move Tab Left'         ➜ return window.tabs.move 'left'
            'Move Tab Right'        ➜ return window.tabs.move 'right'
            'Open...'               ➜ return post.emit 'openFile'
            'Open In New Tab...'    ➜ return post.emit 'openFile' newTab: true
            'Open In New Window...' ➜ return post.emit 'openFile' newWindow: true
            'Save'                  ➜ return post.emit 'saveFile'
            'Save All'              ➜ return post.emit 'saveAll'
            'Save As ...'           ➜ return post.emit 'saveFileAs'
            'Revert'                ➜ return post.emit 'reloadFile'
            'Close Tab or Window'   ➜ return post.emit 'closeTabOrWindow'
            'Close Other Tabs'      ➜ return post.emit 'closeOtherTabs'
            'Close Other Windows'   ➜ return post.toOtherWins 'closeWindow'
            'Clear List'            
                window.state.set 'recentFiles' []
                window.titlebar.refreshMenu()
                return 
            'Preferences'           ➜ return post.emit 'openFiles' [prefs.store.file], newTab:true
            'Cycle Windows'         ➜ opts = @id
    
        log 'onMenuAction unhandled' name#, opts            
        # super name, opts
            
# 00000000   00000000   00000000  00000000   0000000
# 000   000  000   000  000       000       000
# 00000000   0000000    0000000   000000    0000000
# 000        000   000  000       000            000
# 000        000   000  00000000  000       0000000

window.state = new store 'state' separator:'|'
window.prefs = prefs

# saveStash = ->
# 
#     post.emit 'saveChanges' 
#     post.emit 'stash'
#     editor.saveScrollCursorsAndSelections()
#     window.stash.save()
#     
# clearStash = ->
#     post.emit 'saveChanges'
#     window.stash.clear()

# 00000000    0000000    0000000  000000000
# 000   000  000   000  000          000
# 00000000   000   000  0000000      000
# 000        000   000       000     000
# 000         0000000   0000000      000
 
post.on 'singleCursorAtPos' (pos, opt) -> # browser double click and newTabWithFile :l:c
    window.editor.singleCursorAtPos pos, opt 
    window.editor.scroll.cursorToTop()
    
post.on 'focusEditor'  -> window.split.focus 'editor'
post.on 'cloneFile'    -> post.toMain 'newWindowWithFile' window.editor.currentFile
post.on 'closeWindow'  -> post.emit 'menuAction' 'Close'
post.on 'saveStash'    -> saveStash()
post.on 'clearStash'   -> clearStash()
post.on 'editorFocus' (editor) ->
    window.setLastFocus window.editor.name
    window.focusEditor = window.editor
    window.textEditor = window.editor if window.editor.name != 'commandline-editor'
 
post.on 'mainlog' -> 
 
post.on 'ping' (wID, argA, argB) -> post.toWin wID, 'pong' window.winID, argA, argB
post.on 'postEditorState' ->
    post.toAll 'editorState' window.winID,
        lines:      window.editor.lines()
        cursors:    window.editor.cursors()
        main:       window.editor.mainCursor()
        selections: window.editor.selections()
        highlights: window.editor.highlights()
 
# 00000000  0000000    000  000000000   0000000   00000000
# 000       000   000  000     000     000   000  000   000
# 0000000   000   000  000     000     000   000  0000000
# 000       000   000  000     000     000   000  000   000
# 00000000  0000000    000     000      0000000   000   000
 
window.editorWithName = (n) ->
 
    switch n
        'command' 
        'commandline' ➜ window.commandline
        'terminal'    ➜ window.terminal
        'editor'      ➜ window.editor
                      ➜ window.editor
 
#  0000000   000   000  000       0000000    0000000   0000000
# 000   000  0000  000  000      000   000  000   000  000   000
# 000   000  000 0 000  000      000   000  000000000  000   000
# 000   000  000  0000  000      000   000  000   000  000   000
#  0000000   000   000  0000000   0000000   000   000  0000000
 
# window.onload = ->
 
    # window.split?.resized()
    # window.info?.reload()
 
# 00000000   00000000  000       0000000    0000000   0000000
# 000   000  000       000      000   000  000   000  000   000
# 0000000    0000000   000      000   000  000000000  000   000
# 000   000  000       000      000   000  000   000  000   000
# 000   000  00000000  0000000   0000000   000   000  0000000
 
reloadWin = ->
 
    saveStash()
    clearListeners()
    post.toMain 'reloadWin' winID:window.winID, file:window.editor.currentFile
 
 # 00000000   00000000   0000000  000  0000000  00000000  
 # 000   000  000       000       000     000   000       
 # 0000000    0000000   0000000   000    000    0000000   
 # 000   000  000            000  000   000     000       
 # 000   000  00000000  0000000   000  0000000  00000000  
 
window.onresize = ->

    window.split.resized()
    window.win?.onMoved window.win.getBounds()
    if window.stash.get 'centerText' false
        window.editor.centerText true, 200

post.on 'split' (s) ->

    window.filebrowser?.resized()
    window.terminal.resized()
    window.commandline.resized()
    window.editor.resized()

#  0000000  00000000  000   000  000000000  00000000  00000000       000000000  00000000  000   000  000000000
# 000       000       0000  000     000     000       000   000         000     000        000 000      000
# 000       0000000   000 0 000     000     0000000   0000000           000     0000000     00000       000
# 000       000       000  0000     000     000       000   000         000     000        000 000      000
#  0000000  00000000  000   000     000     00000000  000   000         000     00000000  000   000     000

toggleCenterText = ->

    if window.state.get "invisibles|#{window.editor.currentFile}" false
        window.editor.toggleInvisibles()
        restoreInvisibles = true

    if not window.stash.get 'centerText' false
        window.stash.set 'centerText' true
        window.editor.centerText true
    else
        window.stash.set 'centerText' false
        window.editor.centerText false

    if restoreInvisibles
        window.editor.toggleInvisibles()
        
toggleTabPinned = ->
    
    if t = window.tabs.activeTab()
        t.togglePinned()

# 00000000   0000000   000   000  000000000      0000000  000  0000000  00000000
# 000       000   000  0000  000     000        000       000     000   000
# 000000    000   000  000 0 000     000        0000000   000    000    0000000
# 000       000   000  000  0000     000             000  000   000     000
# 000        0000000   000   000     000        0000000   000  0000000  00000000

setFontSize = (s) ->

    s = prefs.get('editorFontSize' 19) if not s is num
    s = clamp 8 100 s

    window.stash.set "fontSize" s
    window.editor.setFontSize s
    if window.editor.currentFile?
        post.emit 'loadFile' window.editor.currentFile, reload:true

changeFontSize = (d) ->

    if  window.editor.size.fontSize >= 20 ➜ f = 2
        window.editor.size.fontSize >= 30 ➜ f = 4
        window.editor.size.fontSize >= 50 ➜ f = 10
                                   ➜ f = 1
                                   
    setFontSize window.editor.size.fontSize + f*d

resetFontSize = ->

    defaultFontSize = prefs.get 'editorFontSize' 19
    window.stash.set 'fontSize' defaultFontSize
    setFontSize defaultFontSize

addToShelf = ->

    return if window.lastFocus == 'shelf'
    fb = window.filebrowser
    if window.lastFocus.startsWith fb.name
        path = fb.columnWithName(window.lastFocus).activePath()
    else
        path = window.editor.currentFile
    post.emit 'addToShelf' path

# 0000000   0000000    0000000   00     00
#    000   000   000  000   000  000   000
#   000    000   000  000   000  000000000
#  000     000   000  000   000  000 0 000
# 0000000   0000000    0000000   000   000

resetZoom = ->

    webframe.setZoomFactor 1
    window.editor.resized()

changeZoom = (d) ->
    
    z = webframe.getZoomFactor()
    z *= 1+d/20
    z = clamp 0.36 5.23 z
    webframe.setZoomFactor z
    window.editor.resized()

# 00000000   0000000    0000000  000   000   0000000
# 000       000   000  000       000   000  000
# 000000    000   000  000       000   000  0000000
# 000       000   000  000       000   000       000
# 000        0000000    0000000   0000000   0000000
 
window.onblur  = (event) -> post.emit 'winFocus' false
window.onfocus = (event) ->
    post.emit 'winFocus' true
    if document.activeElement.className == 'body'
        if window.split.editorVisible()
            window.split.focus 'editor'
        else
            window.split.focus 'commandline-editor'
 
window.setLastFocus = (name) -> window.lastFocus = name

# 000   000  00000000  000   000
# 000  000   000        000 000
# 0000000    0000000     00000
# 000  000   000          000
# 000   000  00000000     000

# onCombo = (combo, info) ->

    # return if not combo

    # { mod, key, combo, char, event } = info

    # return stopEvent(event) if 'unhandled' != window.commandline.globalModKeyComboEvent mod, key, combo, event
    # return stopEvent(event) if 'unhandled' != titlebar.globalModKeyComboEvent mod, key, combo, event

    # for i in [1..9]
        # if combo == "alt+#{i}"
            # return stopEvent event, post.toMain 'activateWindow' i

    # switch combo
        # 'f3'              ➜ return stopEvent event, screenShot()
        # 'command+shift+=' ➜ return stopEvent event, changeZoom +1
        # 'command+shift+-' ➜ return stopEvent event, changeZoom -1
        # 'command+shift+0' ➜ return stopEvent event, resetZoom()
        # 'command+alt+y'   ➜ return stopEvent event, split.do 'minimize editor'

# post.on 'combo' onCombo
    
kakao.init -> kwin = new kakao.window new Window
