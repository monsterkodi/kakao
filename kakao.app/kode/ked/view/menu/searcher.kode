###
 0000000  00000000   0000000   00000000    0000000  000   000  00000000  00000000 
000       000       000   000  000   000  000       000   000  000       000   000
0000000   0000000   000000000  0000000    000       000000000  0000000   0000000  
     000  000       000   000  000   000  000       000   000  000       000   000
0000000   00000000  000   000  000   000   0000000  000   000  00000000  000   000
###

use ../../../kxk    ▪ post kseg slash
use ../../../kxk    ◆ nfs
use ../../edit/tool ◆ belt
use ../../util      ◆ prjcts
use                 ◆ finder searcherfile

function searcher extends finder

    @: @screen @state ->

        super @screen @state 'searcher'
        
        @sfils = []
        
    layout: =>
    
        super()
        
        for sfil in @sfils
            y = @choices.cells.y - @choices.state.s.view[1] + sfil.lineIndex
            sfil.layout @choices.cells.x y @choices.cells.cols 1
            
    draw: ->

        ⮐ if @hidden()

        super()
        
        for sfil in @sfils
            if @choices.state.s.view[1] <= sfil.lineIndex < @choices.state.s.view[1]+@choices.cells.rows
                sfil.draw()
            
    onMouse: event =>

        ⮐ if @hidden()
        
        for sfil in @sfils
            ret = sfil.onMouse event ; ⮐ ret if ret?.redraw
        
        super event
        
    emitFileOpen: choice ->
    
        textToHighlight = @input.current()
        onOpen = (p) =>
        
            log "file.loaded #{p}"
            post.emit 'editor.highlight' textToHighlight
            post.removeListener 'file.opened' onOpen
    
        post.on 'file.loaded' onOpen
        # log "emitFileOpen #{@input.current()}"
        super choice
        
    # ███   ███  ███  ███      ███   ███████   ███   ███  █████████        ████          ███████    ████████    ███████   ███   ███
    # ███   ███  ███  ███      ███  ███        ███   ███     ███          ██  ██         ███   ███  ███   ███  ███   ███  ███ █ ███
    # █████████  ███  ███      ███  ███  ████  █████████     ███           ████ ██       ███   ███  ███████    █████████  █████████
    # ███   ███  ███  ███      ███  ███   ███  ███   ███     ███          ██  ███        ███   ███  ███   ███  ███   ███  ███   ███
    # ███   ███  ███  ███████  ███   ███████   ███   ███     ███           █████ █       ███████    ███   ███  ███   ███  ██     ██

    highlightTextAndEmitRedraw: text ->
    
        log 'highlight & redraw'
        @layout()
        @choices.state.highlightText text
        post.emit 'redraw'
        
    #  0000000  000   000   0000000   000   000  
    # 000       000   000  000   000  000 0 000  
    # 0000000   000000000  000   000  000000000  
    #      000  000   000  000   000  000   000  
    # 0000000   000   000   0000000   00     00  
    
    show: text ○->
        
        text = @searchText text
        
        # log g4("#{r6 'searcher'} '#{text}'")
        
        if empty text
            @layout()
            @input.grabFocus()
            post.emit 'redraw'
            ⮐ 
        
        editorFile = ked_session.get 'editor▸file'
        
        dir   = prjcts.dir   editorFile
        files = prjcts.files editorFile

        @input.grabFocus()
        @choices.clearEmpty()        
        @sfils = []

        @layout()
        post.emit 'redraw'

        for file,idx in files
                    
            filet =○ nfs.readText file
            
            segls = kseg.segls filet
            spans = belt.lineSpansForText segls text
            
            if idx == files.length-1
                @highlightTextAndEmitRedraw text
            
            continue if empty spans
            
            ●▸ searcher
            
            log "append #{spans.length} results for #{file}"
            
            front = belt.frontmostSpans spans
            ext = slash.ext file
            @choices.add line:''
            
            index = @choices.items.length
            sfil = new searcherfile @screen "#{@name}_sfil_#{index}"
            sfil.lineIndex = index
            sfil.set slash.relative(file dir)            
            @sfils.push sfil
            
            @choices.add
                ext:   ext
                line:  '●'
                type:  'file'
                path:  file
                row:   0
                col:   0
                
            for span in front
                if @choices.items[-1].row != span[1]-1
                    @choices.add line:''
                @choices.add 
                    ext:   ext
                    line:  ' ' + kseg.str(segls[span[1]])
                    path:  file
                    row:   span[1]
                    col:   span[2]
            
            ●▪ searcher
            
            ⮐  if @hidden()
            
            if idx % 10 == 0 or idx == files.length-1
                @highlightTextAndEmitRedraw text
                                    
    # ███  ███   ███  ████████   ███   ███  █████████       ███████    ███████  █████████  ███   ███████   ███   ███
    # ███  ████  ███  ███   ███  ███   ███     ███         ███   ███  ███          ███     ███  ███   ███  ████  ███
    # ███  ███ █ ███  ████████   ███   ███     ███         █████████  ███          ███     ███  ███   ███  ███ █ ███
    # ███  ███  ████  ███        ███   ███     ███         ███   ███  ███          ███     ███  ███   ███  ███  ████
    # ███  ███   ███  ███         ███████      ███         ███   ███   ███████     ███     ███   ███████   ███   ███

    onInputAction: action text ->
    
        switch action
            
            'submit' ➜ ⮐  @show text
            'change' ➜ ⮐  
            
        super action text

export searcher
