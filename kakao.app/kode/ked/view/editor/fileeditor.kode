###
00000000  000  000      00000000  00000000  0000000    000  000000000   0000000   00000000 
000       000  000      000       000       000   000  000     000     000   000  000   000
000000    000  000      0000000   0000000   000   000  000     000     000   000  0000000  
000       000  000      000       000       000   000  000     000     000   000  000   000
000       000  0000000  00000000  00000000  0000000    000     000      0000000   000   000
###

use ../../../kxk    ▪ post kstr
use ../../edit/tool ◆ belt
use ../../edit      ◆ editor 
use ../base         ◆ view 
use ../menu         ◆ context
use                 ◆ mapscr 

function fileeditor extends editor

    @: screen name ->
        
        features = [
            'scroll'  # subviews
            'gutter' 
            'mapscr' 
            'complete' 
            
            'filepos' # autostart modes
            'replex' 
            'brckts' 
            
            'unype'   # temporary modes
            'salter' 
            'vimple' 
            'uniko' 
            ]
        
        super screen name features
        
        if @feats.mapscr ➜ @mapscr = new mapscr @screen @state; @mapscr.show()
            
        post.on 'editor.highlight' @state.highlightText
        post.on 'goto.line'        @onGotoLine

    ###        
     ███████    ███████   █████████   ███████         ███      ███  ███   ███  ████████  
    ███        ███   ███     ███     ███   ███        ███      ███  ████  ███  ███       
    ███  ████  ███   ███     ███     ███   ███        ███      ███  ███ █ ███  ███████   
    ███   ███  ███   ███     ███     ███   ███        ███      ███  ███  ████  ███       
     ███████    ███████      ███      ███████         ███████  ███  ███   ███  ████████  
    
    
    sets the main cursor:
        ◆ row       zero based line index to move the main cursor to
        ◆ col       optional column for the main cursor
        ◆ view      optional view offset
    ###
        
    onGotoLine: row col view =>
        
        mc = @state.mainCursor()
        
        col ?= mc[0]
        if col is str
            switch col
                'ind' ➜ col = belt.numIndent @state.s.lines[row]
        
        if valid view
            # log "fileeditor.onGotoLine #{row} #{col} #{view[0]} #{view[1]}"
            @state.setView view
            @state.setCursors [[col row]]
        else
            @state.setCursors [[col row]] adjust:'topBotDelta'
            
    #  ███████   ███████   ███   ███  █████████  ████████  ███   ███  █████████
    # ███       ███   ███  ████  ███     ███     ███        ███ ███      ███   
    # ███       ███   ███  ███ █ ███     ███     ███████     █████       ███   
    # ███       ███   ███  ███  ████     ███     ███        ███ ███      ███   
    #  ███████   ███████   ███   ███     ███     ████████  ███   ███     ███   

    onContext: event =>
    
        ⮐  if not @hover
    
        if event.type == 'press' and event.count == 1
            word = @state.textOfSelectionOrWordAtCursor()
            if valid word ➜ word = " '#{word}'"
            context.show event.cell @onContextChoice [
                "search#{word}" 
                "find#{word}" 
                ]
                
    onContextChoice: choice =>

        if choice.startsWith 'search'
            post.emit 'searcher.show' kstr.trim(choice[6..-2] " '")
        else if choice.startsWith 'find'
            post.emit 'finder.show' kstr.trim(choice[4..-2] " '")
        
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event =>
        
        ret = super event ; ⮐  ret if ret?.redraw
        
        [col row] = @cells.posForEvent event
        
        switch event.type
            
            'press'
            
                if event.count > 1 and @hover

                    @state.deselect() if not event.shift and event.button=='left'
                    
                    x = col + @state.s.view[0]
                    y = row + @state.s.view[1]
                    
                    @state.clearHighlights()
                    
                    if event.count == 2
                        if event.alt
                            @state.selectChunk x y
                        else
                            @state.selectWord x y
                    else
                        @state.selectLine y
                        
                    @state.highlightSelection()
                        
                    @dragStart = copy @state.s.selections[0]
                    
                    ⮐  redraw:true
                    
                else if @hover or @gutter?.cells.isInsideEvent(event)
                    
                    x = col + @state.s.view[0]
                    y = row + @state.s.view[1]
                    
                    @dragStart = [x y x]
                                        
                    @state.deselect()     if not event.shift and event.button=='left'
                    @state.clearCursors() if not event.alt

                    if event.alt
                        @state.addCursor x y
                    else
                        if event.shift and @state.s.cursors.length == 1
                            @state.setMainCursorAndSelect x y
                        else
                            @state.setMainCursor x y
                    
                    @grabFocus()
                    
                    ⮐  redraw:true
                    
            'drag'
            
                if @dragStart
                    
                    x = col + @state.s.view[0]
                    y = row + @state.s.view[1]
                    
                    start = [@dragStart[0] @dragStart[1]]
                    
                    if y < @dragStart[1] 
                        start = [@dragStart[2] @dragStart[1]]
                    
                    if event.shift ➜ @state.addRangeToSelectionWithMainCursorAtEnd belt.rangeFromStartToEnd(start [x y])
                    else             @state.select start [x y]
                        
                    ⮐  redraw:true
                    
            'release'
            
                delete @dragStart
                
            'move'
            
                if @hover
                    
                    if not @hasFocus() and empty view.currentPopup
                        @grabFocus()
                    
                    post.emit 'pointer' 'text'
                    
                else if @gutter?.cells.isInsideEvent event
                    
                    post.emit 'pointer' 'vertical-text'
        @hover
        
    # 000   000  000   000  00000000  00000000  000      
    # 000 0 000  000   000  000       000       000      
    # 000000000  000000000  0000000   0000000   000      
    # 000   000  000   000  000       000       000      
    # 00     00  000   000  00000000  00000000  0000000  
    
    onWheel: event =>
        
        ⮐  if event.cell[1] >= @cells.y + @cells.rows
        
        if @dragStart
            
            steps  = 1 # should be 4 if not using a mouse pad -> config
            steps *= 2 if event.shift
            steps *= 2 if event.ctrl
            steps *= 2 if event.alt
            
            [x y] = @state.mainCursor()
            
            switch event.dir
                'up'    ➜ y -= steps    
                'down'  ➜ y += steps    
                'left'  ➜ x -= 1    
                'right' ➜ x += 1    
                
            y = clamp 0 @state.s.lines.length-1 y
            x = clamp 0 @state.s.lines[y].length-1 x
            
            start = [@dragStart[0] @dragStart[1]]
            
            if y < @dragStart[1] 
                start = [@dragStart[2] @dragStart[1]]
            
            if @state.select start [x y]
                @redraw()
            ⮐ 
            
        super event
    
export fileeditor