###
 0000000  00000000  000      00000000   0000000  000000000    
000       000       000      000       000          000       
0000000   0000000   000      0000000   000          000       
     000  000       000      000       000          000       
0000000   00000000  0000000  00000000   0000000     000       
###

use ../../kxk ◆ kstr
use ../util ◆ util

export
    
    #  0000000  00000000  000      00000000   0000000  000000000  
    # 000       000       000      000       000          000     
    # 0000000   0000000   000      0000000   000          000     
    #      000  000       000      000       000          000     
    # 0000000   00000000  0000000  00000000   0000000     000     
    
    select: from to ->
        
        selections = []
        
        @setMainCursor to[0] to[1]
        
        if from[1] > to[1] or from[1] == to[1] and from[0] > to[0]
            [from to] = [to from]

        to[1]   = clamp 0 @s.lines.length-1 to[1]
        from[1] = clamp 0 @s.lines.length-1 from[1]
            
        to[0]   = clamp 0 @s.lines[to[1]].length to[0]
        from[0] = clamp 0 @s.lines[from[1]].length from[0]
        
        selections.push [from[0] from[1] to[0] to[1]]
        
        @set 'selections' selections
        
    allSelections: -> @s.selections.asMutable()
    allHighlights: -> @s.highlights.asMutable()
        
    # 000   000  000   0000000   000   000  000      000   0000000   000   000  000000000  
    # 000   000  000  000        000   000  000      000  000        000   000     000     
    # 000000000  000  000  0000  000000000  000      000  000  0000  000000000     000     
    # 000   000  000  000   000  000   000  000      000  000   000  000   000     000     
    # 000   000  000   0000000   000   000  0000000  000   0000000   000   000     000     
    
    selectWordAtCursor_highlightSelection_selectAllHighlights: ->
        
        if valid @s.highlights 
            pos = @mainCursor()
            if @s.selections.length < @s.highlights.length
                @selectAllHighlights()
            else
                @addNextHighlightToSelection()
            ⮐ 
        
        @selectWordAtCursor_highlightSelection()
        @selectAllHighlights()
    
    highlightWordAtCursor_deselectCursorHighlight_moveCursorToNextHighlight: ->
        
        if valid @s.highlights 
            if not @deselectCursorHighlight()
                @moveCursorToNextHighlight()
            ⮐ 
            
        @selectWordAtCursor_highlightSelection()
        @deselectCursorHighlight()
    
    selectWordAtCursor_highlightSelection_selectNextHighlight: ->
        
        if valid @s.highlights 
            @clearCursors()
            @selectNextHighlight()
            
        @selectWordAtCursor_highlightSelection()
    
    selectWordAtCursor_highlightSelection_addNextHighlightToSelection: ->
        
        if valid @s.highlights ➜ ⮐ @addCurrentOrNextHighlightToSelection()
            
        @selectWordAtCursor_highlightSelection()
        
    deselectCursorHighlight: ->
        
        ⮐ if empty @s.highlights
        ⮐ if empty @s.selections
        
        if prev = util.prevSpanBeforePos @s.highlights @mainCursor()
            @deselectSpan prev

    selectAllHighlights: ->
        
        ⮐ if empty @s.highlights
        
        selections = []
        cursors = []
        for span in @s.highlights
            selections.push util.rangeForSpan(span)
            cursors.push util.endOfSpan(span)
            
        @addCursors cursors        
        @set 'selections' selections
                    
    selectNextHighlight: ->
        
        ⮐ if empty @s.highlights
        
        if next = util.nextSpanAfterPos @s.highlights @mainCursor()
            @selectSpan next
            @setMainCursor util.endOfSpan(next)
                        
    addCurrentOrNextHighlightToSelection: ->            
        
        if prev = util.prevSpanBeforePos @s.highlights @mainCursor()
            if not util.rangesContainSpan @s.selections prev
                @addSpanToSelection prev
                @addCursor util.endOfSpan(prev)
                ⮐ 
                
        @addNextHighlightToSelection()
        
    addNextHighlightToSelection: ->
        
        ⮐ if empty @s.highlights
        
        if next = util.nextSpanAfterPos @s.highlights @mainCursor()
            @addSpanToSelection next
            @addCursor util.endOfSpan(next)
            
    moveCursorToNextHighlight: pos ->
        
        ⮐ if empty @s.highlights
        
        pos ?= @mainCursor()
        
        if next = util.nextSpanAfterPos @s.highlights pos
            @moveMainCursor util.endOfSpan(next)

    selectSpan: span ->
        
        @set 'selections' [util.rangeForSpan(span)]
        
    deselectSpan: span ->
        
        rng = util.rangeForSpan span
        
        selections = @allSelections()
        for selection,index in selections
            if util.isSameRange selection rng
                selections.splice index 1
                @set 'selections' selections
                ⮐ true
        false
            
    addSpanToSelection: span ->
        
        selections = @allSelections()
        
        selections.push util.rangeForSpan(span)
        
        @set 'selections' selections
                
    selectWordAtCursor_highlightSelection: ->
        
        @selectWord @mainCursor() if empty @s.selections
        
        @highlightSelection()
    
    highlightSelection: ->
        
        ⮐ if empty @s.selections
        
        spans = []
        lines = @allLines()
        for selection in @allSelections()
            continue if selection[1] != selection[3]
            text  = util.textForLinesRange lines selection
            spans = spans.concat util.lineSpansForText(lines text)
        
        @set 'highlights' spans
        
    highlightText: text ->
        
        lines = @allLines()
        spans = util.lineSpansForText lines text

        @set 'highlights' spans
        
    #  0000000  000   000  000   000  000   000  000   000  
    # 000       000   000  000   000  0000  000  000  000   
    # 000       000000000  000   000  000 0 000  0000000    
    # 000       000   000  000   000  000  0000  000  000   
    #  0000000  000   000   0000000   000   000  000   000  
    
    selectChunk: x y ->
        
        if range = util.rangeOfClosestChunkToPos @s.lines util.pos(x y)
            @select range[0..1] range[2..3]
        @

    # 000   000   0000000   00000000   0000000    
    # 000 0 000  000   000  000   000  000   000  
    # 000000000  000   000  0000000    000   000  
    # 000   000  000   000  000   000  000   000  
    # 00     00   0000000   000   000  0000000    
    
    selectWord: x y ->
        
        if range = util.rangeOfClosestWordToPos @s.lines util.pos(x y)
            @select range[0..1] range[2..3]
        @
        
    # 000      000  000   000  00000000  
    # 000      000  0000  000  000       
    # 000      000  000 0 000  0000000   
    # 000      000  000  0000  000       
    # 0000000  000  000   000  00000000  
    
    selectLine: y ->
        
        y ?= @mainCursor()[1]
        if 0 <= y < @s.lines.length
            @select [0 y] [@s.lines[y].length y]
        @
        
    selectAllLines: ->
        
        @moveCursor 'bof'
        @moveCursorAndSelect 'eof'
        
    selectMoreLines: ->
        
        cursors    = @allCursors()
        selections = @allSelections()
        lines      = @allLines()
        lastCursor = []
        
        cursorLineAtIndex = c i ->
            range = util.rangeOfLine lines i
            selections.push range
            lastCursor = util.endOfRange range
            
        start = false
        for c in cursors
            if not util.rangesContainLine selections c[1]
                cursorLineAtIndex c c[1]
                start = true
                
        if not start
            for c in cursors
                cursorLineAtIndex c c[1]+1 if c[1] < lines.length-1
                
        @set 'selections' selections
        @set 'cursors' [lastCursor] 0
        
    # 000000000  00000000  000   000  000000000  
    #    000     000        000 000      000     
    #    000     0000000     00000       000     
    #    000     000        000 000      000     
    #    000     00000000  000   000     000     
    
    textForSelection: -> util.textForLinesRanges @allLines() @allSelections()

    isSingleLineSelected: ->
        
        @s.selections.length == 1 and @s.selections[0][1] == @s.selections[0][3]
        
    isSelectedLine: y ->
        
        for selection in @s.selections
            if selection[3] == y and selection[2] == 0
                continue
            if selection[1] <= y <= selection[3]
                ⮐ true
        false
        
    isHighlightedLine: y ->

        for highlight in @s.highlights
            ⮐ true if highlight[1] == y
                
        false

    isFullySelectedLine: y ->
        
        for selection in @s.selections
            if selection[3] == y and selection[2] == 0
                continue
            if selection[1] <= y <= selection[3]
                ⮐ util.isFullLineRange @allLines() selection
        false
        
    # 0000000    00000000   0000000  00000000  000      00000000   0000000  000000000  
    # 000   000  000       000       000       000      000       000          000     
    # 000   000  0000000   0000000   0000000   000      0000000   000          000     
    # 000   000  000            000  000       000      000       000          000     
    # 0000000    00000000  0000000   00000000  0000000  00000000   0000000     000     
    
    deselect: ->
        
        if valid @s.selections
            @set 'selections' []
            
    clearHighlights: ->

        if valid @s.highlights
            @set 'highlights' []
            
    clearCursors: ->
            
        if @s.cursors.length > 1
            @set 'cursors' [@mainCursor()] 0
            
    clearCursorsHighlightsAndSelections: ->
        
        @clearCursors()
        @clearHighlights()
        @deselect()
            