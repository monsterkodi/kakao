###
    000   000  00000000   0000000  000    
    000  000   000   000     000   000    
    0000000    0000000      000    000    
    000  000   000   000   000     000    
    000   000  000   000  0000000  0000000
    
    stores an array of values
    provides a function to filter values by an abbreviation
###

function Krzl

    @: (@values) -> 
    
        @extract = (i) -> i
        @weight  = @defaultWeight
    
    match: (abbrv, exstr) ->
        
        if abbrv is str and exstr is str
            
            ai = 0
            ei = 0
            
            indices = []
            
            while ai < abbrv.length and ei < exstr.length
                ac = abbrv[ai]
                ec = exstr[ei]
                
                if ac == ec
                    indices.push ei
                    ai++
                    ei++
                    continue
                ei++
                
            if ai == abbrv.length
                return 
                    extract:exstr 
                    indices:indices 
        null

    defaultWeight: (pair) ->
        
        [value, info] = pair
        
        # log 'weight' value, info
        
        info.indices[0] + 1-1/info.extract.length
        
    sort: (pairs) ->
        
        pairs.sort (a,b) => @weight(a) - @weight(b)
        
    filter: (abbrv) -> # subset of values that match abbreviation
    
        pairs = []
        
        for value in @values
            if mi = @match abbrv, @extract value
                pairs.push [value, mi]
        
        @sort pairs
        pairs.map (p) -> p[0]
    
export Krzl

