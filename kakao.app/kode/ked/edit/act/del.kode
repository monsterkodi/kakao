###
0000000    00000000  000    
000   000  000       000    
000   000  0000000   000    
000   000  000       000    
0000000    00000000  0000000
###

use ../../../kxk ▪ kstr kseg
use ../tool      ◆ belt
use ..           ◆ mode

export
    
    # 0000000    00000000  000      00000000  000000000  00000000  
    # 000   000  000       000      000          000     000       
    # 000   000  0000000   000      0000000      000     0000000   
    # 000   000  000       000      000          000     000       
    # 0000000    00000000  0000000  00000000     000     00000000  
    
    delete: type jump ->
                
        ⮐ @deleteSelection() if type in ['back' 'next'] and valid @s.selections
        
        lines   = @allLines()
        cursors = @allCursors()
        
        if cursors.length == 1 and type in ['back' 'next'] and belt.isLinesPosOutside lines cursors[0]
            ⮐ @setMainCursor kseg.width(lines[cursors[0][1]]) cursors[0][1]
            
        if type == 'back'
            minBeforeWs = Infinity
            for cursor in cursors
                rng = belt.rangeOfWhitespaceLeftToPos lines cursor
                minBeforeWs = min minBeforeWs rng[2]-rng[0]
                
        for ci in cursors.length-1..0
            
            cursor = cursors[ci]
            
            [x y] = cursor
                            
            line = lines[y]
            
            remove = 1
            dc = 0
            
            switch type
                
                'eol'  ➜ line = line[0...x]
                
                'back' 
                    if x == 0
                        if cursors.length == 1
                            ⮐ if y <= 0
                            y -= 1
                            x = kseg.width(lines[y])
                            remove = 2
                            line = kseg.join(lines[y] line)
                            cursor[0] = x
                            cursor[1] = y
                    else
                        if jump
                            if rng = belt.rangeOfWordOrWhitespaceLeftToPos lines cursor
                                dc = rng[2] - rng[0]
                            else
                                dc = 1
                        else
                            if minBeforeWs > 1
                                dc = x % 4
                                dc = 4 if dc == 0
                                dc = min minBeforeWs dc
                            else
                                dc = 1

                        if x <= kseg.width(line)
                            segi = kseg.indexAtWidth line x
                            line = kseg.join line[0...segi-dc] line[segi..]

                'next' 
                    if x == kseg.width(lines[y])
                        if cursors.length == 1
                            ⮐ if y >= lines.length-1
                            x = kseg.width(lines[y])
                            remove = 2
                            line = kseg.join line lines[y+1]
                            cursor[0] = x
                            cursor[1] = y
                    else
                        if jump
                            if rng = belt.rangeOfWordOrWhitespaceRightToPos lines cursor
                                dc = rng[2] - rng[0]
                                line = kseg.join line[0...x] line[x+dc..]
                        else
                            dc = 1
                            line = kseg.join line[0...x] line[x+dc..]
                        cursor[0] += dc
                                
            belt.moveCursorsInSameLineBy cursors cursor -dc
            
            lines.splice y remove line
            
        @clearHighlights()
        @setLines   lines
        # log 'del.setCursors' cursors
        @setCursors cursors
            
    #  0000000  00000000  000      00000000   0000000  000000000  000   0000000   000   000  
    # 000       000       000      000       000          000     000  000   000  0000  000  
    # 0000000   0000000   000      0000000   000          000     000  000   000  000 0 000  
    #      000  000       000      000       000          000     000  000   000  000  0000  
    # 0000000   00000000  0000000  00000000   0000000     000     000   0000000   000   000  
    
    deleteSelection: -> 
        
        ⮐ if mode.deleteSelection @
    
        @deleteRanges @allSelections() @allCursors()
        
    deleteRanges: rngs posl ->
        
        ⮐ if empty rngs
        
        posl ?= @allCursors()
        
        @pushState() if not @beginIndex

        [lines cursors] = belt.deleteLineRangesAndAdjustPositions @allLines() rngs posl

        @deselect()
        @clearHighlights()
        @setLines   lines
        @setCursors cursors
