###
00000000  0000000    000  000000000   0000000   00000000
000       000   000  000     000     000   000  000   000
0000000   000   000  000     000     000   000  0000000
000       000   000  000     000     000   000  000   000
00000000  0000000    000     000      0000000   000   000
###

use ./buffer
use ./syntax
use ../../kxk/ffs
use ../../kxk/kstr
use ../../kxk/slash

import Do from './do.js'

__dirname = slash.dir import.meta.url[7..]

function Editor extends buffer

    @actions: null

    @: (name, config) ->

        @stringCharacters =
            "'":  'single'
            '"':  'double'

        @bracketCharacters =
            open:
                '[': ']'
                '{': '}'
                '(': ')'
            close:   {}
            regexps: []
        
        super()

        @name   = name
        @config = config ? {}
        @config.syntaxName ?= 'txt'

        @indentString    = lpad 4 ""
        @stickySelection = false
        @syntax          = new syntax @config.syntaxName, @line, @lines
        @do              = new Do @
        
        if empty Editor.actions
            Editor.initActions @
        else
            @actionsInitialized()

    del: ->

        @do.del()
        
    actionsInitialized: =>
        
        # log 'Editor.actionsInitialized'
        
        @setupFileType()

    #  0000000    0000000  000000000  000   0000000   000   000   0000000
    # 000   000  000          000     000  000   000  0000  000  000
    # 000000000  000          000     000  000   000  000 0 000  0000000
    # 000   000  000          000     000  000   000  000  0000       000
    # 000   000   0000000     000     000   0000000   000   000  0000000

    @initActions: (editor) ○->

        Editor.actions = []
        
        filelist = await ffs.list slash.path __dirname, 'actions'
        
        for item in filelist
            # TODO: move this stuff to a config.noon file
            actionFile = item.path
            continue if slash.ext(actionFile) not in ['js' 'mjs' 'coffee' 'kode']
            # log 'action' actionFile
            actions = await import(actionFile)
            # log 'Editor.initActions' actions.default
            for key,value of actions.default
                if value is func
                    @prototype[key] = value
                else if key == 'actions'
                    for k,v of value
                        if not v is str
                            v.key = k if not v.key?
                            Editor.actions.push v

        # log 'Editor.initActions' Editor.actions
        editor?.actionsInitialized()
        # too early for log here!
        # console.log str @actions

    @actionWithName: (name) ->

        log name, Editor.actions
        for action in Editor.actions
            if action.name == name
                return action
        null

    # 000000000  000   000  00000000   00000000
    #    000      000 000   000   000  000
    #    000       00000    00000000   0000000
    #    000        000     000        000
    #    000        000     000        00000000

    shebangFileType: -> @config?.syntaxName ? 'txt'

    setupFileType: ->

        oldType = @fileType
        newType = @shebangFileType()

        @syntax.name = newType if @syntax
        @setFileType newType

        if oldType != @fileType
            @emit 'fileTypeChanged' @fileType

    setFileType: (@fileType) ->

        # _______________________________________________________________ strings

        # @stringCharacters =
            # "'":  'single'
            # '"':  'double'

        switch @fileType
            when 'md'   then @stringCharacters['*'] = 'bold'
            when 'noon' then @stringCharacters['|'] = 'pipe'

        # _______________________________________________________________ brackets

        # @bracketCharacters =
            # open:
                # '[': ']'
                # '{': '}'
                # '(': ')'
            # close:   {}
            # regexps: []

        switch @fileType
            when 'html' then @bracketCharacters.open['<'] = '>'

        for k,v of @bracketCharacters.open
            @bracketCharacters.close[v] = k

        @bracketCharacters.regexp = []
        for key in ['open' 'close']
            cstr = Object.keys(@bracketCharacters[key]).join ''
            reg = new RegExp "[#{kstr.escapeRegexp cstr}]"
            @bracketCharacters.regexps.push [reg, key]

        @initSurround?()

        # _______________________________________________________________ indent

        @indentNewLineMore = null
        @indentNewLineLess = null
        @insertIndentedEmptyLineBetween = '{}'

        switch @fileType
            when 'coffee' 'kode'
                @indentNewLineMore =
                    lineEndsWith: ['->', '=>', ':', '=']
                    lineRegExp:   /^(\s+when|\s*if|\s*else\s+if\s+)(?!.*\sthen\s)|(^|\s)(else\s*$|switch\s|for\s|while\s|class\s)/

        # _______________________________________________________________ comment
        
        # TODO: move this stuff tp a config.noon file
        
        @multiComment = switch @fileType
            'coffee' 'kode'                              ➜ open:'###'  close:'###'
            'html' 'md'                                  ➜ open:'<!--' close:'-->'
            'styl' 'cpp' 'mm' 'c' 'h' 'hpp' 'cxx' 'cs' 
            'js' 'mjs' 'scss' 'ts' 'swift' 'frag' 'vert' ➜ open:'/*'   close:'*/'
        
        @lineComment = switch @fileType
             'coffee' 'kode' 'sh' 'bat' 'noon' 'ko' 'txt' 'fish'    ➜ '#'
             'styl' 'cpp' 'mm' 'c' 'h' 'hpp' 'cxx' 'cs' 
             'js' 'mjs' 'scss' 'ts' 'swift' 'frag' 'vert'           ➜ '//'
             'iss' 'ini'                                            ➜ ';'
             
        if @lineComment
            @headerRegExp = new RegExp("^(\\s*#{kstr.escapeRegexp @lineComment}\\s*)?(\\s*0[0\\s]+)$")

    #  0000000  00000000  000000000         000      000  000   000  00000000   0000000
    # 000       000          000            000      000  0000  000  000       000
    # 0000000   0000000      000            000      000  000 0 000  0000000   0000000
    #      000  000          000            000      000  000  0000  000            000
    # 0000000   00000000     000            0000000  000  000   000  00000000  0000000

    setText: (text="") ->
        
        # klog 'setText' text.length

        if @syntax.name == 'txt'
            @syntax.name = syntax.shebang text.slice 0, text.search /\r?\n/

        lines = text.split /\n/

        @newlineCharacters = '\n'
        if valid lines
            if lines[0].endsWith '\r'
                lines = text.split /\r?\n/
                @newlineCharacters = '\r\n'

        @setLines lines

    setLines: (lines) ->

        @syntax.setLines lines
        super lines
        @emit 'linesSet' lines

    textOfSelectionForClipboard: ->

        if @numSelections()
            @textOfSelection()
        else
            @textInRanges @rangesForCursorLines()

    splitStateLineAtPos: (state, pos) ->

        l = state.line pos[1]
        error "no line at pos #{pos}?" if not l?
        return ['' ''] if not l?
        [l.slice(0, pos[0]), l.slice(pos[0])]

    # 00000000  00     00  000  000000000       00000000  0000000    000  000000000
    # 000       000   000  000     000          000       000   000  000     000
    # 0000000   000000000  000     000          0000000   000   000  000     000
    # 000       000 0 000  000     000          000       000   000  000     000
    # 00000000  000   000  000     000          00000000  0000000    000     000

    emitEdit: (action) ->

        mc = @mainCursor()
        line = @line mc[1]

        @emit 'edit',
            action: action
            line:   line
            before: line.slice 0, mc[0]
            after:  line.slice mc[0]
            cursor: mc

    # 000  000   000  0000000    00000000  000   000  000000000   0000000  000000000  00000000
    # 000  0000  000  000   000  000       0000  000     000     000          000     000   000
    # 000  000 0 000  000   000  0000000   000 0 000     000     0000000      000     0000000
    # 000  000  0000  000   000  000       000  0000     000          000     000     000   000
    # 000  000   000  0000000    00000000  000   000     000     0000000      000     000   000

    indentStringForLineAtIndex: (li) ->

        while empty(@line(li).trim()) and li > 0
            li--

        if 0 <= li < @numLines()

            il = 0
            line = @line li
            thisIndent   = @indentationAtLineIndex li
            indentLength = @indentString.length

            if @indentNewLineMore?
                if @indentNewLineMore.lineEndsWith?.length
                    for e in @indentNewLineMore.lineEndsWith
                        if line.trim().endsWith e
                            il = thisIndent + indentLength
                            break
                if il == 0
                    if @indentNewLineMore.lineRegExp? and @indentNewLineMore.lineRegExp.test line
                        il = thisIndent + indentLength

            il = thisIndent if il == 0
            il = Math.max il, @indentationAtLineIndex li+1

            lpad il, ""
        else
            ''

export Editor
