###
000   000  00000000  0000000    
000  000   000       000   000  
0000000    0000000   000   000  
000  000   000       000   000  
000   000  00000000  0000000    
###

use ../kxk/nfs
use ../kxk ▪ karg kstr slash post
use        ◆ ttio editor state konsole
use ./view ◆ screen cells status quicky
use ./util ◆ logfile util prjcts

function KED

    @: ->
        
        @version = '0.0.3'
        h = ''
        h += b8 "    ███   ███  ████████  ███████  \n"
        h += b7 "    ███  ███   ███       ███   ███\n"
        h += b6 "    ███████    ███████   ███   ███\n"
        h += b5 "    ███  ███   ███       ███   ███\n"
        h += b4 "    ███   ███  ████████  ███████  \n"
        h = '\n\n' + h + '\n'
        
        args = karg """
            
            ked [file]
                options                      **
                
                version    log version       = false
                
            """ preHelp:h version:@version
        
        process.on 'uncaughtException' @onException
            
        @viewSizes = konsole: [0 0]
        
        @logfile = new logfile
                    
        @t = new ttio
        
        global.lfc = args... => lf.apply null args; if global.lc? ➜ global.lc.apply null args 
        
        @screen  = new screen @t
        
        @quicky  = new quicky   @screen 
        @editor  = new editor   @screen 'editor'  ['scroll' 'gutter' 'mapscr' ]
        @konsole = new konsole  @screen 'konsole' ['scroll' 'gutter' 'mapscr' 'knob']
        @status  = new status   @screen @editor.state
        
        lfc "┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━ ked #{@version} ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓" 

        @editor.state.hasFocus = true
        
        post.on 'redraw'        @redraw
        post.on 'window.focus'  @redraw
        post.on 'window.blur'   @redraw
        post.on 'view.size'     @onViewSize
        post.on 'quicky'        @onQuicky
        
        @mouseHandlers = [@quicky @konsole @editor]
        @wheelHandlers = [@quicky @konsole @editor]
        @keyHandlers   = [@quicky @konsole @editor]
        
        @t.on 'key'    @onKey
        @t.on 'mouse'  @onMouse
        @t.on 'wheel'  @onWheel
        @t.on 'resize' @onResize
        @t.on 'paste'  @onPaste
                                    
        if valid args.options
            @loadFile args.options[0]
        else
            @editor.state.syntax.ext = 'txt'
            @editor.state.loadLines ['']
            @t.setCursor 0 0
            @redraw()
            
    # 00000000   000   000  000   000  
    # 000   000  000   000  0000  000  
    # 0000000    000   000  000 0 000  
    # 000   000  000   000  000  0000  
    # 000   000   0000000   000   000  
    
    @run: -> new KED()
    
    quit: msg ->
        
        lf "┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛" 
        @t?.quit()
        @logfile.close ->
            error msg if msg?
            process.exit msg? ? 1 : 0
                
    onException: err => @quit err
                    
    # 000       0000000    0000000   0000000    
    # 000      000   000  000   000  000   000  
    # 000      000   000  000000000  000   000  
    # 000      000   000  000   000  000   000  
    # 0000000   0000000   000   000  0000000    
        
    reloadFile: => @loadFile @status.file
        
    loadFile: p ○=>
        
        start = process.hrtime()
        
        if slash.isAbsolute p
            @status.file = slash.tilde  p
            @currentFile = slash.path p
        else
            @status.file = slash.normalize p
            @currentFile = slash.path process.cwd() p

        text =○ nfs.read slash.untilde(p)
        
        lines = util.linesForText text
        
        @editor.state.syntax.ext = slash.ext p
        @editor.state.loadLines lines
        
        @status.drawTime = kstr.time(BigInt(process.hrtime(start)[1]))
        
        @redraw()
        
        prjcts.index @currentFile
        @
            
    #  0000000   0000000   000   000  00000000  
    # 000       000   000  000   000  000       
    # 0000000   000000000   000 000   0000000   
    #      000  000   000     000     000       
    # 0000000   000   000      0      00000000  
    
    saveFile: ○=>
        
        text = @editor.state.s.lines.asMutable().join '\n'
        
        if valid @status.file
            ○ nfs.write slash.untilde(@status.file) text
            @reloadFile()
            
    saveAs: => lfc 'saveAs'
    
    # 00000000    0000000    0000000  000000000  00000000  
    # 000   000  000   000  000          000     000       
    # 00000000   000000000  0000000      000     0000000   
    # 000        000   000       000     000     000       
    # 000        000   000  0000000      000     00000000  
    
    onPaste: text => 
    
        @editor.state.insert text
        @redraw()
                                    
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event =>

        if event.type == 'wheel'

            for handler in @wheelHandlers
                if handler.onWheel event
                    break
        
        else
        
            for handler in @mouseHandlers
                if handler.onMouse event
                    break

        @redraw()                
                
    # 000   000  00000000  000   000  
    # 000  000   000        000 000   
    # 0000000    0000000     00000    
    # 000  000   000          000     
    # 000   000  00000000     000     
    
    onKey: key event =>
        
        #lf 'ked.onKey' key noon(event)
        
        switch key
            
            'alt+q' 'ctrl+q' 'cmd+esc'   ➜ ⮐ @quit()
            'alt+r' 'ctrl+r' 'cmd+r'     ➜ ⮐ @reloadFile()
            'ctrl+s' 'cmd+s'             ➜ ⮐ @saveFile()
            'shift+cmd+s' 'shift+ctrl+s' ➜ ⮐ @saveAs()
            'cmd+p' 'ctrl+p'             ➜ ⮐ @quicky.toggle @currentFile
            
        for handler in @keyHandlers
            break if handler.onKey key event
                
        @redraw()
        
    #  0000000   000   000  000   0000000  000   000  000   000  
    # 000   000  000   000  000  000       000  000    000 000   
    # 000 00 00  000   000  000  000       0000000      00000    
    # 000 0000   000   000  000  000       000  000      000     
    #  00000 00   0000000   000   0000000  000   000     000     
    
    onQuicky: event ○=>
        
        
        file = slash.path slash.dir(@currentFile) event
        lf 'event' slash.dir(@currentFile) @currentFile event file
        
        if ○ nfs.fileExists file
            ○ @loadFile file
                                
    #  0000000  000  0000000  00000000  
    # 000       000     000   000       
    # 0000000   000    000    0000000   
    #      000  000   000     000       
    # 0000000   000  0000000  00000000  
    
    onViewSize: name x y => 
    
        @viewSizes[name] = [x min(y @screen.rows-1)]
        
        # @editor.mapscr?.onPreResize()
        # @konsole.mapscr?.onPreResize()
        @editor.mapscr?.onResize()
        @konsole.mapscr?.onResize()
        
    onResize: cols rows size => 
        
        @redraw()
        @editor.mapscr?.onResize()
        @konsole.mapscr?.onResize()
    
    # 00000000   00000000  0000000    00000000    0000000   000   000  
    # 000   000  000       000   000  000   000  000   000  000 0 000  
    # 0000000    0000000   000   000  0000000    000000000  000000000  
    # 000   000  000       000   000  000   000  000   000  000   000  
    # 000   000  00000000  0000000    000   000  000   000  00     00  
    
    redraw: =>    

        clearImmediate @redrawId
        @redrawId = setImmediate @draw
                                      
    draw: =>
              
        start = process.hrtime()
        
        w = @t.cols()
        h = @t.rows()
        
        k = @viewSizes.konsole[1]

        @status.gutter = @editor.state.gutterWidth()
        
        @status.init  0  h-1   w     1
        @editor.init  0  0     w h-k-1    
        @konsole.init 0  h-k-1 w   k
                        
        @screen.init()
        
        @editor.draw()
        @konsole.draw()
        @status.draw()
        @quicky.draw()
        
        @screen.render()
        
        @status.drawTime = kstr.time(BigInt(process.hrtime(start)[1]))

export KED.run

◆main
    log '◆main'
    KED.run()

