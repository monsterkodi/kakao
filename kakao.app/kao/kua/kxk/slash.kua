
#  ███████  ███       ███████    ███████  ███   ███
# ███       ███      ███   ███  ███       ███   ███
# ███████   ███      █████████  ███████   █████████
#      ███  ███      ███   ███       ███  ███   ███
# ███████   ███████  ███   ███  ███████   ███   ███

use ffi os
use ./array
use ./kstr
use ./kseg

ffi.cdef """
    typedef struct DIR DIR;
    struct dirent {
        unsigned long  d_ino;           // Inode number
        unsigned long  d_seekoff;       // Seek offset (optional)
        unsigned short d_reclen;        // Length of this record
        unsigned short d_namlen;        // Length of d_name string
        unsigned char  d_type;          // File type
        char           d_name[1024];    // Filename (null-terminated)
    };
    DIR *opendir(const char *filename);
    struct dirent *readdir(DIR *dirp);
    int closedir(DIR *dirp);
    const char *strerror(int errnum);
"""
    
slash = {}
    
slash.cwd = -> process.cwd()

slash.normalize = path ->

    p = kseg.segs(path)
    frst = p[0]
    swallow = 0xffff
    for i in p.len..1
        if i >= swallow
            table.remove p i
            if i == swallow or i == 1
                if p[1] == '/' and frst != '/'
                    kseg.shift p 
                ⮐  slash.normalize(kseg.str(p))
        else
            if p[i] == '\\' ➜ p[i] = '/'
            if i < p.len and p[i] == '/'
                if i < p.len - 1
                    if p[i+1] == '.' and p[i+2] == '/' 
                        p = kseg.splice p i+1 2
                    if p[i+1] == '.' and p[i+2] == '.' and i > 1 and p[i-1] != '.'
                        p = kseg.splice p i 3
                        swallow = i-1
                        while swallow > 0 and p[swallow] != '/'
                            swallow -= 1                    
                if p[i+1] == '/'
                    p = kseg.splice p i+1 1
    
    if p.len >= 4 and kseg.str(kseg.sub(p 1 4)) == "./.."
        kseg.shift p
        kseg.shift p
 
    if p.len > 1 and p[p.len] == '/'
        kseg.pop p
    elif p.len > 1 and p[0] == '/' and frst != '/' and frst != '\\'
        p = kseg.slice(p, 3)
        
    kseg.str p

slash.path = (...) ->

    slsh = s -> string.gsub(s "\\" "/")
    mpty = s -> s.len > 0
    fpth = table.concat(array.map(array.filter({...}, mpty), slsh), "/")
    log fpth
    log slash.normalize fpth
    slash.normalize fpth

slash.isRelative = path -> path.len == 0 or path.len > 0 and path[1] != '/' and path[1] != '~'

slash.home = -> os.getenv "HOME"
slash.untilde = path -> 

    if path[1] == '~' 
        ⮐  slash.path slash.home() kstr.shift(path)
    slash.path path

slash.absolute = path parent ->

    if slash.isRelative path
        parent ?= slash.cwd()
        ⮐  slash.path parent path
    else
        ⮐  slash.untilde path
    
slash.walk = path ->

    dir = ffi.C.opendir path
    if dir == nil
        error "Failed to open directory: " & path & " " & ffi.string(ffi.C.strerror(ffi.errno()))
    
    files = {}
    while true
        entry = ffi.C.readdir dir
        if entry != nil
            name = ffi.string entry.d_name
            if name != "." and name != ".."
                typ = "???"
                if entry.d_type == 4
                    typ = "dir"
                elif entry.d_type == 8
                    typ = "file"
                elif entry.d_type == 10
                    typ = "link"
                    
                info = { "name": name "type": typ "path": slash.absolute(path & "/" & name) }
                table.insert files info
        else
            break
    ffi.C.closedir dir
    table.sort(files, (a b) -> ⮐  a.path < b.path)
    files

slash.files = path ->

    array.map(slash.walk(path), (info) -> ⮐  info.path)
            
⮐  slash
