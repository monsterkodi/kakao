###
00000000   0000000   0000000   0000000   000    
000       000       000       000   000  000    
000000    0000000   000       000   000  000    
000            000  000       000   000  000    
000       0000000    0000000   0000000   0000000
###

use ../../kxk ▪ slash post kstr
use ../../kxk ◆ nfs
use ◆ choices

import rgxs from './quicky.json' with { type: "json" }

function fscol extends choices

    @: screen name ->
        
        super screen name ['scrllr']
        
        @dirOpenSymbol  = ''
        @dirCloseSymbol = ''
        
        @frontRoundOffset = 1
        @state.syntax.setRgxs rgxs
        
    show: dir -> 

        @cells.rows = 1 
        @listDir dir
        
    isCursorVisible: -> false
    visible: -> @cells.rows > 0 
    
    onMouse: event =>
        
        ⮐ if @hidden()
        
        [col row] = @cells.posForEvent event
        
        if @hoverIndex >= 0 and (@cells.isOutsideEvent(event) or @state.isInvalidLineIndex(row))
            ⮐ @unhover()
            
        super event
        
    listDir: dir ○->
        
        try
            items =○ nfs.list dir recursive:false
        catch err
            log 'list error' dir String(err)
            ⮐ 

        for item in items
            item.tilde = slash.file item.path
            item.tilde = ((item.type == 'dir') ? (@dirCloseSymbol + ' ') : '  ') + item.tilde

        weight = item =>

            p = slash.parse item.path

            w = 0
            ⮐ w if item.tilde == @dirCloseSymbol + ' ..'
            w += 10000 if item.type == 'file'
            w += 1000  if item.tilde.startsWith @dirCloseSymbol + ' .'
            w += 1     if slash.ext(item.tilde) in ['js' 'json']
            w += kstr.weight(p.file)
            w
            
        items.sort (a b) -> weight(a) - weight(b)
        
        items.unshift(tilde:'')
            
        selectIndex = 0
        @set items 'tilde'
        @state.setView [0 0]
        
        post.emit 'redraw'
        
export fscol
