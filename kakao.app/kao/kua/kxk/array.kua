
#  ███████   ████████   ████████    ███████   ███   ███
# ███   ███  ███   ███  ███   ███  ███   ███   ███ ███ 
# █████████  ███████    ███████    █████████    █████  
# ███   ███  ███   ███  ███   ███  ███   ███     ███   
# ███   ███  ███   ███  ███   ███  ███   ███     ███   

class array

    @: ... ->
    
        for v in ...
            @insert v

    $: -> 
    
        s = "."  
        for i v in ipairs @
            s &= "\n" & "    " & $i & "    " & $v
        s

    str: -> $@
    len: -> @len
    
    @from: a -> array(unpack(a))
    @str:  a -> $array.from(a)
    
    each: -> i=0; -> i+=1; (@[i] i)
    @each: a -> i=0; -> i+=1; (a[i] i)
    
    join: c="" -> @concat c
    
    map: f ->
    
        t = @.class()
        for i v in ipairs @
            t[i] = f v
        t
        
    @map: a f ->
    
        t = []
        if a.len > 0 
            for i in 1..a.len
                t[i] = f a[i]
        elif a∙len() > 0 
            for i in 1..a∙len()
                t[i] = f a[i]
        else
            for i v in a
                t[i] = f v
        t
    
    filter: f ->
    
        t = @.class()
        for i v in ipairs @
            if f v i
                t∙push v
        t
        
    shift: -> 

        if @len > 0 ➜ @remove 1
        
    pop: -> 

        if @len > 0 ➜ @remove @len
        
    push: ... -> 

        for v in ...
            @[@len+1] = v
        @
        
    unshift: ... -> 

        l = select("#", ...)        
        if l > 0                    
            for i in l..1
                v = select(i, ...)     
                @insert 1 v
        @
        
    splice: (i n ...) ->
        
        if n > 0
            for d in 1..n
                @remove i
        a = @.class(...)
        if a.len > 0
            while a.len > 0
                @insert i a∙pop()
        @
        
    slice: first last ->

        s = @.class()
        ⮐  s if first > @len
        if last == nil ➜ last = @len
        else last = min @len last
        ⮐  s if last < 1 or last < first
        for i in first...last+1
            s∙push @[i]
        s
        
    @slice: a first last ->
    
        s = []
        if a.class and a.class != immutable
            s = a.class()

        ⮐  s if first > a∙len()
        if last == nil ➜ last = a∙len()
        else last = min @len last
        ⮐  s if last < 1 or last < first
        for i in first...last+1
            s∙push a[i]
        s
        
    keydict: key ->

        t = {}
        for i v in ipairs @
            t[v[key]] = v
        t
    
    indexdict: ->

        t = {}
        for i v in ipairs @
            t[v] = i
        t
                    
    indexof: e ->
    
        for i v in ipairs @
            if v == e ➜ ⮐  i 
        -1

    find: e ->
    
        for i v in ipairs @
            if v == e ➜ ⮐  i 
        -1
        
    rnd: -> @[math.random(@len)]
    
    sort: f ->
    
        @sort f
        @
    
    swap: i j -> 
    
        ⮐  if i < 1 or i > @len
        ⮐  if j < 1 or j > @len
        ⮐  if i == j 
        (@[i] @[j]) = (@[j] @[i])
        
    shuffle: ->
    
        for i in 1..@len
            @swap i math.random(@len)
        
    has:      e -> @indexof(e) >= 0
    contains: e -> @indexof(e) >= 0
    
    __add: a ->
    
        s = @.class.from @
        for o in a
            table.insert s o
        s

    @isarr: a ->
        
        ⮐  false if type(a) != "table"
        a.len > 0

array∙include table

⮐  array