
#  ███████   ████████   ████████    ███████   ███   ███
# ███   ███  ███   ███  ███   ███  ███   ███   ███ ███ 
# █████████  ███████    ███████    █████████    █████  
# ███   ███  ███   ███  ███   ███  ███   ███     ███   
# ███   ███  ███   ███  ███   ███  ███   ███     ███   

array = {}

array.map = tbl f ->

    t = {}
    for k,v in pairs(tbl)
        t[k] = f(v)
    t

array.filter = tbl f ->

    t = {}
    for k,v in pairs(tbl)
        if f(v k)
            table.insert t v
    t
    
array.shift   = a -> 
    ▴ type(a) == "table" 
    if a.len > 0 ➜ ⮐  table.remove a 1
    nil
    
array.pop     = a -> 
    ▴ type(a) == "table"
    if a.len > 0 ➜ ⮐  table.remove a a.len
    nil
    
array.push    = a ... -> 
    ▴ type(a) == "table" "array.push -- not a table: " & $a 
    for i v in ipairs {...}
        a[a.len+1] = v
    a
    
array.unshift = a v -> 
    ▴ type(a) == "table"
    table.insert a 1 v
    a
    
array.slice = a first last ->
    ▴ type(a) == "table"
    if last == nil ➜ last = a.len
    s = {}
    for i in first..last
        array.push s a[i]
    s
    
array.keydict = a key ->
    ▴ type(a) == "table"
    t = {}
    for i v in ipairs(a)
        t[v[key]] = v
    t

array.indexdict = a ->
    ▴ type(a) == "table"
    t = {}
    for i v in ipairs(a)
        t[v] = i
    t

array.str = a -> 

    s = ".\n"  
    for i v in pairs(a)
        s = s & "    " & i & "    " & v & "\n"
    s
    
array.isarr = a ->

    ⮐  false if type(a) != "table"
    a.len > 0
    
array.indexof = a e ->

    if array.isarr a
        for i v in ipairs a
            if v == e ➜ ⮐  i 
    -1
    
array.has      = a e -> array.indexof(a e) >= 0
array.contains = a e -> array.indexof(a e) >= 0

⮐  array