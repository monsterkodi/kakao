# █████████   ███████   ███   ███   ███████
#    ███     ███   ███  ███  ███   ███     
#    ███     ███   ███  ███████    ███████ 
#    ███     ███   ███  ███  ███        ███
#    ███      ███████   ███   ███  ███████ 

use kxk os
export kxk

enum tt*
        
    ◂text
    ◂number
    ◂comment
        
struct Tkn*

    t : tt
    s : int
    e : int
    l : int
    c : int
    
    @: ◇tt t s=-1 e=-1 l=-1 c=-1 -> @t=t ; @s=s ; @e=e ; @l=l ; @c=c
        
    $: ➜string -> "tkn(#{@t} #{@s} #{@e} #{@l} #{@c})"
        
    len: ➜int -> @e - @s

tkn* = ◇tt typ ◇int ss         ◇int line ◇int col ➜Tkn -> t = Tkn() ; t.init typ ss ss line col
tkn* = ◇tt typ ◇int ss ◇int se ◇int line ◇int col ➜Tkn -> t = Tkn() ; t.init typ ss se line col

class Tknz

    lines : seq[seq[Tkn]]
    line  : seq[Tkn]
    token : Tkn
    segi  : int
    segs  : seq[string]
    bol   : int # segi at start of current line
    idx   : int # current line index
    
    col: ➜int -> @segi - @bol
    
    $: ➜string ->
    
        "▸▸▸ #{@lines} #{@token} bol #{@bol} segi #{@segi} #{@segs} ◂◂◂"
                
    # █████████  ███   ███  ███   ███  ███████
    #    ███     ███  ███   ████  ███     ███ 
    #    ███     ███████    ███ █ ███    ███  
    #    ███     ███  ███   ███  ████   ███   
    #    ███     ███   ███  ███   ███  ███████
    
    push: ->
        if @token.len ➜ @line.add @token
        if @line.len  ➜ @lines.add @line
    
    next: ➜ bool ->
    
        ⮐  false if @segi >= @segs.len
        
        switch @segs[@segi] 
        
            "\n"
                @push()
                @line  = []
                @idx  += 1
                @bol   = @segi
                @token = tkn ◂text @segi+1 @idx 0
            " "
                if @token.len
                    @line.add @token
                    @token = tkn ◂text @segi+1 @idx @col+1
                else
                    @token.s += 1
                    @token.e += 1
                    @token.c += 1
            ➜ 
                @token.e += 1
                    
        @segi += 1
        true
    
    tknz: ◇seq[string] segs ➜seq[seq[Tkn]] ->
    
        @segs = segs
        while @next() ➜ discard
        @push()
        ⮐  @lines

toks* = ◇seq[string] segs ➜seq[seq[Tkn]] -> t = Tknz() ; t.tknz segs
toks* =     ◇string  text ➜seq[seq[Tkn]] -> toks kseg(text)
    
nums* = ◇seq[string] segs ◇seq[seq[Tkn]] tkns ➜seq[seq[Tkn]] ->

    for i1 tknl in tkns
        for i2 tkn in tknl
            p = tkn.s
            seg = o=0 ➜string  -> if p+o < tkn.e ➜ segs[p+o] ➜ ""
            chr = o=0 ➜char    -> s = seg(o) ; if s.len > 0 ➜ s[0] ➜ ' '
            adv = ◇set[char] s -> while chr() in s ➜ p += 1
            dig = -> while chr() in "0123456789" ➜ p += 1
            num = ->  
                if p == tkn.e
                    (cast[ref seq[seq[Tkn]]](unsafeAddr(tkns)))[i1][i2].t = ◂number
                
            if segs[p] == "0" 
                switch seg 1 
                    "x"
                        p += 2
                        adv {'0'..'9' 'a'..'f' 'A'..'F'}
                        num()
                        continue
                    "b"
                        p += 2
                        adv {'0' '1'}
                        num()
                        continue
                    "o"
                        p += 2
                        adv {'0'..'7' }
                        num()
                        continue
            dig()
            
            continue if p == tkn.s
                
            if seg() == "." and chr(1) in {'0'..'9'}
                p += 1
                dig()
                
            if seg() == "e" and chr(1) in {'0'..'9' '+' '-'}
                p += 2
                dig()
                
            num()
    tkns
        