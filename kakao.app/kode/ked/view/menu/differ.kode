###
    0000000    000  00000000  00000000  00000000  00000000   
    000   000  000  000       000       000       000   000  
    000   000  000  000000    000000    0000000   0000000    
    000   000  000  000       000       000       000   000  
    0000000    000  000       000       00000000  000   000  

    displays git diffs
###

use ../../../kxk    ▪ post kseg slash
use ../../../kxk    ◆ nfs
use ../../edit/tool ◆ belt
use ../../index     ◆ prjcts
use ../../util      ◆ git fileinfo
use                 ◆ searcher searcherfile

function differ extends searcher

    @: @screen ->
    
        super @screen null 'differ'
        
        post.on 'differ.status'  @status
        post.on 'differ.file'    @file
        post.on 'differ.history' @history

    # ████████  ██     ██  ███  █████████        ████████  ███  ███      ████████         ███████   ████████   ████████  ███   ███
    # ███       ███   ███  ███     ███           ███       ███  ███      ███             ███   ███  ███   ███  ███       ████  ███
    # ███████   █████████  ███     ███           ██████    ███  ███      ███████         ███   ███  ████████   ███████   ███ █ ███
    # ███       ███ █ ███  ███     ███           ███       ███  ███      ███             ███   ███  ███        ███       ███  ████
    # ████████  ███   ███  ███     ███           ███       ███  ███████  ████████         ███████   ███        ████████  ███   ███

    emitFileOpen: choice ->
    
        # log 'emit file.open'
    
        post.emit 'file.open' choice.path choice.row choice.col
        
        @hide()
        
    onFileLoaded: file =>
    
        # ⮐  if @hidden()
        # 
        # log "#{@name} onFileLoaded #{file} hide?"
    
        # @hide()
        
    #  0000000  000   000   0000000   000   000  
    # 000       000   000  000   000  000 0 000  
    # 0000000   000000000  000   000  000000000  
    #      000  000   000  000   000  000   000  
    # 0000000   000   000   0000000   00     00  
    
    show: =>
        
        @isVisible = true
        post.emit 'view.show' @name
        @arrange()
        
        @input.grabFocus()
        @choices.clearEmpty()        
        @sfils = []
        redraw:true
        
    # ███████    ███  ████████  ████████
    # ███   ███  ███  ███       ███     
    # ███   ███  ███  ██████    ██████  
    # ███   ███  ███  ███       ███     
    # ███████    ███  ███       ███     

    diff: diff =>
    
        file = diff.file
        ext  = slash.ext file
        
        items = []
            
        for change in diff.changes
        
            if empty change.add and empty change.mod
                continue # skip when only deleted
        
            modded = change.mod ? []
            added  = change.add ? []
            modadd = modded.concat added
            if empty modadd.filter((m) -> valid trim(m.new))
                log 'skip only whitespace' change
                continue
        
            items.push line:''
            
            for add,li in change.add
                if valid trim add.new
                    items.push
                        line:  ' ' + add.new
                        path:  file
                        row:   change.line+li-1
                        col:   0
                    
            for add,li in change.mod
                if valid trim add.new
                    items.push
                        line:  ' ' + add.new
                        path:  file
                        row:   change.line+li-1
                        col:   0
                    
        items.push line:'' 

        @choices.append items ext
        post.emit 'redraw'
            
    #  ███████  █████████   ███████   █████████  ███   ███   ███████
    # ███          ███     ███   ███     ███     ███   ███  ███     
    # ███████      ███     █████████     ███     ███   ███  ███████ 
    #      ███     ███     ███   ███     ███     ███   ███       ███
    # ███████      ███     ███   ███     ███      ███████   ███████ 

    status: ○=> 
    
        currentFile = ked_session.get 'editor▸file'
        status =○ git.status currentFile

        ⮐  if empty status
        ⮐  if empty status.gitDir
            
        @show()
        
        fileHeader = change file status =>

            # ⮐  if slash.ext(file) in IGNORE_FILE_EXTS

            symbol = switch change

                'changed' ➜ '●'
                'added'   ➜ '◼'
                'deleted' ➜ '✘'

            # log "differ.status.fileHeader #{symbol} #{file}"
            
            path = slash.relative file status.gitDir
                
            sfil = new searcherfile @screen "#{@name}_sfil_#{@sfils.length}"
            sfil.lineIndex = @choices.items.length#+1
            sfil.set path
            @sfils.push sfil
            
            @choices.append [
                line:   symbol
                type:  'file'
                path:  file
                row:   0
                col:   0
                ]
    
        noCounterpart = file ->
        
            cpt = 
                js:  'kode'
                pug: 'html'
                css: 'styl'
            
            if ext = cpt[slash.ext file]
                counter = slash.swapExt file ext
                ⮐  if status.files[counter]
                counter = fileinfo.swapLastDir counter slash.ext(file) ext 
                ⮐  if status.files[counter]
            true
    
        for file in status.deleted

            if true # slash.ext(file) in SOURCE_FILE_EXTS
                fileHeader 'deleted' file status

        for file in status.added

            fileHeader 'added' file status
            
            if noCounterpart(file) #slash.ext(file) in SOURCE_FILE_EXTS
            
                text =○ nfs.read file
                lines = belt.linesForText text
                newl  = lines.map (l) -> new:l
                diff  = file:file changes:[line:1 add:newl]
                @diff diff

        # for file in status.changed.filter((f) -> slash.ext(f) in SOURCE_FILE_EXTS)
        for file in status.changed
            
            fileHeader 'changed' file status
            
            if noCounterpart(file)
                
                diff =○ git.diff file
                
                if @hidden()
                    log 'hidden?'
                
                @diff diff if valid diff
            
    # ████████  ███  ███      ████████
    # ███       ███  ███      ███     
    # ██████    ███  ███      ███████ 
    # ███       ███  ███      ███     
    # ███       ███  ███████  ████████

    file: ○=>
    
        currentFile = ked_session.get 'editor▸file'
        diff =○ git.diff currentFile
                       
        if valid diff
            @show()
            @diff diff
            
    history: ○=>
    
        history =○ git.history()
        
        log 'differ.history' history
    
    #  0000000   00000000   00000000   000      000   000  
    # 000   000  000   000  000   000  000       000 000   
    # 000000000  00000000   00000000   000        00000    
    # 000   000  000        000        000         000     
    # 000   000  000        000        0000000     000     
        
    apply: choice ->
        
        ⮐  @emitFileOpen(choice) if choice?.path
        
        super choice
        
    #  ███████   ███████   ██     ██  ██     ██  ███  █████████
    # ███       ███   ███  ███   ███  ███   ███  ███     ███   
    # ███       ███   ███  █████████  █████████  ███     ███   
    # ███       ███   ███  ███ █ ███  ███ █ ███  ███     ███   
    #  ███████   ███████   ███   ███  ███   ███  ███     ███   

    commit: msg ○=>
    
        log "commit #{msg}"
        
        currentFile = ked_session.get 'editor▸file'
        gitDir =○ git.dir currentFile
        
        ⮐  if empty gitDir
        
        out = ''
        out +=○ git.exec "add ."                cwd:gitDir       
        out +=○ git.exec "commit -m \"#{msg}\"" cwd:gitDir
        out +=○ git.exec "push -q"              cwd:gitDir

        log '▸' out
                                    
    # ███  ███   ███  ████████   ███   ███  █████████       ███████    ███████  █████████  ███   ███████   ███   ███
    # ███  ████  ███  ███   ███  ███   ███     ███         ███   ███  ███          ███     ███  ███   ███  ████  ███
    # ███  ███ █ ███  ████████   ███   ███     ███         █████████  ███          ███     ███  ███   ███  ███ █ ███
    # ███  ███  ████  ███        ███   ███     ███         ███   ███  ███          ███     ███  ███   ███  ███  ████
    # ███  ███   ███  ███         ███████      ███         ███   ███   ███████     ███     ███   ███████   ███   ███

    onInputAction: action text ->
    
        switch action

            'submit' 
                if valid text
                    @commit text
                    ⮐  
            
        super action text

export differ
