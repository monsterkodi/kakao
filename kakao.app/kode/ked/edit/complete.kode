###
 0000000   0000000   00     00  00000000   000      00000000  000000000  00000000
000       000   000  000   000  000   000  000      000          000     000     
000       000   000  000000000  00000000   000      0000000      000     0000000 
000       000   000  000 0 000  000        000      000          000     000     
 0000000   0000000   000   000  000        0000000  00000000     000     00000000
###

use ../../kxk ▪ kseg kutil post
use ../theme  ◆ theme 
use ./tool    ◆ belt
use           ◆ specs

function complete

    @: @editor ->
        
        @name = @editor.name + '_complete'
        
        @choices = new choices_class @editor.cells.screen "#{@name}_choices" ['scrllr']
        @choices.focusable = false
        @choices.rounded   = false
        @choices.color.bg  = theme.editor_complete_choices
        
        @choices.scroll.handle = '▐'
        @choices.scroll.color.bg   = theme.editor_complete_choices
        @choices.scroll.color.knob = theme.editor_complete_choices_scroll
        @choices.scroll.color.dot  = theme.editor_complete_choices_scroll
        
        @choices.on 'action' @onChoicesAction
        
        @visible = false

    #  0000000   0000000   00     00  00000000   000      00000000  000000000  00000000  
    # 000       000   000  000   000  000   000  000      000          000     000       
    # 000       000   000  000000000  00000000   000      0000000      000     0000000   
    # 000       000   000  000 0 000  000        000      000          000     000       
    #  0000000   0000000   000   000  000        0000000  00000000     000     00000000  
    
    complete: ->
        
        after = @editor.state.chunkAfterCursor()
        if empty after or true
            @word @editor.state.chunkBeforeCursor() 
        
    # 000   000   0000000   00000000   0000000    
    # 000 0 000  000   000  000   000  000   000  
    # 000000000  000   000  0000000    000   000  
    # 000   000  000   000  000   000  000   000  
    # 00     00   0000000   000   000  0000000    
    
    word: @turd ->
    
        # log "complete.word #{b8 @turd}"
        
        if empty @turd
            @visible = false
            ⮐ 
                
        @words = kseg.chunks(@editor.state.s.lines).map (chunk) -> chunk.chunk        
        @words = belt.prepareWordsForCompletion @turd @words
        
        # log "@words #{y6 @words.length}" @words
        
        if inserts = specs.trigger[@turd]  # prepend special completions, eg ~O 
            @words = inserts.concat @words

        @visible = valid @words

        ⮐ if empty @words
        
        mc = @editor.state.mainCursor()
        
        head = @words[0]
        
        cx = mc[0] - @editor.state.s.view[0]     
        cy = mc[1] - @editor.state.s.view[1]
        
        for ch,ci in head[@turd.length..]
            @editor.cells.set cx+ci cy ch '#fff' theme.selection
        
        if @words.length <= 1
            @choices.clear()                    
        else
            mlw = max 1 belt.widthOfLines(@words)
            
            h = min 8 @words.length
            x  = @editor.cells.x+cx-@turd.length
            y  = @editor.cells.y+cy+1
            
            @choices.layout x y mlw+3 h
            @choices.set @words.map((w) -> ' '+w)
            @choices.selectFirst()
        
    hide:   -> @visible = false
    hidden: -> not @visible
        
    onEditorLayout: =>
    
        ⮐ if @hidden()
        
        # log "complete.onEditorLayout #{@name}"
        
    # ███   ███  ████████  ███   ███
    # ███  ███   ███        ███ ███ 
    # ███████    ███████     █████  
    # ███  ███   ███          ███   
    # ███   ███  ████████     ███   

    handleKey: key event ->
        
        ⮐ 'unhandled' if @hidden() or empty @words
        
        switch key
            'tab'
            'right'
            'return' ➜ ⮐ @apply()
            'esc'    ➜ ⮐ @hide()
            'up'
            'down' 
                if @words.length > 1
                    ⮐ @moveSelection key
        
        'unhandled'
    
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event =>
        
        ⮐ if @hidden()
        
        cret = @choices.onMouse event
        
        if not cret and event.type in ['press' 'drag' 'release']
            @hide()
            ⮐ 
        
        cret

    onWheel: event => 
        
        ⮐ if @hidden()
        
        @choices.onWheel event
        # post.emit 'redraw'
        
    #  0000000  00000000  000      00000000   0000000  000000000  
    # 000       000       000      000       000          000     
    # 0000000   0000000   000      0000000   000          000     
    #      000  000       000      000       000          000     
    # 0000000   00000000  0000000  00000000   0000000     000     
    
    moveSelection: dir ->
        
        @choices.moveSelection dir
        
    #  0000000   00000000   00000000   000      000   000  
    # 000   000  000   000  000   000  000       000 000   
    # 000000000  00000000   00000000   000        00000    
    # 000   000  000        000        000         000     
    # 000   000  000        000        0000000     000     
    
    apply: ->
    
        word = @currentWord()
        
        if specs.inserts[word]
            @editor.state.delete 'back'
            @editor.state.insert word
        else
            @editor.state.insert word[@turd.length..]
                    
        post.emit 'focus' 'editor'
        @complete()
        
    #  ███████  ███   ███   ███████   ███   ███████  ████████    ███████          ███████    ███████  █████████  ███  ███   ███
    # ███       ███   ███  ███   ███  ███  ███       ███        ███              ███   ███  ███          ███     ███  ████  ███
    # ███       █████████  ███   ███  ███  ███       ███████    ███████          █████████  ███          ███          ███ █ ███
    # ███       ███   ███  ███   ███  ███  ███       ███             ███         ███   ███  ███          ███          ███  ████
    #  ███████  ███   ███   ███████   ███   ███████  ████████   ███████          ███   ███   ███████     ███          ███   ███

    onChoicesAction: action choice=>
        
        switch action
            'click' ➜ ⮐ @apply()
            
        # log "onChoicesAction #{action} #{choice}"
        
    #  ███████  ███   ███  ████████   ████████   ████████  ███   ███  █████████      ███   ███   ███████   ████████   ███████  
    # ███       ███   ███  ███   ███  ███   ███  ███       ████  ███     ███         ███ █ ███  ███   ███  ███   ███  ███   ███
    # ███       ███   ███  ███████    ███████    ███████   ███ █ ███     ███         █████████  ███   ███  ███████    ███   ███
    # ███       ███   ███  ███   ███  ███   ███  ███       ███  ████     ███         ███   ███  ███   ███  ███   ███  ███   ███
    #  ███████   ███████   ███   ███  ███   ███  ████████  ███   ███     ███         ██     ██   ███████   ███   ███  ███████  

    currentWord: ->
        
        word = @choices.current trim:'front'
        if empty word
            word = @words[0]
        word
        
    # 0000000    00000000    0000000   000   000  
    # 000   000  000   000  000   000  000 0 000  
    # 000   000  0000000    000000000  000000000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000  00     00  
    
    draw: ->
        
        ⮐ if @hidden() or empty @words

        mc = @editor.state.mainCursor()

        cx = mc[0] - @editor.state.s.view[0]
        cy = mc[1] - @editor.state.s.view[1]
        
        word = @currentWord()
                    
        # log "complete #{word} #{@choices.current()} #{@words.length}" @words
        
        for ch,ci in word[@turd.length..] # insert the first completion at main cursor
            bg = theme[@editor.name + '_selection'] ? theme.editor_selection
            @editor.cells.set cx+ci cy ch '#fff' bg
        
        ⮐ if @words.length <= 1
        
        fx = cx-@turd.length-1
        x = fx+1+@editor.cells.x
        y = cy+@editor.cells.y+2
        w = @choices.cells.cols+1
        h = @choices.cells.rows
        
        fy = cy+1

        @editor.cells.draw_frame fx fy fx+w+1 fy+h+1 fg:theme.editor_complete_choices bg:'#000'
        
        @choices.layout x y w h
        @choices.draw()

export complete
