###
0000000    00000000  000    
000   000  000       000    
000   000  0000000   000    
000   000  000       000    
0000000    00000000  0000000
###

use ../../kxk ▪ kstr kseg
use ../util   ◆ util

export
    
    # 0000000    00000000  000      00000000  000000000  00000000  
    # 000   000  000       000      000          000     000       
    # 000   000  0000000   000      0000000      000     0000000   
    # 000   000  000       000      000          000     000       
    # 0000000    00000000  0000000  00000000     000     00000000  
    
    delete: type mods ->
                
        ⮐ @deleteSelection() if type in ['back' 'next'] and valid @s.selections
        
        lines   = @allLines()
        cursors = @allCursors()
        
        if cursors.length == 1 and type in ['back' 'next'] and util.isLinesPosOutside lines cursors[0]
            ⮐ @setMainCursor lines[cursors[0]].length cursors[0]
            
        for ci in cursors.length-1..0
            
            cursor = cursors[ci]
            
            [x y] = cursor
                            
            line = lines[y]
            
            remove = 1
            dc = 0
            
            switch type
                
                'eol'  ➜ line = line[0...x]
                
                'back' 
                    if x == 0
                        if cursors.length == 1
                            ⮐ if y <= 0
                            y -= 1
                            x = lines[y].length
                            remove = 2
                            line = kseg.join lines[y] line
                            cursor[0] = x
                            cursor[1] = y
                    else
                        if mods == 'alt'
                            rng = util.rangeOfWordOrWhitespaceLeftToPos lines cursor
                            dc = rng[2] - rng[0]
                        else
                            before = util.textFromBolToPos lines cursor
                            if util.isOnlyWhitespace before
                                dc = x % 4
                                dc = 4 if dc == 0
                            else
                                dc = 1
                        line = line[0...x-dc].concat line[x..]

                'next' 
                    if x == lines[y].length
                        if cursors.length == 1
                            ⮐ if y >= lines.length-1
                            x = lines[y].length
                            remove = 2
                            line = line.concat lines[y+1]
                            cursor[0] = x
                            cursor[1] = y
                    else
                        if mods == 'alt'
                            if rng = util.rangeOfWordOrWhitespaceRightToPos lines cursor
                                dc = rng[2] - rng[0]
                                line = line[0...x].concat line[x+dc..]
                        else
                            dc = 1
                            line = line[0...x].concat line[x+dc..]
                        cursor[0] += dc
                                
            util.moveCursorsInSameLineBy cursors cursor -dc
            
            lines.splice y remove line
            
        @clearHighlights()
        @setLines   lines
        @setCursors cursors
            
    #  0000000  00000000  000      00000000   0000000  000000000  000   0000000   000   000  
    # 000       000       000      000       000          000     000  000   000  0000  000  
    # 0000000   0000000   000      0000000   000          000     000  000   000  000 0 000  
    #      000  000       000      000       000          000     000  000   000  000  0000  
    # 0000000   00000000  0000000  00000000   0000000     000     000   0000000   000   000  
    
    deleteSelection: -> @deleteRanges @allSelections() @allCursors()
        
    deleteRanges: rngs posl ->
        
        ⮐ if empty rngs
        
        @pushState() if not @beginIndex
        
        [lines cursors] = util.deleteLineRangesAndAdjustPositions @allLines() rngs posl
        @deselect()
        @clearHighlights()
        @setLines   lines
        @setCursors cursors
