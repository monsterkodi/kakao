###
0000000    00000000    0000000   000   000
000   000  000   000  000   000  000 0 000
000   000  0000000    000000000  000000000
000   000  000   000  000   000  000   000
0000000    000   000  000   000  00     00
###

use ../../kxk    ▪ kseg kstr 
use ../util      ◆ prof 
use ../theme     ◆ color theme 
use ../view/base ◆ view
use ./tool       ◆ belt
use              ◆ mode

function draw extends view

    @: screen name features ->
        
        super screen name features
        
        @setColor 'bg'                   theme.editor
        @setColor 'empty'                theme.editor_empty
        @setColor 'cursor_main'          theme.editor_cursor_main
        @setColor 'cursor_empty'         theme.editor_cursor_empty
        @setColor 'highlight'            theme.editor_highlight
        @setColor 'highlight_ul'         theme.editor_highlight_ul
        @setColor 'highlight_bracket'    theme.editor_highlight_bracket
        @setColor 'highlight_string'     theme.editor_highlight_string
        @setColor 'highlight_bracket_ul' theme.editor_highlight_bracket_ul
        @setColor 'highlight_string_ul'  theme.editor_highlight_string_ul
        
    # 0000000    00000000    0000000   000   000  
    # 000   000  000   000  000   000  000 0 000  
    # 000   000  0000000    000000000  000000000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000  00     00  
    
    draw: =>
        
        ⮐ if @hidden()
        
        # prof.start 'draw' if @name == 'editor'
        # ● draw
                
        syntax = @state.syntax
        view   = @state.s.view
        lines  = @state.s.lines
        lines  = @complete.preDrawLines lines if @complete?
        # lines  = mode.preDrawLines @state @state.s.lines
        
        bg = @color.bg
        
        for row in 0...@cells.rows
            
            checkColor = false
            headerClass = null
            
            y = row+view[1]
            
            break if y >= lines.length
            
            line  = lines[y]
            linel = kseg.width(line) - view[0]
            
            c = 0
            firstIndex = kseg.indexAtWidth line view[0] # check if leftmost grapheme is 
            firstSegi  = kseg.segiAtWidth line view[0]  # cut in half, if yes, start at
            c = 1 if firstIndex != firstSegi            # one column to the right 
            
            x = 0
            while x < @cells.cols
                
                ci = x+view[0]
                si = kseg.indexAtWidth line ci
                
                # log "draw #{@name} #{ci} #{si} #{view} #{r3 kseg.str(line)} #{row} #{@cells.cols} #{@cells.rows} #{y} #{lines.length}"
                
                if si >= line.length
                    # log "#{@name} #{r5 'break!'}"
                    break 
                
                clss = syntax.getClass ci y
                if clss == 'invert_bg'
                    fg = bg
                    bg = @color.bg
                else
                    fg = syntax.getColor clss
                    
                ch = syntax.getChar ci y line[si]
                if ch == "#" ➜ checkColor = true
                else if ch in '0█' and clss.endsWith('header') ➜ headerClass = clss

                x += kseg.width(line[si]) ? 1
                if x < @cells.cols
                    c += @cells.add c row ch fg bg
                
                if clss == 'invert_bg'
                    bg = @color.bg
                    
            @drawRowBackground row linel
                    
            @drawColorPills  line row linel       if checkColor
            @drawAsciiHeader line row headerClass if headerClass

        @drawTrailingRows()        
        @drawHighlights()
        @drawSelections()
        
        @complete?.drawCompletion()
        @drawCursors()
        @complete?.drawPopup()
        
        @gutter?.draw()
        @mapscr?.draw()
        @scroll?.draw()
        
        # prof.end 'draw'   if @name == 'editor'
        
        mode.postDraw @state
        
        super()
        
    # 00000000    0000000   000   000       0000000     0000000    0000000  000   000   0000000   00000000   0000000    
    # 000   000  000   000  000 0 000       000   000  000   000  000       000  000   000        000   000  000   000  
    # 0000000    000   000  000000000       0000000    000000000  000       0000000    000  0000  0000000    000   000  
    # 000   000  000   000  000   000       000   000  000   000  000       000  000   000   000  000   000  000   000  
    # 000   000   0000000   00     00       0000000    000   000   0000000  000   000   0000000   000   000  0000000    
    
    drawRowBackground: row linel ->
        
        if row+view[1] == @state.mainCursor()[1]
            if linel > 0
                @cells.bg_rect 0 row linel row @color.cursor_main
            if linel < @cells.cols
                @cells.bg_fill max(0 linel) row -1 row @color.cursor_empty
        else
            if linel > 0
                @cells.bg_rect 0 row linel row @color.bg
            @cells.bg_fill max(0 linel) row -1 row @color.empty
            
    # 000000000  00000000    0000000   000  000      000  000   000   0000000   
    #    000     000   000  000   000  000  000      000  0000  000  000        
    #    000     0000000    000000000  000  000      000  000 0 000  000  0000  
    #    000     000   000  000   000  000  000      000  000  0000  000   000  
    #    000     000   000  000   000  000  0000000  000  000   000   0000000   
    
    drawTrailingRows: -> # fill empty rows below last line
        
        vl = @state.s.lines.length - @state.s.view[1]
        ⮐ if vl >= @cells.rows 

        for row in vl...@cells.rows
            @cells.bg_fill 0 row -1 row @color.empty         
                
    # 000   000  000   0000000   000   000  000      000   0000000   000   000  000000000   0000000  
    # 000   000  000  000        000   000  000      000  000        000   000     000     000       
    # 000000000  000  000  0000  000000000  000      000  000  0000  000000000     000     0000000   
    # 000   000  000  000   000  000   000  000      000  000   000  000   000     000          000  
    # 000   000  000   0000000   000   000  0000000  000   0000000   000   000     000     0000000   
    
    drawHighlights: ->
                            
        bg = @color.highlight
        ul = @color.highlight_ul
        bg = color.darken bg if not @cells.screen.t.hasFocus
        
        [vx vy] = @state.s.view
        
        for highlight in @state.s.highlights
            
            y = highlight[1]-vy
            
            break if y >= @cells.rows
                
            for x in highlight[0]...highlight[2]
                hlc = @cells.get_char x-vx y
                switch hlc
                    '{' '[' '(' ')' ']' '}' ➜ ulc = @color.highlight_bracket_ul ; bgc = @color.highlight_bracket
                    "'" '"'                 ➜ ulc = @color.highlight_string_ul  ; bgc = @color.highlight_string
                                            ➜ ulc = ul ; bgc = bg
                
                @cells.set_bg x-vx y bgc
                @cells.set_char x-vx y color.ul_rgb(ulc)+'\x1b[4:1m'+hlc+'\x1b[4:0m'
                @cells.adjustContrastForHighlight x-vx y bgc
                    
    #  0000000  00000000  000      00000000   0000000  000000000  000   0000000   000   000   0000000  
    # 000       000       000      000       000          000     000  000   000  0000  000  000       
    # 0000000   0000000   000      0000000   000          000     000  000   000  000 0 000  0000000   
    #      000  000       000      000       000          000     000  000   000  000  0000       000  
    # 0000000   00000000  0000000  00000000   0000000     000     000   0000000   000   000  0000000   
    
    drawSelections: ->
        
        # prof.start 'selection'
        
        spanbg = theme[@name + '_selection'     ] ? theme.editor_selection
        linebg = theme[@name + '_selection_line'] ? theme.editor_selection_line
        
        if not @cells.screen.t.hasFocus
            spanbg = color.darken spanbg
            linebg = color.darken linebg
        
        for selection in @state.s.selections
                
            bg = belt.isSpanLineRange(@state.s.lines selection) ? spanbg : linebg
            
            for li in selection[1]..selection[3]
                
                y = li-@state.s.view[1]
                
                break if y >= @cells.rows
                
                if li == selection[1]
                    xs = selection[0]
                else
                    xs = 0
                    
                if li == selection[3]
                    xe = selection[2]
                else
                    xe = kseg.width(@state.s.lines[li])
                
                for x in xs...xe
                    @cells.set_bg x-@state.s.view[0] y bg 
                    
        # prof.end 'selection'
            
    #  0000000  000   000  00000000    0000000   0000000   00000000    0000000  
    # 000       000   000  000   000  000       000   000  000   000  000       
    # 000       000   000  0000000    0000000   000   000  0000000    0000000   
    # 000       000   000  000   000       000  000   000  000   000       000  
    #  0000000   0000000   000   000  0000000    0000000   000   000  0000000   
    
    drawCursors: ->
        
        s = @state.s
        mainCursor = @state.mainCursor()
        
        fg = theme[@name + '_cursor_fg'] ? theme.editor_cursor_fg

        bg = mode.themeColor @state 'editor_cursor_multi'

        bg = color.darken bg if not @cells.screen.t.hasFocus
        
        for cursor in s.cursors
 
            continue if cursor eql mainCursor
            
            if @isCursorVisible cursor
             
                @cells.set_fg_bg cursor[0]-s.view[0] cursor[1]-s.view[1] fg bg
                        
        if @isCursorVisible mainCursor
             
            fg  = theme[@name + '_cursor_fg'] ? theme.editor_cursor_fg
            
            bg = mode.themeColor @state 'editor_cursor_main'
            if not @hasFocus() ➜ bg = color.darken bg 
             
            [x y] = [mainCursor[0]-s.view[0] mainCursor[1]-s.view[1]]
             
            if s.cursors.length <= 1
                if @isCursorInEmpty()
                    bg = color.darken bg 0.5
                else if ' ' == @cells.get_char(x y)
                    bg = color.darken bg 0.8
                     
            bg = color.darken bg if not @cells.screen.t.hasFocus
                 
            @cells.set_fg_bg x y fg bg

    #  0000000   0000000   000       0000000   00000000   00000000   000  000      000       0000000  
    # 000       000   000  000      000   000  000   000  000   000  000  000      000      000       
    # 000       000   000  000      000   000  0000000    00000000   000  000      000      0000000   
    # 000       000   000  000      000   000  000   000  000        000  000      000           000  
    #  0000000   0000000   0000000   0000000   000   000  000        000  0000000  0000000  0000000   
    
    drawColorPills: line row linel ->
        
        if rngs = kstr.colorRanges kseg.str(line)
            cx = max(0 linel)+1
            for rng,idx in rngs
                clr = color.rgb rng.color
                dta = 4
                if idx == 0             ➜ @cells.set cx row '' clr @color.empty; cx += 1; dta--
                if idx == rngs.length-1 ➜ dta--
                @cells.bg_rect cx row cx+dta row rng.match; cx += dta
                if idx == rngs.length-1 ➜ @cells.set cx row '' clr @color.empty
                    
    drawAsciiHeader: line row clss ->
        
        # █ █ █ █ █ █ █ █
        # █ ▉ ▊ ▋ ▌ ▍ ▏ ▕
        
        # log "▸ #{kseg.str line}"
        
        chunks = kseg.chunks line
        for chunk in chunks
            continue if chunk.segl[0] not in '0█'
            x = chunk.index-@state.s.view[0]
            ch = '▎'
            fg = theme.syntax[clss+' highlight']
            bg = theme.syntax[clss]
            @cells.set x row ch fg bg
            
            continue if chunk.segl.length <= 1
            x += chunk.segl.length - 1 
            ch = '▋' # '▊'
            fg = theme.syntax[clss]
            bg = theme.syntax[clss+' shadow']
            @cells.set x row ch fg bg

export draw
