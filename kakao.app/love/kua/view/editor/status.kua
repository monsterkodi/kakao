###
     ███████  █████████   ███████   █████████  ███   ███   ███████  
    ███          ███     ███   ███     ███     ███   ███  ███       
    ███████      ███     █████████     ███     ███   ███  ███████   
         ███     ███     ███   ███     ███     ███   ███       ███  
    ███████      ███     ███   ███     ███      ███████   ███████   

    the row above the fileeditor
    displays currently edited file and editor state information 
###

use view.base.view
use view.base.crumbs
use view.base.bubble
use view.editor.statusfile

class fileposSyntax

    @: ->
    
        @color = {}
        @color.number = theme.status.filepos
        # @color.symbol = color.darken @color.number 0.8
        @color.symbol = theme.status.filepos
        
    getColor: x y ch ->
    
        switch ch
            '' ➜ @color.symbol
                ➜ @color.number

class status extends view

    @: editor ->
    
        @editor = editor
        
        view.init @, 'status'
        
        @state    = @editor.state
        @gutter   = 4
        @file     = ''
        @drawTime = ''
        @fileposl = []
        @fileoffs = []
        
        @pointerType = 'pointer'
        
        @setColor 'gutter' theme.gutter.bg
           
        @crumbs     = crumbs     "status_crumbs"
        @statusfile = statusfile "status_file"
        @filepos    = bubble     "status_filepos"
        
        @crumbs∙setColor 'empty_left'  @color.gutter
        @crumbs∙setColor 'empty_right' theme.status.empty
        
        @filepos∙setColor 'empty' theme.status.empty
        @filepos.syntax = fileposSyntax()
        
        @crumbs∙on     'action' @onCrumbsAction  @
        @statusfile∙on 'action' @onFileAction    @
        @filepos∙on    'action' @onFileposAction @
        
        post∙on 'status.filepos' @onStatusFilepos
        
    onStatusFilepos: fileposl fileoffs -> 
    
        log "STATUS FILEPOS" fileposl
        log "STATUS FILEOFF" fileoffs
        
        @fileposl = fileposl
        @fileoffs = fileoffs
    
        if @filepos∙len() > 1
            off = if @fileoffs ➜ @fileposl∙len()-@fileoffs-1 ➜ ''
            tilde = "#{tilde}#{@fileposl∙len()-1}"
            @filepos∙set({tilde:tilde})
        else
            @filepos∙set nil 
        @
            
    onFileposAction: action item ->
    
        switch action
            'leave' ➜ log 'hide filepos files?'
            'click' ➜ post∙emit 'filepos.swapPrevious'
            'enter'
                if filepos.fileposl.length > 1
                    files = filepos.fileposl.map((fp) -> ⮐  fp[0])
                    files = files.reverse()
                    files.shift() if filepos.offset == 0
                    # post∙emit 'droop.show' files:files pos:[@filepos.cells.x+int(@filepos.cells.cols/2) @filepos.cells.y+1]
        
    onCrumbsAction: action path event ->
    
        # switch action
        # 
        #     'click'  
        #         
        #         clearTimeout @droopTimer
        #         if valid event.mods
        #             post∙emit 'dircol.root' path
        #         else
        #             post∙emit 'browse.dir' path
        #             
        #     'enter'
        #         
        #         post∙emit 'droop.hide'
        #         clearTimeout @droopTimer
        #         @droopTimer = setTimeout ((p e) -> ->  @droopCrumb(p e))(path event), 500
        #         
        #     'leave'
        #         
        #         clearTimeout @droopTimer
        #         delete @droopTimer
                
    droopCrumb: path crumb ->

        # clearTimeout @droopTimer
        # delete @droopTimer
    
        path = slash.untilde path
        files = slash.walk path {recursive:false}
        x = @crumbs.cells.x + int((crumb.cols[2]+crumb.cols[1])/2)
        post∙emit 'droop.show' {files:files pos:[x @crumbs.cells.y+1]}

    onFileAction: action file ->
    
        ⮐  if empty file
        log "onFileAction" action, file
        switch action
            'click' ➜ post∙emit 'browse.dir' slash.dir(file) file
            
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouse: event ->
        
        cret = @crumbs∙onMouse event
        sret = @statusfile∙onMouse event
        fret = @filepos∙onMouse event
        
        if sret or cret or fret
            ⮐  sret or cret or fret 

        view.onMouse @ event
        
        (col row) = unpack @eventPos(event)

        if @hover
            post∙emit 'pointer' @pointerType 
        
        switch event.type 
            
            'press' 
                
                if @hover 
                    if 1 <= col and col <= 4
                        post∙emit 'dircol.toggle'
                        ⮐  true
                    if @cells.cols-12 <= col and col <= @cells.cols
                        post∙emit 'funcol.toggle'
                        ⮐  true
        @hover
        
    #  0000000  00000000  000000000        00000000  000  000      00000000  
    # 000       000          000           000       000  000      000       
    # 0000000   0000000      000           000000    000  000      0000000   
    #      000  000          000           000       000  000      000       
    # 0000000   00000000     000           000       000  0000000  00000000  
    
    setFile: file -> 
        
        @file = file
        ⮐  if empty @file
        @crumbs∙set slash.dir(@file)
        @statusfile∙set @file
        
    # ███       ███████   ███   ███   ███████   ███   ███  █████████
    # ███      ███   ███   ███ ███   ███   ███  ███   ███     ███   
    # ███      █████████    █████    ███   ███  ███   ███     ███   
    # ███      ███   ███     ███     ███   ███  ███   ███     ███   
    # ███████  ███   ███     ███      ███████    ███████      ███   

    layout: x y w h ->
        
        view.layout @ x y w h
        
        cw = floor w/2
        
        x += @gutter+1
        
        @crumbs∙layout x y cw 1                     # 𝜏𝖍𝚒𝖘 ⟅◯⊚𝛋𝖘 ϝ𝚒𝖘𝖍𝛾 
        @crumbs∙layout x y @crumbs.rounded.length 1 # ⟒𝖍𝛾 𝜏⟒⊚ ⟅𝕒𝛾◯𝓊𝜏𝖘?
    
        x += @crumbs.rounded.length
        
        @statusfile∙layout x y @statusfile.rounded.length 1
        
        x += @statusfile.rounded.length
        
        @filepos∙layout x y @filepos.rounded.length 1
                
    # 0000000    00000000    0000000   000   000  
    # 000   000  000   000  000   000  000 0 000  
    # 000   000  0000000    000000000  000000000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000  00     00  
    
    draw: ->
        
        ⮐  if @hidden() #or empty @file
                
        @gutter ?= 4
                
        x      = 1
        y      = 1
        cursor = @state∙mainCursor()
        cols   = @cells.cols
        fnl    = @file.len
        rdo    = @state∙hasRedo()
        dty    = @state∙isDirty()
        fg     = [255 0 0]
        bg     = [55 55 55]
        
        set = x char fg bg -> 
            if fg is "string" ➜ fg = theme.status[fg]
            if bg is "string" ➜ bg = theme.status[bg]
            @cells∙set x y char fg bg
            1
            
        add = char fg bg -> x += set x char fg bg
        
        add '' 'col' @color.gutter
        colno = kstr.rpad @gutter-1 "#{cursor[1]}"
        for ci in 1...@gutter
            fg = if (cursor[1] > 1) ➜ 'fg' ➜ 'col_zero'
            if belt.isLinesPosOutside @state.s.lines cursor 
                fg = 'col_empty' 
            char = if (ci-1 < colno.len) ➜ string.sub(colno ci-1 ci-1) ➜ ' '
            add char fg 'col'
        add '' 'col' @color.gutter
              
        @crumbs∙draw()
        @statusfile∙draw()
        @filepos∙draw()
        
        x += @crumbs.rounded.len
        x += @statusfile.rounded.len
        x += @filepos.rounded.len
        
        add '' 'dark'  'empty'
        if dty ➜ add ' ' 'dirty' 'dark' 
        if dty ➜ add ' ' 'dirty' 'dark' 
        if rdo ➜ add '➜' 'redo'  'dark' 
        add ' ' 'dark'  'dark'
        
        if @state.s.cursors∙len() > 1

            cur = "#{@state.s.cursors∙len()}♦"
             
            for i in 1..cur∙len()
                color = if (i<cur∙len()) ➜ 'cur' ➜ color.darken(theme.status.cur)
                add cur[i] color 'dark'
                
        if @state.s.selections∙len()

            sel = "#{@state.s.selections∙len()}≡"
             
            for i in 1..sel∙len()
                color = if (i<sel∙len()) ➜ 'sel' ➜ color.darken(theme.status.sel) 
                add sel[i] color 'dark'

        if @state.s.highlights∙len()

            hil = "#{@state.s.highlights∙len()}❇"
             
            for i in 1..hil∙len()
                color = if (i<hil∙len()) ➜ 'hil' ➜ color.darken(theme.status.hil) 
                add hil[i] color 'dark'
                                
        # fill to the right
        
        for ci in x...cols
            add ' ' nil 'dark'
            
        add '' 'dark' @color.gutter 
        
        @render()
             
⮐  status

